
DEFINE_ACTION_FUNCTION rewrite_caelar BEGIN

/*

***** write journal entries

rewrite plot:
//- Caelar's first line w/ hooded man			-	bdcut11
	...motive? only changing the course of history, nbd
//- Caelar's speech to followers in cut scene	-	bdscry3a.bcs, bdcaelar states 88-92
//- halfling inn owner							-	bdtakos state 4
	...crusaders too lawful, attacked brigands
//- Caelar dialogue at the bridge				-	state 10 et seq.
//- caelar/heph condolence scene 				-	bdcut20a
	...talk about his rituals empowering her
//- cut scene w/ Caelar and Ashatiel 			-	bdcut30a
	...talk about... zariel?
//- remove Belben from Dragonspear basement		-	
	...repurpose him elsewhere?
//- rewrite the Heph/Bel expodump				-	replace state 1 of behepher.dlg
	...talk about slowly opening the portal
//- Add something in the Hephernaan fight 		-
	...like, have Spellstrike applied to them
	...(idea being, Irenicus helps)
	...after ~20 sec,ApplySpellRES("SPWI903")
	...and DisplayStringHead("You dare?!")
  - rewrite parlay scene						-	from bdcut37; then bddelanc.dlg
//- OR: skip it???								-	
	...but then still need to set variables
	...once you've been in the underground river,
	...set a variable, and then conditionally:
	...set bd_plot to 393 in river exterior
- cut scene dialogue before dragonspear 		-	
- ashatiel challenge dialogue					-	
	...remove "met at the parley"
  - Caelar cut-scene as Dragonspear falls		-	bdcut45; state 44
//- OR, replace this with a portal cut scene 	-	adapt from bddsgat2/bdcut45a
	...add a comment in castle courtyard:
	..."why caelar not leading troops? weird"
	...then cut scene with her in basement
	...as heph goes into Avernus
//- change line about conj/illu magic at door 	-	bdffmage state 0, bdbence state 105
	...and bence's response
//- new portal scene for charname				-	adapt from bdcut45b?
	...portal is already open
	..."quick, get delancie, seal the portal!
	..."no!" caelar seals doors behind you, goes through
//- add Skie to portal room						-	
	...make portal work like bridgefort
	...little cut scene when click
	...skie appears and follows you through
	...EDIT she just goes after the crusaders do
//- add Skie to Avernus							-	
	...just outside portal
	...dialogue if !InCombat()
	...paralyzed with fear
	...stay there until you return
//- Darnas dialogue								-	rewrite state 2, trans 2-1, & state 4
//- remove umbral accord minutes from illaruel	-	
//- amend bdcut50 line 105					 	-	bdcut50
	..."promised to restore my wife"
//- Caelar dialogue in Avernus					-	state 53 & transitions
	...explain Herphernaan's betrayal
	...still hold out hope for Zariel
//- change bdcut57								-	bdcut57
	..."know what I want/make a mockery"
//- Behlifet dialogue							-	bdbelhif.dlg states 0/1
//- remove aun argent 							-	
	...remove from .ARE
	...find all dependencies
	...advance the bd_plot variable
//- new post-bel dlg from Caelar  				-	
//- back to portal after Bel  					-	
	...portal works like bridgefort
	...Skie returns with you
	...Darnas returns with you
	...if Global("D5SkipBel","GLOBAL",1), SetGlobal("D5SkipBel","GLOBAL",2)
//- back to basement after portal  				-	
	...portal closes
	...Skie appears... dialogue?
	...Darnas appears... dialogue?
	...upon opening door, new cut scene
	...Bence & Torsin enter
	...Torsin seals the portal; do the animation
//- new Torsin dialogue after seals portal 		-	
	...he has to re-seal the portal
//- change Daeros' dialogue about the portal  	-	state 2 and...
- add fiend army to BD4400  					-	BD4500.bcs and BD4400.bcs
	...if Charname never enters basalt tower
	...and backtracks

- add a messenger to dead man's pass if bd_plot=393, saying that caelar's forces were marching out to smash the coalition before it could surround Dragonspear, the main coalition force was out in the field but a contingent of elite crusader fighters had broken off and were moving toward the camp, and Charname needs to get there ASAP!
	...and preclude travel to anywhere by the camp in such case?
	...just add travel triggers that are active if bd_plot=393?

*/


//___________________________________________________________________________________
//
// prep journal entries

// ***** write these!

ADD_JOURNAL TITLE (@20001) @20002 @20003 @20004 @20005 



//___________________________________________________________________________________
//
// amend cut scene w/ caelar and hood - bdcut11
// add new line w/ RESOLVE_STR_REF and then REPLACE_TEXTUALLY ~%eet_2%66071~ with it

OUTER_SET goal_string = RESOLVE_STR_REF (@20011)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut11.bcs~) BEGIN
  COPY_EXISTING ~bdcut11.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~%eet_2%66071~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%goal_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// change caelar's cut scene speech to followers
// set unattainable trigger for bdcaelar state 88
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5scry3a.d
ADD_STATE_TRIGGER bdcaelar 88 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~AreaCheck("bd0071")~ BEGIN d5caelar_a1
  SAY @20012
  IF ~~ GOTO d5caelar_a2
END

IF ~~ BEGIN d5caelar_a2
  SAY @20013
  IF ~~ GOTO d5caelar_a3
END

IF ~~ BEGIN d5caelar_a3
  SAY @20014
  IF ~~ GOTO d5caelar_a4
END

IF ~~ BEGIN d5caelar_a4
  SAY @20015
  IF ~~ GOTO d5caelar_a5
END

IF ~~ BEGIN d5caelar_a5
  SAY @20016
  IF ~~ 
    DO ~AddJournalEntry(%eet_2%64435,INFO) 
    	SetCutSceneLite(TRUE) 
    	StartCutSceneEx("bdscry04",FALSE)~ 
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5scry3a.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5scry3a.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5scry3a.d~ EVALUATE_BUFFER


//___________________________________________________________________________________
//
// takos' story about crusaders
// just rewrite the state

<<<<<<<< d5/d5takos.d
REPLACE_SAY BDTAKOS 4 @20020
>>>>>>>>
COPY ~d5/d5takos.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5takos.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5takos.d~


//___________________________________________________________________________________
//
// change dialogue at coast way crossing
// set unattainable trigger for bdcaelar state 10
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5coast.d
ADD_STATE_TRIGGER bdcaelar 10 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF WEIGHT #3 ~GlobalGT("bd_plot","global",160) GlobalLT("bd_plot","global",170) AreaCheck("bd1000")~ BEGIN d5caelar_b1
  SAY @20021
  IF ~~ REPLY @20022 DO ~SetGlobal("bd_plot","global",170)~ GOTO d5caelar_b2
  IF ~~ REPLY @20023 DO ~SetGlobal("bd_plot","global",170)~ GOTO d5caelar_b2
END

IF ~~ BEGIN d5caelar_b2
  SAY @20024
  IF ~~ GOTO d5caelar_b3
END

IF ~~ BEGIN d5caelar_b3
  SAY @20025
  IF ~~ REPLY @20026 GOTO d5caelar_b4
END

IF ~~ BEGIN d5caelar_b4
  SAY @20029
  IF ~~ REPLY @20030 GOTO d5caelar_b5
  IF ~~ REPLY @20031 GOTO d5caelar_b5
END

IF ~~ BEGIN d5caelar_b5
  SAY @20033
  IF ~~ GOTO d5caelar_b6
END

IF ~~ BEGIN d5caelar_b6
  SAY @20034
  IF ~~ REPLY @20035 GOTO d5caelar_b7
  IF ~~ REPLY @20036 GOTO d5caelar_b7
END

IF ~~ BEGIN d5caelar_b7
  SAY @20038
  IF ~~ REPLY @20039 GOTO d5caelar_b8
END

IF ~~ BEGIN d5caelar_b8
  SAY @20041
  IF ~~ GOTO d5caelar_b9
END

IF ~~ BEGIN d5caelar_b9
  SAY @20042
  IF ~~ REPLY @20043 GOTO d5caelar_b10
  IF ~~ REPLY @20044 GOTO d5caelar_b10
END

IF ~~ BEGIN d5caelar_b10
  SAY @20046
  IF ~~ REPLY @20047 GOTO d5caelar_b11
END

IF ~~ BEGIN d5caelar_b11
  SAY @20050
  IF ~~ GOTO 34
END

END
>>>>>>>>
COPY ~d5/d5coast.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5coast.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5coast.d~


//___________________________________________________________________________________
//
// rewrite cut scene w/ condolence letters
// write out new cut scene script
// preserve all technical aspects of the original
// mention the 'treatments' working, she feels empowered

<<<<<<<< d5/bdcut20a.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		StorePartyLocations()
		FadeToColor([0.0],0)
		SmallWait(5)
		ApplySpellRES("bdhide",Player1)
		ApplySpellRES("bdhide",Player2)
		ApplySpellRES("bdhide",Player3)
		ApplySpellRES("bdhide",Player4)
		ApplySpellRES("bdhide",Player5)
		ApplySpellRES("bdhide",Player6)
		ActionOverride(Player2,LeaveAreaLUA("bd4100","",[2120.1120],S))
		ActionOverride(Player3,LeaveAreaLUA("bd4100","",[2055.1145],S))
		ActionOverride(Player4,LeaveAreaLUA("bd4100","",[2085.1145],S))
		ActionOverride(Player5,LeaveAreaLUA("bd4100","",[2020.1170],S))
		ActionOverride(Player6,LeaveAreaLUA("bd4100","",[2055.1170],S))
		LeaveAreaLUAPanic("bd4100","",[2085.1120],S)
		LeaveAreaLUA("bd4100","",[2085.1120],S)
		MultiPlayerSync()
		MoveViewPoint([2000.1090],INSTANT)
		CreateCreature("BDSERV",[1960.1110],NW) 
		CreateCreature("BDCAELAR",[2070.1060],NE)
		SmallWait(1)
		Explore()
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",20)
		SetCutSceneBreakable(TRUE)
		SmallWait(10)
		FadeFromColor([20.0],0)
		SmallWait(10)
		DisplayStringWait("BDCAELAR",%eet_2%66627)
		SmallWait(15)
		CreateCreature("BDHEPH2",[1735.835],SW)
		SmallWait(20)
		ActionOverride("BDHEPH2",MoveToPoint([1865.1060]))
		SmallWait(45)
		ActionOverride("BDSERV",FaceObject("BDHEPH2"))
		ActionOverride("BDCAELAR",FaceObject("BDHEPH2"))
		DisplayStringWait("BDHEPH2",@20061)
		SmallWait(5)
		ActionOverride("BDHEPH2",Face(E))
		DisplayStringWait("BDCAELAR",@20062)
		Wait(1)
		DisplayStringWait("BDHEPH2",@20063)
		ActionOverride("BDSERV",FaceObject("BDCAELAR"))
		SmallWait(10)
		DisplayStringWait("BDCAELAR",@20064)
		SmallWait(10)
		DisplayStringWait("BDHEPH2",@20065)
		ActionOverride("BDSERV",FaceObject("BDCAELAR"))
		SmallWait(10)
		DisplayStringWait("BDCAELAR",@20066)
		PlaySound("cas_p07n")
		SmallWait(2)
		ActionOverride("BDCAELAR",SetSequence(SEQ_CONJURE))
		Wait(1)
		ActionOverride("BDCAELAR",SetSequence(SEQ_CAST))
		SmallWait(5)
		CreateVisualEffectObject("ICPRAYI","BDCAELAR")
		ApplySpellRES("bdcpulse","BDCAELAR")
		CreateVisualEffectObject("ICPRAYI","BDHEPH2")
		ApplySpellRES("bdcpulse","BDHEPH2")
		CreateVisualEffectObject("ICPRAYI","BDSERV")
		ApplySpellRES("bdcpulse","BDSERV")
		Wait(1)
		DisplayStringWait("BDCAELAR",%eet_2%66633)  // Gather the faithful. We have much to do.
		SmallWait(10)
		FadeToColor([20.0],0)
		SmallWait(10)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		UndoExplore()
		ActionOverride("BDCAELAR",DestroySelf())
		ActionOverride("BDHEPH2",DestroySelf())
		ActionOverride("BDSERV",DestroySelf())
		SmallWait(5)
		SmallWait(10)
		RestorePartyLocations()
		MultiPlayerSync()
		ApplySpellRES("bdunhide",Player1)
		ApplySpellRES("bdunhide",Player2)
		ApplySpellRES("bdunhide",Player3)
		ApplySpellRES("bdunhide",Player4)
		ApplySpellRES("bdunhide",Player5)
		ApplySpellRES("bdunhide",Player6)
		SmallWait(7)
		SetGlobalTimer("bd_bence_delay","bd7100",ONE_ROUND)
		FadeFromColor([20.0],0)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/bdcut20a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut20a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut20a.baf~ EVALUATE_BUFFER 


//___________________________________________________________________________________
//
// rewrite cut scene w/ caelar and ashatiel
// write out new cut scene script
// preserve all technical aspects of the original

OUTER_SET ca_string_1 = RESOLVE_STR_REF (@20071)
OUTER_SET ca_string_2 = RESOLVE_STR_REF (@20072)
OUTER_SET ca_string_3 = RESOLVE_STR_REF (@20073)
OUTER_SET ca_string_4 = RESOLVE_STR_REF (@20074)
OUTER_SET ca_string_5 = RESOLVE_STR_REF (@20075)
OUTER_SET ca_string_6 = RESOLVE_STR_REF (@20076)
OUTER_SET ca_string_7 = RESOLVE_STR_REF (@20077)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut30a.bcs~) BEGIN
  COPY_EXISTING ~bdcut30a.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~%eet_2%66080~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_1%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66079~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_1%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66081~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_2%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66082~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_3%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66083~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_4%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66084~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_5%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66085~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_6%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66086~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_7%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
// less moving around
		SPRINT textToReplace ~\(3626.1127\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~3576.1236~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(3679.1169\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~3577.1237~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// remove Belben from DS basement

<<<<<<<< d5/d5belbo.baf
IF
	GlobalLT("chapter","global",11)
	AreaCheck("bd4300")  // Dragonspear Castle Basement
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>
COPY ~d5/d5belbo.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belbo.baf~
EXTEND_TOP ~bdbelben.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belbo.baf~

<<<<<<<< d5/d54300_1.baf
IF
	GlobalLT("bd_plot","global",350)
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",350)
		Continue()
END
>>>>>>>>
COPY ~d5/d54300_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_1.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_1.baf~


//___________________________________________________________________________________
//
// change dialogue between hephernaan and belhifet's voice
// set unattainable trigger for bdhepher state 1
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5hefbel.d
ADD_STATE_TRIGGER BDHEPHER 1 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER BDHEPHER 8 ~GlobalGT("D5Intercession","BD4300",0)~
ADD_STATE_TRIGGER BDHEPHER 8 ~GlobalGT("D5Intercession","BD4300",0)~

APPEND BDHEPHER

IF WEIGHT #-1 ~AreaCheck("BD4300") GlobalLT("bd_plot","GLOBAL",360)~ BEGIN d5heph_b1
  SAY @20081
  IF ~~ GOTO d5heph_b2
END

IF ~~ BEGIN d5heph_b2
  SAY @20082
  IF ~~ EXTERN BDVOICE d5bel_b1
END

IF ~~ BEGIN d5heph_b3
  SAY @20083
  IF ~~ EXTERN BDVOICE d5bel_b2
END

IF ~~ BEGIN d5heph_b4
  SAY @20084
  IF ~~ EXTERN BDVOICE d5bel_b4
END

IF ~~ BEGIN d5heph_b5
  SAY @20085
  IF ~~ GOTO d5heph_b6
END

IF ~~ BEGIN d5heph_b6
  SAY @20086
  IF ~~ REPLY @20087 GOTO d5heph_b7
  IF ~~ REPLY @20088 GOTO d5heph_b8
END

IF ~~ BEGIN d5heph_b7
  SAY @20089
  IF ~~ GOTO d5heph_b9
END

IF ~~ BEGIN d5heph_b8
  SAY @20090
  IF ~~ GOTO d5heph_b9
END

IF ~~ BEGIN d5heph_b9
  SAY @20091
  IF ~~ DO ~SetGlobal("D5Intercession","BD4300",1)
  			ActionOverride("bdhepher",Enemy())
			ActionOverride("bdesseri",Enemy())
			ActionOverride("bdolvena",Enemy())
			ActionOverride("cutspy",DestroySelf())
			ActionOverride("bdhepher",ForceSpell(Myself,"WIZARD_ORACLE"))
			AddJournalEntry(%eet_2%59795,QUEST)~ 
  		EXIT
END

END

APPEND BDVOICE

IF ~~ BEGIN d5bel_b1
  SAY @20092
  IF ~~ EXTERN BDHEPHER d5heph_b3
END

IF ~~ BEGIN d5bel_b2
  SAY @20093
  IF ~~ GOTO d5bel_b3
END

IF ~~ BEGIN d5bel_b3
  SAY @20094
  IF ~~ EXTERN BDHEPHER d5heph_b4
END

IF ~~ BEGIN d5bel_b4
  SAY @20095
  IF ~~ EXTERN BDHEPHER d5heph_b5
END

END
>>>>>>>>
COPY ~d5/d5hefbel.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5hefbel.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5hefbel.d~ EVALUATE_BUFFER

<<<<<<<< d5/d54300_2.baf
IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdolvena")
THEN
	RESPONSE #100
		ApplySpell("bdolvena","WIZARD_SPELL_STRIKE")
		Continue()
END

IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdesseri")
THEN
	RESPONSE #100
		Wait(1)
		ApplySpell("bdesseri","WIZARD_SPELL_STRIKE")
		Continue()
END

IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdhepher")
THEN
	RESPONSE #100
		Wait(2)
		ApplySpell("bdhepher","WIZARD_SPELL_STRIKE")
		SetGlobal("D5Intercession","BD4300",3)
		DisplayStringHead("bdhepher",@20099) 
		Continue()
END

IF
	Global("D5Intercession","BD4300",1)
THEN
	RESPONSE #100
		SetGlobal("D5Intercession","BD4300",2)
		SetGlobalTimer("D5IntercessionTimer","BD5300",21)
		Continue()
END
>>>>>>>>
COPY ~d5/d54300_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_2.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_2.baf~


//___________________________________________________________________________________
//
// adjustments to Daeros' expository dialogue

<<<<<<<< d5/d5daero+.d
REPLACE_SAY BDDAEROS 2 @20101

REPLACE_SAY BDDAEROS 3 @20102

ADD_TRANS_TRIGGER BDDAEROS 2 ~FALSE()~ DO 1

ADD_TRANS_TRIGGER BDDAEROS 3 ~FALSE()~ DO 0
>>>>>>>>
COPY ~d5/d5daero+.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5daero+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5daero+.d~


//___________________________________________________________________________________
//
// remove parlay scene
// once you've been in the underground river, set a variable
// then conditionally set bd_plot to 393 in river exterior
/*
 - ***** add a messenger to dead man's pass if bd_plot=393, saying that caelar's forces were marching out to smash the coalition before it could surround Dragonspear, the main coalition force was out in the field but a contingent of elite crusader fighters had broken off and were moving toward the camp, and Charname needs to get there ASAP!
	...and preclude travel to anywhere but the camp in such case?
	...just add travel triggers that are active if bd_plot=393?
*/
<<<<<<<< d5/d5bd5100.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",1)
		Continue()
END
>>>>>>>>
COPY ~d5/d5bd5100.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5100.baf~
EXTEND_TOP ~bd5100.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5100.baf~

COPY_EXISTING ~BD5000.are~ ~override~
 /* add travel region that links to dead man's pass */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2 	//travel
    fj_box_left     = 1
    fj_box_top      = 1024
    fj_box_right    = 30
    fj_box_bottom   = 3839
    fj_cursor_idx   = 30
    fj_flags        = 4
    fj_vertex_0     = 1 + (1024 << 16) 
    fj_vertex_1     = 30 + (1024 << 16)
    fj_vertex_2     = 30 + (3839 << 16)
    fj_vertex_3     = 1 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300a
    fj_destination_area = BD7300
    fj_destination_name = d5bd5000
  END

  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2
    fj_box_left     = 31
    fj_box_top      = 3809
    fj_box_right    = 5188
    fj_box_bottom   = 3839
    fj_cursor_idx   = 30
    fj_flags        = 4
    fj_vertex_0     = 31 + (3809 << 16) 
    fj_vertex_1     = 5188 + (3809 << 16)
    fj_vertex_2     = 5188 + (3839 << 16)
    fj_vertex_3     = 31 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300b
    fj_destination_area = BD7300
    fj_destination_name = d5bd5000
  END

  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2
    fj_box_left     = 5089
    fj_box_top      = 1105
    fj_box_right    = 5119
    fj_box_bottom   = 3839
    fj_cursor_idx   = 30
    fj_flags        = 4
    fj_vertex_0     = 5089 + (1105 << 16) 
    fj_vertex_1     = 5119 + (1105 << 16)
    fj_vertex_2     = 5119 + (3839 << 16)
    fj_vertex_3     = 5089 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300c
    fj_destination_area = BD7300
    fj_destination_name = d5bd5000
  END
BUT_ONLY

COPY_EXISTING ~BD7300.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 4900
    fj_loc_y       = 600
    fj_orientation = 4 	// W f
    STR_VAR
    fj_structure_type = entrance
    fj_name           = d5bd5000
  END
BUT_ONLY 

<<<<<<<< d5/d5bd5000.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",1)
	Global("D5BD5000toBD7300","BD5000",0)
THEN
	RESPONSE #100
		SetGlobal("D5BD5000toBD7300","BD5000",1)
		TriggerActivation("bd5000_bd7300b",TRUE)
		TriggerActivation("bd5000_bd7300b",TRUE)
		TriggerActivation("bd5000_bd7300c",TRUE)
END

IF
	Global("D5TakeMeToTheRiver","GLOBAL",2)
	Global("D5BD5000toBD7300","BD5000",1)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",3)
		TriggerActivation("bd5000_bd7300b",FALSE)
		TriggerActivation("bd5000_bd7300b",FALSE)
		TriggerActivation("bd5000_bd7300c",FALSE)
END
>>>>>>>> 
COPY ~d5/d5bd5000.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5000.baf~
EXTEND_TOP ~bd5000.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5000.baf~


<<<<<<<< d5/d5bd7300.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",1)
	GlobalLT("bd_plot","GLOBAL",393)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",2)
		SetGlobal("bd_plot","GLOBAL",393)
		Continue()
END
>>>>>>>>
COPY ~d5/d5bd7300.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd7300.baf~
EXTEND_TOP ~bd7300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd7300.baf~


//___________________________________________________________________________________
//
// new portal cut scene w/ just Heph & Caelar & Crusaders
// set unattainable trigger for bdcaelar state 44
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/bddsgat2.baf
IF
	Clicked([ANYONE])
	!Range(LastTrigger,12)
THEN
	RESPONSE #100
		DisplayString(Myself,%eet_2%38224)  // You are too far away to use that.
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,12)
	Global("bd_inner_gate_ot","bd4000",0)
THEN
	RESPONSE #100
		SetGlobal("bd_inner_gate_ot","bd4000",1)
		DisplayString(Myself,%eet_2%40901)
END

IF
	GlobalLT("bd_plot","global",450)
	GlobalTimerExpired("bd_caelar_delay","bd4000")
	Dead("bdashati")
	TriggerOverride("gate_proxy",Range([PC],23))
	!TriggerOverride("bddelanc",Detect([PC])) 
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("bd_plot","global",450)
		StartCutSceneMode()
		FadeToColor([20.0],0)
		StartCutSceneEx("d5port1",TRUE)
END

IF
	GlobalLT("bd_plot","global",450)
	GlobalTimerExpired("bd_caelar_delay","bd4000")
	Dead("bdashati")
	TriggerOverride("gate_proxy",Range([PC],23))
	TriggerOverride("bddelanc",Detect([PC])) 
THEN
	RESPONSE #100
		ActionOverride("bddelanc",StartDialogueNoSet(Player1))
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,12)
THEN
	RESPONSE #100
		DisplayString(Myself,%eet_2%40901)  // These sturdy gates are very likely to hold off any assault conducted without the use of heavy siege machines. Or a good deal of explosive material.
END
>>>>>>>>
COPY ~d5/bddsgat2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bddsgat2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bddsgat2.baf~ EVALUATE_BUFFER

<<<<<<<< d5/d5port1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ClearAllActions()
		StorePartyLocations()
		SmallWait(5)
		ApplySpellRES("bdhide",Player1)
		ApplySpellRES("bdhide",Player2)
		ApplySpellRES("bdhide",Player3)
		ApplySpellRES("bdhide",Player4)
		ApplySpellRES("bdhide",Player5)
		ApplySpellRES("bdhide",Player6)
		ActionOverride(Player2,LeaveAreaLUA("bd4300","",[815.545],S))
		ActionOverride(Player3,LeaveAreaLUA("bd4300","",[845.515],S))
		ActionOverride(Player4,LeaveAreaLUA("bd4300","",[845.545],S))
		ActionOverride(Player5,LeaveAreaLUA("bd4300","",[875.545],S))
		ActionOverride(Player6,LeaveAreaLUA("bd4300","",[845.575],S))
		LeaveAreaLUAPanic("bd4300","",[815.515],S)
		LeaveAreaLUA("bd4300","",[815.515],S)
		MultiPlayerSync()
		MoveViewPoint([615.420],INSTANT)
		CreateCreature("bdcaelar",[520.400],SE)
		CreateCreature("bdhepher",[720.470],S)
		SmallWait(5)
		CreateCreature("bdcrue50",[440.520],NE)  // Crusader Elite
		CreateCreature("bdcrue53",[470.295],SE)  // Crusader Elite
		CreateCreature("bdlieutg",[745.300],SW)  // Crusader Elite
		CreateCreature("bdlieutc",[340.395],E)  // Crusader Elite
		CreateCreature("bdlieute",[505.220],S)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdlieuta",[500.570],NE)  // Caelar's Lieutenant
		CreateCreature("bdcrue52",[425.340],SE)  // Crusader Elite
		CreateCreature("bdcrue55",[645.270],S)  // Crusader Elite
		CreateCreature("bdlieutd",[390.280],SE)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdcrue51",[410.435],E)  // Crusader Elite
		CreateCreature("bdcrue54",[550.255],S)  // Crusader Elite
		CreateCreature("bdlieutb",[375.520],NE)  // Caelar's Lieutenant
		CreateCreature("bdlieutf",[645.230],S)  // Crusader Elite
		SmallWait(1)
		Explore()
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SmallWait(10)
		FadeFromColor([20.0],0)
		SmallWait(10)
		ActionOverride("bdcaelar",SetSequence(SEQ_READY))
		DisplayStringWait("BDCAELAR",@20110)
		SmallWait(45)
		ActionOverride("bdhepher",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5port1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5port1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5port1.baf~

<<<<<<<< d5/d5port2.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetAreaScript("cutskip",OVERRIDE)
		ActionOverride("bdhepher",ForceSpellRES("bdhepher",Myself))
		ActionOverride("bdhepher",MoveToPoint([590.400]))
		SmallWait(23)
		ReallyForceSpellRES("bdglowgr","bdlieuta")
		ReallyForceSpellRES("bdglowgr","bdcrue50")
		ReallyForceSpellRES("bdglowgr","bdcrue51")
		ReallyForceSpellRES("bdglowgr","bdcrue52")
		ReallyForceSpellRES("bdglowgr","bdcrue53")
		ReallyForceSpellRES("bdglowgr","bdcrue54")
		ReallyForceSpellRES("bdglowgr","bdcrue55")
		ReallyForceSpellRES("bdglowgr","bdlieutg")
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		SmallWait(3)
		ReallyForceSpellRES("bdglowgr","bdlieutb")
		ReallyForceSpellRES("bdglowgr","bdlieutc")
		ReallyForceSpellRES("bdglowgr","bdlieutd")
		ReallyForceSpellRES("bdglowgr","bdlieute")
		ReallyForceSpellRES("bdglowgr","bdlieutf")
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		ActionOverride("bdhepher",Face(W))
		SmallWait(25)
		ActionOverride("bdhepher",SetSequence(SEQ_CAST))
		SmallWait(5)
		CreateVisualEffectObject("spfleshs","bdhepher")
		SmallWait(25)
		CreateVisualEffect("bdwardgr",[550.360])
		PlaySound("EFF_M08")
		SmallWait(10)
		PlaySound("EFF_P13")
		SmallWait(2)
		CreateVisualEffect("spcallli",[615.415])
		ScreenShake([15.15],10)
		PlaySound("EFF_M13")
		SmallWait(10)
		ActionOverride("bdcaelar",SetSequence(SEQ_DAMAGE))
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(SEQ_DIE))
		DisplayStringWait("bdhepher",@20111)
		SmallWait(35)
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		CreateVisualEffect("spfleshs",[615.415])
		SmallWait(5)
		AmbientActivate("Portal_Activate",TRUE)
		SmallWait(5)
		CreateVisualEffect("spfirepi",[615.415])
		SmallWait(5)
		AmbientActivate("glyph1",TRUE)
		AmbientActivate("glyph2",TRUE)
		AmbientActivate("glyph3",TRUE)
		AmbientActivate("glyph4",TRUE)
		AmbientActivate("glyph5",TRUE)
		AmbientActivate("glyph6",TRUE)
		AmbientActivate("glyph7",TRUE)
		AmbientActivate("glyph8",TRUE)
		AmbientActivate("glyph9",TRUE)
		AmbientActivate("glyph10",TRUE)
		AmbientActivate("glyph11",TRUE)
		AmbientActivate("glyph12",TRUE)
		AmbientActivate("glyph13",TRUE)
		AmbientActivate("glyph14",TRUE)
		AmbientActivate("glyph15",TRUE)
		AmbientActivate("glyph16",TRUE)
		SmallWait(10)
		OpenDoor("Door04")
		AmbientActivate("portal_visuals",TRUE)
		AmbientActivate("portal_webm",TRUE)
		AmbientActivate("Portal_Activate",FALSE)
		SmallWait(5)
		AmbientActivate("glyph1",FALSE)
		AmbientActivate("glyph2",FALSE)
		AmbientActivate("glyph3",FALSE)
		AmbientActivate("glyph4",FALSE)
		AmbientActivate("glyph5",FALSE)
		AmbientActivate("glyph6",FALSE)
		AmbientActivate("glyph7",FALSE)
		AmbientActivate("glyph8",FALSE)
		AmbientActivate("glyph9",FALSE)
		AmbientActivate("glyph10",FALSE)
		AmbientActivate("glyph11",FALSE)
		AmbientActivate("glyph12",FALSE)
		AmbientActivate("glyph13",FALSE)
		AmbientActivate("glyph14",FALSE)
		AmbientActivate("glyph15",FALSE)
		AmbientActivate("glyph16",FALSE)
		SmallWait(5)
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		ActionOverride("bdhepher",MoveToPoint([630.430]))
		SmallWait(6)
		CreateVisualEffectObject("shair","bdhepher")
		ActionOverride("bdhepher",DestroySelf())
		FadeToColor([20.0],0)
		SmallWait(10)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SmallWait(5)
		UndoExplore()
		SmallWait(10)
		RestorePartyLocations()
		MultiPlayerSync()
		ApplySpellRES("bdunhide",Player1)
		ApplySpellRES("bdunhide",Player2)
		ApplySpellRES("bdunhide",Player3)
		ApplySpellRES("bdunhide",Player4)
		ApplySpellRES("bdunhide",Player5)
		ApplySpellRES("bdunhide",Player6)
		SetGlobal("D5BackToGate","BD4000",1)
		FadeFromColor([20.0],0)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5port2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5port2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5port2.baf~

<<<<<<<< d5/d5bd4000.baf
IF
	Global("D5BackToGate","BD4000",1)
THEN
	RESPONSE #100
		SetGlobal("D5BackToGate","BD4000",2)
		MoveViewPoint([2680.1885],INSTANT)
		CreateCreature("bdcutid",[2795.1790],S)
		CreateCreature("bdphosse",[2380.2050],NE)
		ClearAllActions()
		StartCutSceneMode()
		StartCutSceneEx("bdcut43",TRUE)
END
>>>>>>>>
COPY ~d5/d5bd4000.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd4000.baf~
EXTEND_TOP ~bd4000.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd4000.baf~

<<<<<<<< d5/bdcut43.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")
		SetGlobal("bd_cant_rest","myarea",0)
		SmallWait(5)
		CreateCreature("cutspy",[2680.1885],S)
		ActionOverride("bdphosse",MoveToPoint([2760.1845]))
		SmallWait(30)
END

IF
	GlobalGT("bd_wall_explosion","bd4000",0)
THEN
	RESPONSE #100
		CutSceneId("bdcutid")
		DisplayStringHead("bdphosse",%eet_2%38366)  // This one'll be a touch easierâ€”fewer arrows flying at my bloody head!
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")
		SmallWait(35)
		ActionOverride("bdphosse",Face(NE))
		ActionOverride("bdphosse",SetSequence(SEQ_CONJURE))
		SmallWait(45)
		ActionOverride("bdphosse",SetSequence(SEQ_READY))
		SmallWait(10)
		ActionOverride("bdphosse",MoveToPoint([2525.2005]))
		SmallWait(35)
		ActionOverride("bdphosse",Face(NE))
		SmallWait(20)
		ScreenShake([100.100],10)
		CreateVisualEffect("bdFLAST1",[2750.1785])
		CreateVisualEffect("SPFLAMES",[2765.1830])
		CreateVisualEffect("bdFLAST1",[2825.1825])
		CreateVisualEffect("SPFLAMES",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		PlaySound("EFF_M36B")
		SmallWait(3)
		ActionOverride("dsc_gate_inner",ReallyForceSpellPoint([2795.1800],WIZARD_FIREBALL))
		SmallWait(2)
		CreateVisualEffect("SPFLAMES",[2750.1785])
		CreateVisualEffect("bdFLAST1",[2765.1830])
		CreateVisualEffect("SPFLAMES",[2825.1825])
		CreateVisualEffect("bdFLAST1",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		CreateVisualEffect("SPFLAST2",[3155.3430])
		PlaySound("EFF_M36B")
		SmallWait(3)
		ActionOverride("dsc_gate_inner",ReallyForceSpellPoint([2795.1800],WIZARD_FIREBALL))
		Unlock("door01")
		SmallWait(2)
		OpenDoor("door01")
		ScreenShake([100.100],10)
		CreateVisualEffect("bdFLAST1",[2750.1785])
		CreateVisualEffect("SPFLAMES",[2765.1830])
		CreateVisualEffect("bdFLAST1",[2825.1825])
		CreateVisualEffect("SPFLAMES",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		PlaySound("EFF_M36B")
		SmallWait(5)
		CreateVisualEffect("SPFLAMES",[2750.1785])
		CreateVisualEffect("bdFLAST1",[2765.1830])
		CreateVisualEffect("SPFLAMES",[2825.1825])
		CreateVisualEffect("bdFLAST1",[2810.1765])
		SmallWait(10)
		TriggerActivation("dsc_gate_inner",FALSE)
		DisplayStringHead("bdphosse",%eet_2%38367)  // All right boys and girls, have at 'em. I'll be back at the camp drinking myself into a stupor. *hic* Haha!
		SmallWait(50)
		ActionOverride("bdphosse",MoveToObject("courtyard"))
		SmallWait(30)
		ActionOverride("bddelanc",SetGlobal("bd_delancie_moves","locals",2))
		ActionOverride("bdbence",MoveToObject("inner_courtyard"))
		ActionOverride("bdnederl",MoveToObject("inner_courtyard"))
		ActionOverride("bddelanc",MoveToPoint([3030.1465]))
		ActionOverride("bdsorali",MoveToPoint([2925.1430]))
		ActionOverride("bdwarma1",MoveToPoint([2965.1395]))
		ActionOverride("bdwarma2",MoveToPoint([3035.1380]))
		ActionOverride("bdwarma3",MoveToPoint([3095.1400]))
		ActionOverride("bddosia",MoveToPoint([3130.1445]))
		ActionOverride("bdhensle",MoveToPoint([3140.1490]))
		ActionOverride("bdstoneh",MoveToPoint([4300.975]))
		SmallWait(20)
		FadeToColor([20.0],0)
		SmallWait(10)
		ActionOverride("bdphosse",DestroySelf())
		SmallWait(15)
		ActionOverride("cutspy",DestroySelf())
		ActionOverride("cutspy2",DestroySelf())
		ActionOverride("bdbence",DestroySelf())
		ActionOverride("bdnederl",DestroySelf())
		ActionOverride("bddelanc",JumpToPoint([3030.1465]))
		ActionOverride("bddelanc",Face(S))
		ActionOverride("bddelanc",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
		CreateCreature("bddagoff",[4325.1025],W)  // Daggerford Officer
		CreateCreature("bdwtrx37",[1850.2285],NW)  // Waterdhavian Sergeant
		CreateCreature("bdwtr39",[950.2550],NE)  // Waterdhavian Soldier
		CreateCreature("bdwtr37",[1590.2885],E)  // Waterdhavian Soldier
		CreateCreature("bdfist35",[2005.1685],NW)  // Flaming Fist Mercenary
		CreateCreature("bdwtr35",[2405.1865],S)  // Waterdhavian Soldier
		CreateCreature("bdwtr38",[2440.2260],NW)  // Waterdhavian Soldier
		CreateCreature("bdwtr35",[2805.1170],NW)  // Waterdhavian Soldier
		CreateCreature("bdwtr36",[2965.1120],N)  // Waterdhavian Soldier
		CreateCreature("bdwtrx36",[2690.757],SE)  // Waterdhavian Sergeant
		CreateCreature("bddagf37",[2790.735],S)  // Daggerford Militia
		CreateCreature("bdfist35",[2980.675],NE)  // Flaming Fist Mercenary
		CreateCreature("bdwtr38",[3015.735],NE)  // Waterdhavian Soldier
		CreateCreature("bdfist36",[3380.1730],SW)  // Flaming Fist Mercenary
		CreateCreature("bdwtr37",[3435.1755],S)  // Waterdhavian Soldier
		CreateCreature("bddagf38",[3810.2070],NW)  // Daggerford Militia
		CreateCreature("bdfisx35",[3860.2040],N)  // Flaming Fist Sergeant
		CreateCreature("bdfist38",[3905.2055],NE)  // Flaming Fist Mercenary
		CreateCreature("bdwtr35",[4405.1875],SE)  // Waterdhavian Soldier
		CreateCreature("bddagf35",[4405.1945],NE)  // Daggerford Militia
		CreateCreature("bddagf36",[3910.1660],SW)  // Daggerford Militia
		CreateCreature("bddagf37",[3555.1155],SW)  // Daggerford Militia
		CreateCreature("bddagf38",[4005.905],NE)  // Daggerford Militia
		CreateCreature("bddagf35",[4375.1035],SW)  // Daggerford Militia
		CreateCreature("bddagf36",[4840.865],SE)  // Daggerford Militia
		CreateCreature("bdfist35",[4405.700],N)  // Flaming Fist Mercenary
		CreateCreature("bddagf37",[4455.725],NE)  // Daggerford Militia
		CreateCreature("bdfist38",[4565.630],NW)  // Flaming Fist Mercenary
		CreateCreature("bdfist35",[3735.1925],SE)  // Flaming Fist Mercenary
		ActionOverride("bdfist35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bddagf35",[3640.1925],SE)  // Daggerford Militia
		ActionOverride("bddagf35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfist36",[2965.1695],E)  // Flaming Fist Mercenary
		ActionOverride("bdfist36",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfisx35",[2120.2235],NE)  // Flaming Fist Sergeant
		ActionOverride("bdfisx35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdwtr35",[2040.2210],NE)  // Waterdhavian Soldier
		ActionOverride("bdwtr35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfist37",[1190.2815],NE)  // Flaming Fist Mercenary
		ActionOverride("bdfist37",SetGlobal("bd_move_into_keep","locals",1))
END

IF
	!Dead("bdwarma1")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma1",JumpToPoint([2965.1395]))
		ActionOverride("bdwarma1",Face(SE))
END

IF
	!Dead("bdwarma2")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma2",JumpToPoint([3035.1380]))
		ActionOverride("bdwarma2",Face(S))
END

IF
	!Dead("bdwarma3")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma3",JumpToPoint([3095.1400]))
		ActionOverride("bdwarma3",Face(SW))
END

IF
	!Dead("bdhensle")  // Dahk Hensleigh
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdhensle",JumpToPoint([3130.1445]))
		ActionOverride("bdhensle",Face(W))
END

IF
	!Dead("bdsorali")  // Soralis
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdsorali",JumpToPoint([3140.1490]))
		ActionOverride("bdsorali",Face(W))
END

IF
	!Dead("bdstoneh")  // General Stonehand
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdstoneh",JumpToPoint([4300.975]))
		ActionOverride("bdstoneh",Face(SW))
		ActionOverride("bdstoneh",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
END

IF
	!Dead("bddosia")  // Dosia
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bddosia",JumpToPoint([4360.820]))
		ActionOverride("bddosia",Face(SW))
		AddMapNoteColor([4360.820],%eet_2%67718,SALMON)  // Theodosia Immartyred
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		SmallWait(20)
		FadeFromColor([20.0],0)
		AddJournalEntry(%eet_2%71126,QUEST)  // The Siege of Dragonspear I should speak with Torsin deLancie. Dragonspear Castle's inner courtyard has been penetrated by the forces aligned against the crusade. I would do well to consult a coalition commander about our troops' progress before entering the castle itself.
		SmallWait(10)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/bdcut43.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut43.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut43.baf~ EVALUATE_BUFFER

<<<<<<<< d5/d5caehef.d
ADD_STATE_TRIGGER bdcaelar 44 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 50 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 52 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDDELANC

IF WEIGHT #-1 ~AreaCheck("bd4000") GlobalGT("bd_plot","global",430) GlobalLT("bd_plot","global",450)~ BEGIN d5delanc_c1
  SAY @20112
  IF ~~ REPLY @20113 GOTO d5delanc_c2
END

IF ~~ BEGIN d5delanc_c2
  SAY @20114
  IF ~~ 
    DO ~ClearAllActions()
		SetGlobal("bd_plot","global",450)
		StartCutSceneMode()
		FadeToColor([20.0],0)
		StartCutSceneEx("d5port1",TRUE)~
    EXIT
END

END

APPEND BDHEPHER

IF WEIGHT #5 ~GlobalGT("bd_plot","global",449) GlobalLT("bd_plot","global",480) AreaCheck("bd4300")~ BEGIN d5heph_c1
  SAY @20115
  IF ~~ EXTERN BDCAELAR d5caelar_c1
END

IF ~~ BEGIN d5heph_c2
  SAY @20116
  IF ~~ GOTO d5heph_c3
END

IF ~~ BEGIN d5heph_c3
  SAY @20117
  IF ~~ GOTO d5heph_c4
END

IF ~~ BEGIN d5heph_c4
  SAY @20118
  IF ~~ EXTERN BDCAELAR d5caelar_c2
END

IF ~~ BEGIN d5heph_c5
  SAY @20119
  IF ~~ GOTO d5heph_c7
END

IF ~~ BEGIN d5heph_c6
  SAY @20120
  IF ~~ GOTO d5heph_c7
END

IF ~~ BEGIN d5heph_c7
  SAY @20121
  IF ~~
    DO ~StartCutSceneMode()
		StartCutSceneEx("d5port2",FALSE)~
    EXIT
END

END

APPEND BDCAELAR

IF ~~ BEGIN d5caelar_c1
  SAY @20122
  IF ~~ EXTERN BDHEPHER d5heph_c2
END

IF ~~ BEGIN d5caelar_c2
  SAY @20123
  IF ~Global("bd_halata_freed","global",0)~ EXTERN BDHEPHER d5heph_c5
  IF ~Global("bd_halata_freed","global",1)~ EXTERN BDHEPHER d5heph_c6
END

END
>>>>>>>>
COPY ~d5/d5caehef.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5caehef.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5caehef.d~


//___________________________________________________________________________________
//
// coalition mage Detect Magic
// just rewrite the state

<<<<<<<< d5/d5pordor.d
REPLACE_SAY BDFFMAGE 0 @20125

REPLACE_SAY BDANDRUS 0 @20125

REPLACE_SAY BDBENCE 105 @20126
>>>>>>>>
COPY ~d5/d5pordor.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5pordor.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5pordor.d~


//___________________________________________________________________________________
//
// new portal cut scene for Charname
// adapt from bdcut45b
// caelar frees herself, closes the door, goes into the portal "as long as I control the portal, the day is not lost!"
// fiends appear
// pay attention to the bd_plot variable!

<<<<<<<< d5/bdcut45.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")
		ClearAllActions()
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",60)
		SetCutSceneBreakable(TRUE)
		SmallWait(5)
		MoveViewPoint([1060.685],BD_NORMAL)
		ActionOverride("bdffsol1",FaceObject("door03"))
		ActionOverride("bdffsol2",FaceObject("door03"))
		ActionOverride("bdffmage",FaceObject("door03"))
		ActionOverride("bdbence",FaceObject("door03"))
		SmallWait(5)
		Unlock("door03")
		OpenDoor("door03")
		SmallWait(5)
		CreateCreature("bdcaelar",[520.400],SE)
		CreateCreature("bdcrue50",[440.520],NE)  // Crusader Elite
		CreateCreature("bdcrue53",[470.295],SE)  // Crusader Elite
		CreateCreature("bdlieutg",[745.300],SW)  // Crusader Elite
		CreateCreature("bdlieutc",[340.395],E)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdlieute",[505.220],S)  // Crusader Elite
		CreateCreature("bdlieuta",[500.570],NE)  // Caelar's Lieutenant
		CreateCreature("bdcrue52",[425.340],SE)  // Crusader Elite
		CreateCreature("bdcrue55",[645.270],S)  // Crusader Elite
		CreateCreature("bdlieutd",[390.280],SE)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdcrue51",[410.435],E)  // Crusader Elite
		CreateCreature("bdcrue54",[550.255],S)  // Crusader Elite
		CreateCreature("bdlieutb",[375.520],NE)  // Caelar's Lieutenant
		CreateCreature("bdlieutf",[645.230],S)  // Crusader Elite
		ActionOverride("bdcaelar",SetSequence(16))
		SmallWait(5)
		ReallyForceSpellRES("bdglowgr","bdlieuta")
		ReallyForceSpellRES("bdglowgr","bdcrue50")
		ReallyForceSpellRES("bdglowgr","bdcrue51")
		ReallyForceSpellRES("bdglowgr","bdcrue52")
		ReallyForceSpellRES("bdglowgr","bdcrue53")
		ReallyForceSpellRES("bdglowgr","bdcrue54")
		ReallyForceSpellRES("bdglowgr","bdcrue55")
		ReallyForceSpellRES("bdglowgr","bdlieutg")
		ReallyForceSpellRES("bdglowgr","bdlieutb")
		ReallyForceSpellRES("bdglowgr","bdlieutc")
		ReallyForceSpellRES("bdglowgr","bdlieutd")
		ReallyForceSpellRES("bdglowgr","bdlieute")
		ReallyForceSpellRES("bdglowgr","bdlieutf")
		SmallWait(5)
		ActionOverride("bdffsol1",MoveToPoint([995.665]))
		ActionOverride("bdffsol2",MoveToPoint([1055.605]))
		ActionOverride("bdffmage",MoveToPoint([1050.625]))
		ActionOverride("bdbence",MoveToPoint([1005.630]))
		ActionOverride(Player1,MoveToPoint([820.530]))
		ActionOverride(Player2,MoveToPoint([855.480]))
		ActionOverride(Player3,MoveToPoint([870.555]))
		ActionOverride(Player4,MoveToPoint([905.505]))
		ActionOverride(Player5,MoveToPoint([915.580]))
		ActionOverride(Player6,MoveToPoint([950.530]))
		SmallWait(10)
		FadeToColor([10.0],0)
		SmallWait(10)
		ActionOverride("bdffsol1",JumpToPoint([995.665]))
		ActionOverride("bdffsol2",JumpToPoint([1055.605]))
		ActionOverride("bdffmage",JumpToPoint([1050.625]))
		ActionOverride("bdbence",JumpToPoint([1005.630]))
		ActionOverride(Player1,JumpToPoint([820.530]))
		ActionOverride(Player2,JumpToPoint([855.480]))
		ActionOverride(Player3,JumpToPoint([870.555]))
		ActionOverride(Player4,JumpToPoint([905.505]))
		ActionOverride(Player5,JumpToPoint([915.580]))
		ActionOverride(Player6,JumpToPoint([950.530]))
		MoveViewPoint([590.430],INSTANT)
		SmallWait(10)
		FadeFromColor([10.0],0)
		ActionOverride(Player1,MoveToPoint([675.485]))
		ActionOverride(Player2,MoveToPoint([710.435]))
		ActionOverride(Player3,MoveToPoint([725.510]))
		ActionOverride(Player4,MoveToPoint([760.460]))
		ActionOverride(Player5,MoveToPoint([775.535]))
		ActionOverride(Player6,MoveToPoint([815.485]))
		ActionOverride("bdffsol1",MoveToPoint([885.600]))
		ActionOverride("bdffsol2",MoveToPoint([945.505]))
		ActionOverride("bdffmage",MoveToPoint([950.580]))
		ActionOverride("bdbence",MoveToPoint([865.540]))
		SmallWait(40)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(1))
		SmallWait(10)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcaelar")
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(SEQ_ATTACK_BACKSLASH))
		SmallWait(10)
		ActionOverride("bdlieuta",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue51",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue53",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue55",SetSequence(SEQ_DAMAGE))
		SmallWait(3)
		PlaySound("EFF_E06")
		ActionOverride("bdcrue50",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue52",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue54",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutg",SetSequence(SEQ_DAMAGE))
		SmallWait(3)
		PlaySound("EFF_E06")
		ActionOverride("bdlieutb",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutc",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutd",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieute",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutf",SetSequence(SEQ_DAMAGE))
		ReallyForceSpellRES("bddispel","bdlieuta")
		ReallyForceSpellRES("bddispel","bdcrue51")
		ReallyForceSpellRES("bddispel","bdcrue53")
		ReallyForceSpellRES("bddispel","bdcrue55")
		SmallWait(3)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcrue50")
		ReallyForceSpellRES("bddispel","bdcrue52")
		ReallyForceSpellRES("bddispel","bdcrue54")
		ReallyForceSpellRES("bddispel","bdlieutg")
		SmallWait(3)
		ReallyForceSpellRES("bddispel","bdlieutb")
		ReallyForceSpellRES("bddispel","bdlieutc")
		ReallyForceSpellRES("bddispel","bdlieutd")
		ReallyForceSpellRES("bddispel","bdlieute")
		ReallyForceSpellRES("bddispel","bdlieutf")
		DisplayStringHead("bdcaelar",@20130)
		SmallWait(10)
		EndCutSceneMode()
		ActionOverride("bdcaelar",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/bdcut45.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut45.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut45.baf~

<<<<<<<< d5/d5nuport.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")
		ClearAllActions()
		DisplayStringHead("bdbence",@20131)
		ActionOverride("bdbence",MoveToPoint([1100.725]))
		Wait(2)
		DisplayStringHead("bdcaelar",@20132)
		ActionOverride("bdbence",JumpToPoint([1100.725]))
		SmallWait(1)
		ActionOverride("bdcaelar",SetSequence(SEQ_ATTACK_BACKSLASH))
		SmallWait(5)
		ReallyForceSpellRES("bddispel","bdlieuta")
		ReallyForceSpellRES("bddispel","bdcrue51")
		ReallyForceSpellRES("bddispel","bdcrue53")
		ReallyForceSpellRES("bddispel","bdcrue55")
		SmallWait(3)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcrue50")
		ReallyForceSpellRES("bddispel","bdcrue52")
		ReallyForceSpellRES("bddispel","bdcrue54")
		ReallyForceSpellRES("bddispel","bdlieutg")
		SmallWait(3)
		ReallyForceSpellRES("bddispel","bdlieutb")
		ReallyForceSpellRES("bddispel","bdlieutc")
		ReallyForceSpellRES("bddispel","bdlieutd")
		ReallyForceSpellRES("bddispel","bdlieute")
		ReallyForceSpellRES("bddispel","bdlieutf")
		SmallWait(5)
		CloseDoor("door03")
		ActionOverride("bdcaelar",Face(N))
		SmallWait(10)
		ActionOverride("bdcaelar",Face(NW))
		SmallWait(1)
		ActionOverride("bdcaelar",Face(W))
		SmallWait(1)
		ActionOverride("bdcaelar",Face(SW))
		DisplayStringHead("bdcaelar",@20133)
		SmallWait(10)
		ActionOverride("bdcaelar",MoveToPoint([630.430]))
		SmallWait(5)
		ActionOverride("bdlieuta",MoveToPoint([630.430]))
		ActionOverride("bdcrue51",MoveToPoint([630.430]))
		ActionOverride("bdcrue53",MoveToPoint([630.430]))
		ActionOverride("bdcrue55",MoveToPoint([630.430]))
		SmallWait(3)
		ActionOverride("bdcrue50",MoveToPoint([630.430]))
		ActionOverride("bdcrue52",MoveToPoint([630.430]))
		ActionOverride("bdcrue54",MoveToPoint([630.430]))
		ActionOverride("bdlieutg",MoveToPoint([630.430]))
		SmallWait(3)
		ActionOverride("bdlieutb",MoveToPoint([630.430]))
		ActionOverride("bdlieutc",MoveToPoint([630.430]))
		ActionOverride("bdlieutd",MoveToPoint([630.430]))
		ActionOverride("bdlieute",MoveToPoint([630.430]))
		ActionOverride("bdlieutf",MoveToPoint([630.430]))
		CreateVisualEffectObject("shair","bdcaelar")
		ActionOverride("bdcaelar",DestroySelf())
		SmallWait(11)
		CreateVisualEffectObject("shair","bdlieuta")  // Caelar's Lieutenant
		CreateVisualEffectObject("shair","bdcrue51")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue53")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue55")  // Crusader Elite
		ActionOverride("bdlieuta",DestroySelf())
		ActionOverride("bdcrue51",DestroySelf())
		ActionOverride("bdcrue53",DestroySelf())
		ActionOverride("bdcrue55",DestroySelf())
		SmallWait(6)
		CreateVisualEffectObject("shair","bdcrue50")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue52")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue54")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutg")  // Crusader Elite
		ActionOverride("bdcrue50",DestroySelf())
		ActionOverride("bdcrue52",DestroySelf())
		ActionOverride("bdcrue54",DestroySelf())
		ActionOverride("bdlieutg",DestroySelf())
		SmallWait(7)
		CreateVisualEffectObject("shair","bdlieutb")  // Caelar's Lieutenant
		CreateVisualEffectObject("shair","bdlieutc")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutd")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieute")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutf")  // Crusader Elite
		ActionOverride("bdlieutb",DestroySelf())
		ActionOverride("bdlieutc",DestroySelf())
		ActionOverride("bdlieutd",DestroySelf())
		ActionOverride("bdlieute",DestroySelf())
		ActionOverride("bdlieutf",DestroySelf())
		SmallWait(10)
		Lock("door03")
		CreateCreature("BDSKIE",[920.360],SW)
		SmallWait(10)
		DisplayStringHead("BDSKIE",@20134)
		ActionOverride("BDSKIE",MoveToPoint([630.430]))
		SmallWait(30)
		CreateVisualEffectObject("shair","BDSKIE")
		ActionOverride("BDSKIE",DestroySelf())
		SetGlobal("D5SkieInHell","global",1)
		Wait(3)
		ActionOverride("bdbence",DestroySelf())
		TriggerActivation("portal",TRUE)
		CreateCreatureEffect("BDIMP","SPFLESHS",[615.420],E)  // Imp
		SmallWait(5)
		CreateCreatureEffect("bdlemure","SPFLESHS",[490.470],NW)  // Lemure
		SmallWait(5)
		CreateCreatureEffect("bdabibla","SPFLESHS",[630.300],SW)  // Black Abishai
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5nuport.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.baf~

<<<<<<<< d5/d5nuport.d
APPEND BDCAELAR

IF ~GlobalGT("bd_plot","global",479) GlobalLT("bd_plot","global",500) AreaCheck("bd4300")~ BEGIN d5caelar_d1
  SAY @20135
  IF ~~ REPLY @20136 EXTERN BDBENCE d5benc_d1
END

END

APPEND BDBENCE

IF ~~ BEGIN d5benc_d1
  SAY @20137
  IF ~~ GOTO d5benc_d2
END

IF ~~ BEGIN d5benc_d2
  SAY @20138
  IF ~~
    DO ~SetGlobal("bd_plot","global",490)
    	StartCutSceneMode()
		StartCutSceneEx("d5nuport",FALSE)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5nuport.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.d~


//___________________________________________________________________________________
//
// add skie to avernus, right near portal
// have her start dialogue if nearby and no combat happening
// she is freaked out; moves to portal after dialogue

<<<<<<<< d5/d54400_1.baf
IF
	Global("D5SkieInHell","global",1)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",2)
		CreateCreature("bdskie",[930.1150],E)
		Continue()
END
>>>>>>>>
COPY ~d5/d54400_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_1.baf~
EXTEND_TOP ~bd4400.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_1.baf~

<<<<<<<< d5/d5skie_1.baf
IF
	AreaCheck("BD4400") 
	Global("D5SkieInHell","global",2)
	!ActuallyInCombat()
	Range([PC],9)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",3)
		Wait(1)
		StartDialogueNoSet(Player1)
END	

IF
	AreaCheck("BD4400") 
	Global("D5SkieInHell","global",4)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",5)
		Wait(1)
		MoveToPoint([1290.1375])
END	
>>>>>>>>
COPY ~d5/d5skie_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skie_1.baf~
EXTEND_BOTTOM ~bdskie.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skie_1.baf~


<<<<<<<< d5/d5skaver.d
APPEND BDSKIE

IF ~AreaCheck("BD4400") GlobalGT("D5SkieInHell","global",1) GlobalLT("D5SkieInHell","global",4)~ BEGIN d5bdskie_a1
  SAY @20141
  IF ~~ GOTO d5bdskie_a2
END

IF ~~ BEGIN d5bdskie_a2
  SAY @20142
  IF ~~ GOTO d5bdskie_a3
END

IF ~~ BEGIN d5bdskie_a3
  SAY @20143
  IF ~~ REPLY @20144 GOTO d5bdskie_a4
  IF ~~ REPLY @20145 GOTO d5bdskie_a5
END

IF ~~ BEGIN d5bdskie_a4
  SAY @20146
  IF ~~ REPLY @20147 GOTO d5bdskie_a6
END

IF ~~ BEGIN d5bdskie_a5
  SAY @20149
  IF ~~ REPLY @20150 GOTO d5bdskie_a6
END

IF ~~ BEGIN d5bdskie_a6
  SAY @20152
  IF ~~ 
    DO ~SetGlobal("D5SkieInHell","global",4)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5skaver.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skaver.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5skaver.d~


//___________________________________________________________________________________
//
// darnas' dialogue in avernus
// just rewrite the states
<<<<<<<< d5/d5darnas.d
REPLACE_SAY BDDARNAS 2 @20156

ALTER_TRANS BDDARNAS // file name
  BEGIN 2 END // state number
  BEGIN 1 END // transition number
  BEGIN "REPLY" ~@20157~ END

REPLACE_SAY BDDARNAS 4 @20158
>>>>>>>>
COPY ~d5/d5darnas.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5darnas.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5darnas.d~

// if !enemy, have him walk over to the portal after dialogue

<<<<<<<< d5/bdavern_.baf
IF
	AreaCheck("BD4400") 
	GlobalGT("bd_darnas_plot","global",2)
	GlobalLT("bd_darnas_plot","global",9)
	Name("bddarnas",Myself)
THEN
	RESPONSE #100
		Wait(1)
		MoveToPoint([1185.1330])
END	
>>>>>>>>
COPY ~d5/bdavern_.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdavern_.baf~
EXTEND_BOTTOM ~bdavernu.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/bdavern_.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdavernu.bcs~) BEGIN
  COPY_EXISTING ~bdavernu.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Switch("bd_darnas_plot","global")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !Name("bddarnas",Myself)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// remove umbral accord minutes from illaruel
COPY_EXISTING ~bdillaru.cre~ ~override~
  REMOVE_CRE_ITEM ~bdmisc65~
IF_EXISTS BUT_ONLY


//___________________________________________________________________________________
//
// change cut scene line when you catch up to caelar in avernus - bdcut50

OUTER_SET promise_string = RESOLVE_STR_REF (@20160)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut50.bcs~) BEGIN
  COPY_EXISTING ~bdcut50.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(%eet_2%38863\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%promise_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// change caelar's dialogue when you catch up to her in avernus
// set unattainable trigger for bdcaelar state 53
// append new state w/ new speech

<<<<<<<< d5/d5cavern.d
ADD_STATE_TRIGGER bdcaelar 53 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~Global("bd_plot","global",530) AreaCheck("bd4500")~ BEGIN d5caelar_e1
  SAY @20161
  IF ~~ REPLY @20162 GOTO d5caelar_e6
  IF ~~ REPLY @20163 GOTO d5caelar_e2
  IF ~~ REPLY @20164 GOTO d5caelar_e6
END

IF ~~ BEGIN d5caelar_e2
  SAY @20165
  IF ~~ GOTO d5caelar_e3
END

IF ~~ BEGIN d5caelar_e3
  SAY @20166
  IF ~~ GOTO d5caelar_e4
END

IF ~~ BEGIN d5caelar_e4
  SAY @20167
  IF ~~ GOTO d5caelar_e5
END

IF ~~ BEGIN d5caelar_e5
  SAY @20168
  IF ~~ REPLY @20169 GOTO d5caelar_e6
  IF ~CheckStatGT(Myself,16,CHR) CheckStatGT(Myself,15,INT)~ REPLY @20170 GOTO d5caelar_e8
END

IF ~~ BEGIN d5caelar_e6
  SAY @20172
  IF ~~ REPLY @20173 GOTO d5caelar_e7
  IF ~~ REPLY @20174 GOTO d5caelar_e7
END

IF ~~ BEGIN d5caelar_e7
  SAY @20176
  IF ~~ DO ~SetGlobal("bd_plot","global",530)
  			StartCutSceneMode()
  			StartCutSceneEx("bdcut51",FALSE)~
    EXIT
END

IF ~~ BEGIN d5caelar_e8
  SAY @20177
  IF ~~ DO ~SetGlobal("bd_plot","global",530)
  			SetGlobal("D5SkipBel","GLOBAL",1)
  			GiveItemCreate("bdkey02",Player1,1,0,0)
  			StartCutSceneEx("bdcut51",FALSE)~ // maybe make new cut scene for this...?
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5cavern.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cavern.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cavern.d~


//___________________________________________________________________________________
//
// change cut scene in advance of belhifet talk - bdcut57

OUTER_SET caebel_string = RESOLVE_STR_REF (@20181)

OUTER_SET belcae1_string = RESOLVE_STR_REF (@20182)

OUTER_SET belcae2_string = RESOLVE_STR_REF (@20183)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut57.bcs~) BEGIN
  COPY_EXISTING ~bdcut57.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(%eet_2%39110\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%caebel_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(%eet_2%39111\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%belcae1_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(%eet_2%39112\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%belcae2_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// rewrite behlifet's dialogue
// set unattainable trigger for bdbelhif states 0/1
// append entirely new dialogue

<<<<<<<< d5/d5belcae.d
ADD_STATE_TRIGGER bdbelhif 0 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdbelhif 1 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDBELHIF

IF ~GlobalLT("bd_plot","global",565)~ BEGIN d5belhif_f1
  SAY #%eet_2%39113 // What is this? The spawn of Bhaal, already? I'll have Thrix's hide for this!
  IF ~~ REPLY @20201 DO ~SetGlobal("bd_plot","global",565)~ EXTERN BDCAELAR d5caelar_f1
  IF ~~ REPLY @20202 DO ~SetGlobal("bd_plot","global",565)~ GOTO d5belhif_f2
  IF ~~ REPLY @20203 DO ~SetGlobal("bd_plot","global",565)~ GOTO d5belhif_f2
END

IF ~~ BEGIN d5belhif_f2
  SAY @20204
  IF ~~ GOTO d5belhif_f4
END

IF ~~ BEGIN d5belhif_f3
  SAY @20205
  IF ~~ GOTO d5belhif_f4
END

IF ~~ BEGIN d5belhif_f4
  SAY @20206
  IF ~~ GOTO d5belhif_f5
END

IF ~~ BEGIN d5belhif_f5
  SAY @20207
  IF ~~ GOTO d5belhif_f6
END

IF ~~ BEGIN d5belhif_f6
  SAY @20208
  IF ~~ EXTERN BDCAELAR d5caelar_f2
END

IF ~~ BEGIN d5belhif_f7
  SAY @20209
  IF ~~ REPLY @20210 EXTERN BDCAELAR d5caelar_f3
END

IF ~~ BEGIN d5belhif_f8
  SAY @20215
  IF ~~ GOTO d5belhif_f9
END

IF ~~ BEGIN d5belhif_f9
  SAY @20216
  IF ~~ EXTERN BDCAELAR d5caelar_f5
END

IF ~~ BEGIN d5belhif_f10
  SAY @20217
  IF ~~ GOTO d5belhif_f11
END

IF ~~ BEGIN d5belhif_f11
  SAY @20218
  IF ~~ REPLY @20221 EXTERN BDCAELAR d5caelar_f6
  IF ~~ REPLY @20222 EXTERN BDCAELAR d5caelar_f6
  IF ~~ REPLY @20223 EXTERN BDCAELAR d5caelar_f8
  IF ~~ REPLY @20224 EXTERN BDCAELAR d5caelar_f8
END

IF ~~ BEGIN d5belhif_f12
  SAY @20225
  IF ~~ GOTO d5belhif_f13
END

IF ~~ BEGIN d5belhif_f13
  SAY @20226
  IF ~~ 
    DO ~
    	AddJournalEntry(%eet_2%59847,QUEST)
    	SetGlobal("BDFinHep","GLOBAL",1)~
    EXIT
END

IF ~~ BEGIN d5belhif_f14
  SAY @20227
  IF ~~ REPLY @20228 EXTERN BDCAELAR d5caelar_f10
END

IF ~~ BEGIN d5belhif_f15
  SAY @20230
  IF ~~ EXTERN BDHEPHER d5hepher_f2
END

END

APPEND BDCAELAR

IF ~~ BEGIN d5caelar_f1
  SAY @20231
  IF ~~ EXTERN BDHEPHER d5hepher_f1
END

IF ~~ BEGIN d5caelar_f2
  SAY @20232
  IF ~~ EXTERN BDBELHIF d5belhif_f7
END

IF ~~ BEGIN d5caelar_f3
  SAY @20233
  IF ~~ GOTO d5caelar_f4
END

IF ~~ BEGIN d5caelar_f4
  SAY @20234
  IF ~~ EXTERN BDBELHIF d5belhif_f8
END

IF ~~ BEGIN d5caelar_f5
  SAY @20235
  IF ~~ EXTERN BDBELHIF d5belhif_f10
END

IF ~~ BEGIN d5caelar_f6
  SAY #%eet_2%39160
  IF ~~ REPLY @20236 GOTO d5caelar_f7
END

IF ~~ BEGIN d5caelar_f7
  SAY #%eet_2%59720
  IF ~~
    DO ~SetGlobal("bd_caelar_fate","global",2)	//	stays good
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdbelhif",DestroyItem("minhp1"))
		DestroyItem("minhp1")
		DestroyItem("monhp1")
		GiveItemCreate("bdkey02",Myself,1,0,0)~
	EXTERN BDBELHIF d5belhif_f12
  IF ~NumInPartyLT(6)~
    DO ~SetGlobal("bd_caelar_fate","global",2)	//	stays good
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdbelhif",DestroyItem("minhp1"))
		DestroyItem("minhp1")
		DestroyItem("monhp1")
		GiveItemCreate("bdkey02",Myself,1,0,0)
		JoinParty()~ 
	EXTERN BDBELHIF d5belhif_f12
END

IF ~~ BEGIN d5caelar_f8
  SAY @20240
  IF ~~ GOTO d5caelar_f9
END

IF ~~ BEGIN d5caelar_f9
  SAY @20241
  IF ~~ EXTERN BDBELHIF d5belhif_f14
END

IF ~~ BEGIN d5caelar_f10
  SAY @20242
  IF ~~ EXTERN BDBELHIF d5belhif_f15
END

END

APPEND BDHEPHER

IF ~~ BEGIN d5hepher_f1
  SAY @20244
  IF ~~ EXTERN BDBELHIF d5belhif_f3
END

IF ~~ BEGIN d5hepher_f2
  SAY @20245
  IF ~~ 
    DO ~StartCutSceneMode()
    	AddJournalEntry(%eet_2%59849,QUEST)
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdcaelar",DestroyItem("minhp1"))
		ActionOverride("bdcaelar",DestroyItem("monhp1"))
//		GiveItemCreate("bdkey02","bdcaelar",1,0,0)
    	SetGlobal("bd_caelar_fate","global",1)	//	turns evil
    	StartCutSceneEx("bdcut57a",FALSE)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5belcae.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belcae.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5belcae.d~ EVALUATE_BUFFER

COPY_EXISTING ~bdcaelar.cre~ ~override~
	ADD_CRE_ITEM ~bdkey02~ #1 #0 #0 ~IDENTIFIED~ ~INV~
IF_EXISTS


//___________________________________________________________________________________
//
// scrub aun argent from the game

<<<<<<<< d5/d54700_1.baf
IF
	InMyArea("BDAUN")	
	Global("D5PeaceOutAun","BD4700",0)
THEN
	RESPONSE #100
		SetGlobal("D5PeaceOutAun","BD4700",1)
		ActionOverride("BDAUN",DestroySelf())
		Continue()
END
>>>>>>>>
COPY ~d5/d54700_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_1.baf~
EXTEND_TOP ~bd4700.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_1.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut57b.bcs~) BEGIN
  COPY_EXISTING ~bdcut57b.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(DisplayStringHead("bdaun",%eet_2%39174)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
/*
		SPRINT textToReplace ~\(DisplayStringHead("bdaun",239174)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
*/
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// new post-belhifet behavior
// new dialogue for bdcaelar if alive
// set variable if bdcaelar is dead
// cut scene(s) to portal

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcaelar.bcs~) BEGIN
  COPY_EXISTING ~bdcaelar.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("bd_plot","global",570)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("bd_plot","global",999) AreaCheck("BD4700")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d54700_2.baf
IF
	GlobalGT("bd_plot","global",569)
	GlobalLT("bd_plot","global",577)
	InParty("bdcaelar")
	!Dead("bdcaelar")
THEN
	RESPONSE #100
		ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
		SetGlobal("bd_plot","global",577)
		Continue()
END

IF
	GlobalGT("bd_plot","global",569)
	GlobalLT("bd_plot","global",577)
	!InParty("bdcaelar")
	!Dead("bdcaelar")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",577)
		Continue()
END

IF
	Global("bd_plot","global",570)
	InPartyAllowDead("bdcaelar")
	Dead("bdcaelar")
THEN
	RESPONSE #100
		ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
		SetGlobal("bd_plot","global",579)
END

IF
	Global("bd_plot","global",570)
	!InPartyAllowDead("bdcaelar")
	Dead("bdcaelar")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",579)
END

IF	
	GlobalGT("bd_plot","global",577)
	PartyHasItem("bdkey02")
THEN
	RESPONSE #100
		StartCutSceneMode()
    	AddJournalEntry(%eet_2%59849,QUEST)
    	Wait(2)
    	StartCutSceneEx("bdcut58",TRUE)
END
>>>>>>>>
COPY ~d5/d54700_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_2.baf~
EXTEND_TOP ~bd4700.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_2.baf~ EVALUATE_BUFFER

/* maybe let's just use bdcut58
<<<<<<<< d5/d5cut58a.baf
IF
	
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",579)
END
>>>>>>>>
COPY ~d5/d5cut58a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut58a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut58a.baf~ EVALUATE_BUFFER
*/

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut58.bcs~) BEGIN
  COPY_EXISTING ~bdcut58.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CreateCreature("bdaun2",\).+$~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~ActionOverride("bdaun".+$~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~EndCutSceneMode()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!Dead("bdcaelar")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("D5SkieInHell","global",1)
	GlobalLT("D5SkieInHell","global",6)
	!InMyArea("bdskie")
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",6)
		CreateCreature("bdskie",[1290.1375],NE)
END

IF
	!Dead("bdcaelar")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d5postb.d
ADD_STATE_TRIGGER bdcaelar 68 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 83 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~Global("bd_plot","global",577) AreaCheck("bd4700") !ActuallyInCombat()~ BEGIN d5caelar_g1
  SAY @20251
  IF ~~ GOTO d5caelar_g2
END

IF ~~ BEGIN d5caelar_g2
  SAY @20252
  IF ~~ GOTO d5caelar_g3
END
  
IF ~~ BEGIN d5caelar_g3
  SAY @20253
  IF ~~ REPLY @20254 GOTO d5caelar_g4
  IF ~~ REPLY @20255 GOTO d5caelar_g4
END

IF ~~ BEGIN d5caelar_g4
  SAY @20257
  IF ~~ GOTO d5caelar_g5
END

IF ~~ BEGIN d5caelar_g5
  SAY @20258
  IF ~~ 
    DO ~SetGlobal("bd_caelar_fate","global",5) // ***** and AddJournalEntry
    	SetGlobal("bd_plot","global",578)
    	GiveItemCreate("bdkey02",Player1,1,0,0)~
    EXIT
  IF ~InParty("bdcaelar")~ 
    DO ~ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
    	SetGlobal("bd_plot","global",578)
		SetGlobal("bd_caelar_fate","global",5) // ***** and AddJournalEntry
    	GiveItemCreate("bdkey02",Player1,1,0,0)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5postb.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5postb.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5postb.d~ EVALUATE_BUFFER



//___________________________________________________________________________________
//
// new portal scene 
// new portal dialogue from caelar if she is alive
// note triggers for new dialogue
// new portal behavior, have to actually click it
// have skie & darnas go through with you
// rewrite BDINFO12.bcs, BDCUT59.bcs

<<<<<<<< d5/d54400_2.baf
IF
	PartyHasItem("bdkey02")
	GlobalLT("bd_plot","global",580)
THEN
	RESPONSE #100
		Wait(2)
		SetGlobal("bd_plot","global",580)
		Continue()
END
/*
IF
	GlobalGT("bd_plot","global",580)
	GlobalLT("D5SkieInHell","global",6)
	!InMyArea("bdskie")
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",6)
		CreateCreature("bdskie",[1290.1375],NE)
		Continue()
END
*/
>>>>>>>>
COPY ~d5/d54400_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_2.baf~
EXTEND_TOP ~bd4400.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_2.baf~

<<<<<<<< d5/d5gohome.d
ADD_STATE_TRIGGER bdcaelar 84 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~Global("bd_plot","global",580) AreaCheck("BD4400") !ActuallyInCombat()~ BEGIN d5caelar_h1
  SAY @20261
  IF ~~ REPLY @20262 DO ~SetGlobal("bd_plot","global",581)~ GOTO d5caelar_h2
END

IF ~~ BEGIN d5caelar_h2
  SAY @20264
  IF ~~ GOTO d5caelar_h3
END

IF ~~ BEGIN d5caelar_h3
  SAY @20265
  IF ~~ REPLY @20266 GOTO d5caelar_h6
  IF ~~ REPLY @20267 GOTO d5caelar_h5
  IF ~~ REPLY @20268 GOTO d5caelar_h4
END

IF ~~ BEGIN d5caelar_h4
  SAY @20269
  IF ~~ DO ~Enemy()~ EXIT
END

IF ~~ BEGIN d5caelar_h5
  SAY @20270
  IF ~~ REPLY @20271 GOTO d5caelar_h6
END

IF ~~ BEGIN d5caelar_h6
  SAY @20273
  IF ~~ GOTO d5caelar_h7
END

IF ~~ BEGIN d5caelar_h7
  SAY @20274
  IF ~~ REPLY @20275 GOTO d5caelar_h9
  IF ~~ REPLY @20276 GOTO d5caelar_h9
  IF ~GlobalGT("D5DaustonQuest","GLOBAL",6)~ REPLY @20277 GOTO d5caelar_h8
END

IF ~~ BEGIN d5caelar_h8
  SAY @20278
  IF ~~ GOTO d5caelar_h9
END

IF ~~ BEGIN d5caelar_h9
  SAY @20279
  IF ~~ GOTO d5caelar_h10
END

IF ~~ BEGIN d5caelar_h10
  SAY @20280
  IF ~~ EXIT
END

IF ~GlobalGT("bd_plot","global",582) AreaCheck("BD4400")~ BEGIN d5caelar_h11
  SAY @20281
  IF ~~ EXIT
END

END

APPEND BDSKIE

IF ~GlobalGT("bd_plot","global",579) AreaCheck("bd4400")~ BEGIN d5skie_h1
  SAY @20284
  IF ~~ EXIT
END

END

APPEND BDDARNAS

IF ~GlobalGT("bd_plot","global",579) AreaCheck("bd4400")~ BEGIN d5darnas_h1
  SAY @20285
  IF ~~ EXIT
END

END
>>>>>>>>
COPY ~d5/d5gohome.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5gohome.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5gohome.d~ EVALUATE_BUFFER

// ***** add waves of fiends appearing? like the end of BP1

ACTION_IF (FILE_EXISTS_IN_GAME ~bdinfo12.bcs~) BEGIN
  COPY_EXISTING ~bdinfo12.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Name("portal",Myself)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !PartyHasItem("bdkey02")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/nuinfo12.baf
IF
	Clicked([ANYONE])
	Name("portal",Myself)
	AreaCheck("bd4400")
	PartyHasItem("bdkey02")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",583)
		StartCutSceneMode()
    	StartCutSceneEx("d5cut59",TRUE)
END
>>>>>>>>
COPY ~d5/nuinfo12.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/nuinfo12.baf~
EXTEND_TOP ~bdinfo12.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/nuinfo12.baf~

<<<<<<<< d5/d5cut59.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",59)
		SetCutSceneBreakable(TRUE)
		SmallWait(15)
END

IF
	InMyArea("bdskie")
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride("bdskie",MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride("bdskie",SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh","bdskie")
		SmallWait(2)
		ApplySpellRES("bdhide","bdskie")  // No such index
		SmallWait(5)
		ActionOverride("bdskie",JumpToPoint([1455.1175]))
END

IF
	InMyArea("bddarnas")
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride("bddarnas",MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride("bddarnas",SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh","bddarnas")
		SmallWait(2)
		ApplySpellRES("bdhide","bddarnas")  // No such index
		SmallWait(5)
		ActionOverride("bddarnas",JumpToPoint([1485.1175]))
END

IF
	InMyArea(Player2)
	!StateCheck(Player2,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player2,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player2,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player2)
		SmallWait(2)
		ApplySpellRES("bdhide",Player2)  // No such index
		SmallWait(5)
		ActionOverride(Player2,JumpToPoint([1485.1270]))
END

IF
	InMyArea(Player3)
	!StateCheck(Player3,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player3,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player3,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player3)
		SmallWait(2)
		ApplySpellRES("bdhide",Player3)  // No such index
		SmallWait(5)
		ActionOverride(Player3,JumpToPoint([1465.1300]))
END

IF
	InMyArea(Player4)
	!StateCheck(Player4,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player4,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player4,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player4)
		SmallWait(2)
		ApplySpellRES("bdhide",Player4)  // No such index
		SmallWait(5)
		ActionOverride(Player4,JumpToPoint([1430.1315]))
END

IF
	InMyArea(Player5)
	!StateCheck(Player5,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player5,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player5,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player5)
		SmallWait(2)
		ApplySpellRES("bdhide",Player5)  // No such index
		SmallWait(5)
		ActionOverride(Player5,JumpToPoint([1390.1320]))
END

IF
	InMyArea(Player6)
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player6,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player6,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player6)
		SmallWait(2)
		ApplySpellRES("bdhide",Player6)  // No such index
		SmallWait(5)
		ActionOverride(Player6,JumpToPoint([1340.1325]))
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player1,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player1,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player1)
		SmallWait(2)
		ApplySpellRES("bdhide",Player1)  // No such index
		SmallWait(5)
		ActionOverride(Player1,JumpToPoint([1485.1240]))
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		SmallWait(5)
		ActionOverride("cutspy",DestroySelf())
		FadeToColor([40.0],0)
		SmallWait(25)
		ApplySpellRES("bdunhide",Player1)
		ApplySpellRES("bdunhide",Player2)
		ApplySpellRES("bdunhide",Player3)
		ApplySpellRES("bdunhide",Player4)
		ApplySpellRES("bdunhide",Player5)
		ApplySpellRES("bdunhide",Player6)
		ApplySpellRES("bdunhide","bdskie")
		ApplySpellRES("bdunhide","bddarnas")
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		StartCutScene("bdcut59a")
END
>>>>>>>>
COPY ~d5/d5cut59.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59.baf~

<<<<<<<< d5/d5cut59a.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SmallWait(5)
//		ActionOverride("bdskie",LeaveAreaLUA("bd4300","",[600.350],S))
//		ActionOverride("bddarnas",LeaveAreaLUA("bd4300","",[535.375],SE))
		ActionOverride(Player2,LeaveAreaLUA("bd4300","",[600.350],S))
		ActionOverride(Player3,LeaveAreaLUA("bd4300","",[535.375],SE))
		ActionOverride(Player4,LeaveAreaLUA("bd4300","",[500.425],E))
		ActionOverride(Player5,LeaveAreaLUA("bd4300","",[520.475],NE))
		ActionOverride(Player6,LeaveAreaLUA("bd4300","",[580.495],NE))
		LeaveAreaLUAPanic("bd4300","",[660.365],SW)
		LeaveAreaLUA("bd4300","",[660.365],SW)
		MultiPlayerSync()
		Wait(1)
		StartCutSceneEx("bdcut59b",TRUE)
END
>>>>>>>>
COPY ~d5/d5cut59a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut59a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut59a.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut59b.bcs~) BEGIN
  COPY_EXISTING ~bdcut59b.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(!Dead("bdcaelar")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("bd_plot","global",999)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(AmbientActivate("portal_visuals",FALSE)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(AmbientActivate("portal_webm",FALSE)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d5cut59b.baf
IF
	GlobalGT("D5SkieInHell","global",1)
	GlobalLT("D5SkieInHell","global",7)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetGlobal("D5SkieInHell","global",7)
		CreateCreature("bdskie",[600.350],E)
		Continue()
END
>>>>>>>>
COPY ~d5/d5cut59b.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59b.baf~
EXTEND_TOP ~bdcut59b.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59b.baf~


//___________________________________________________________________________________
//
// new return to dragonspear scene
// portal script sends you to the vault
// some new dialogue from Skie/Darnas?
// make sure everyone in place (just set bd_plot var?)
// make sure the vault door can be opened

// add portal closing animation
COPY_EXISTING ~bd4300.are~ ~override~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x = 370
      fj_loc_y = 290
      fj_flags = 12608
      fj_width = 512
      fj_height = 256
    STR_VAR
      fj_structure_type = ~animation~
      fj_name = ~Portal_Closing~
      fj_bam_resref = ~BD4400PC~
  END
BUT_ONLY

<<<<<<<< d5/d54300_3.baf
IF
	Global("bd_plot","global",590)
	Global("D5ClosePortal","BD4300",0)
THEN
	RESPONSE #100
		SetGlobal("D5ClosePortal","BD4300",1)
		StartCutSceneMode()
    	StartCutSceneEx("d5cut59z",FALSE)
END
>>>>>>>>
COPY ~d5/d54300_3.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_3.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_3.baf~

<<<<<<<< d5/d5cut59z.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bddelanc")
		SetCutSceneBreakable(FALSE)
		SetGlobal("D5ClosePortal","BD4300",1)
// make bdskie go away?
		TakePartyItem("bdkey02")
	    DestroyItem("bdkey02")
		MoveViewPoint([720.480],VERY_FAST)
		MoveToPoint([735.495])
		DisplayStringHead(Myself,@20291)
		SmallWait(5)
		ForceSpell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
		ForceSpell(Myself,CLERIC_HOLY_POWER)
		SmallWait(100)
		AmbientActivate("Portal_Closing",TRUE)
		AmbientActivate("portal_visuals",FALSE)
		AmbientActivate("portal_webm",FALSE)
		SmallWait(60)
		AmbientActivate("Portal_Closing",FALSE)
		SmallWait(10)
		StartDialogueNoSet(Player1)
END
>>>>>>>>
COPY ~d5/d5cut59z.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59z.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59z.baf~

<<<<<<<< d5/d5imback.d
REPLACE_SAY BDBENCE 66 @20292

ADD_STATE_TRIGGER BDDELANC 95 ~Global("D5ClosePortal","BD4300",2)~

// replace the text of bddelanc state 77 transions 0-1
ALTER_TRANS BDDELANC BEGIN 77 END BEGIN 0 END BEGIN "REPLY" ~@20293~ END 

ALTER_TRANS BDDELANC BEGIN 77 END BEGIN 1 END BEGIN "REPLY" ~@20294~ END 

APPEND BDDELANC

IF ~Global("bd_plot","global",590) AreaCheck("bd4300") Global("D5ClosePortal","BD4300",1)~ BEGIN d5delanc_i1
  SAY @20295
  IF ~~ DO ~SetGlobal("D5ClosePortal","BD4300",2)~ EXIT
END

END

APPEND BDSKIE

IF ~GlobalGT("bd_plot","global",581) AreaCheck("bd4300") GlobalGT("D5SkieInHell","global",1)~ BEGIN d5skie_i1
  SAY @20300
  IF ~~ EXIT
END

END
>>>>>>>>
COPY ~d5/d5imback.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5imback.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5imback.d~ EVALUATE_BUFFER


//___________________________________________________________________________________
//


END	//	end define function

/*

IF ~~ BEGIN d5_
  SAY ~~
  IF ~~ GOTO d5_
END

*/

////////////////////
////////////////////
////////////////////
