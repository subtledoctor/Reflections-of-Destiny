
DEFINE_ACTION_FUNCTION rewrite_caelar BEGIN

/*

***** traify everything

***** write journal entries

rewrite plot:
//- Caelar's first line w/ hooded man			-	bdcut11
	...motive? only changing the course of history, nbd
//- Caelar's speech to followers in cut scene	-	bdscry3a.bcs, bdcaelar states 88-92
//- halfling inn owner							-	bdtakos state 4
	...crusaders too lawful, attacked brigands
//- Caelar dialogue at the bridge				-	state 10 et seq.
//- caelar/heph condolence scene 				-	bdcut20a
	...talk about his rituals empowering her
//- cut scene w/ Caelar and Ashatiel 			-	bdcut30a
	...talk about... zariel?
//- remove Belben from Dragonspear basement		-	
	...repurpose him elsewhere?
//- rewrite the Heph/Bel expodump				-	replace state 1 of behepher.dlg
	...talk about slowly opening the portal
//- Add something in the Hephernaan fight 		-
	...like, have Spellstrike applied to them
	...(idea being, Irenicus helps)
	...after ~20 sec,ApplySpellRES("SPWI903")
	...and DisplayStringHead("You dare?!")
  - rewrite parlay scene						-	from bdcut37; then bddelanc.dlg
//- OR: skip it???								-	
	...but then still need to set variables
	...once you've been in the underground river,
	...set a variable, and then conditionally:
	...set bd_plot to 393 in river exterior
- cut scene dialogue before dragonspear 		-	
- ashatiel challenge dialogue					-	
	...remove "met at the parley"
  - Caelar cut-scene as Dragonspear falls		-	bdcut45; state 44
//- OR, replace this with a portal cut scene 	-	adapt from bddsgat2/bdcut45a
	...add a comment in castle courtyard:
	..."why caelar not leading troops? weird"
	...then cut scene with her in basement
	...as heph goes into Avernus
//- change line about conj/illu magic at door 	-	bdffmage state 0, bdbence state 105
	...and bence's response
//- new portal scene for charname				-	adapt from bdcut45b?
	...portal is already open
	..."quick, get delancie, seal the portal!
	..."no!" caelar seals doors behind you, goes through
//- add Skie to portal room						-	
	...make portal work like bridgefort
	...little cut scene when click
	...skie appears and follows you through
	...EDIT she just goes after the crusaders do
//- add Skie to Avernus							-	
	...just outside portal
	...dialogue if !InCombat()
	...paralyzed with fear
	...stay there until you return
//- Darnas dialogue								-	rewrite state 2, trans 2-1, & state 4
//- remove umbral accord minutes from illaruel	-	
//- amend bdcut50 line 105					 	-	bdcut50
	..."promised to restore my wife"
//- Caelar dialogue in Avernus					-	state 53 & transitions
	...explain Herphernaan's betrayal
	...still hold out hope for Zariel
//- change bdcut57								-	bdcut57
	..."know what I want/make a mockery"
//- Behlifet dialogue							-	bdbelhif.dlg states 0/1
//- remove aun argent 							-	
	...remove from .ARE
	...find all dependencies
	...advance the bd_plot variable
//- new post-bel dlg from Caelar  				-	
//- back to portal after Bel  					-	
	...portal works like bridgefort
	...Skie returns with you
	...Darnas returns with you
	...if Global("D5SkipBel","GLOBAL",1), SetGlobal("D5SkipBel","GLOBAL",2)
//- back to basement after portal  				-	
	...portal closes
	...Skie appears... dialogue?
	...Darnas appears... dialogue?
	...upon opening door, new cut scene
	...Bence & Torsin enter
	...Torsin seals the portal; do the animation
//- new Torsin dialogue after seals portal 		-	
	...he has to re-seal the portal
//- change Daeros' dialogue about the portal  	-	state 2 and...
- add fiend army to BD4400  					-	BD4500.bcs and BD4400.bcs
	...if Charname never enters basalt tower
	...and backtracks


- Change the Avernus portal to work like Bridgefort as well
Upon return to portal, if Global("D5SkipBel","GLOBAL",1), then upon using it SetGlobal("D5SkipBel","GLOBAL",2)
Have Skie talk to you when you return, and go through the portal with you
If Darnas is there, him too.

[
- add a messenger to dead man's pass if bd_plot=393, saying that caelar's forces were marching out to smash the coalition before it could surround Dragonspear, the main coalition force was out in the field but a contingent of elite crusader fighters had broken off and were moving toward the camp, and Charname needs to get there ASAP!
	...and preclude travel to anywhere by the camp in such case?
	...just add travel triggers that are active if bd_plot=393?
]

- Bel's plan all along: turn caelar. just a little game he is playing to embarrass zariel.

----------------

Caelar wants to take over the Sword Coast, to suppress the chaos of the bhaalspawn; Belhifet wants to prevent Bhaal's return, since Bhaal was a powerful presence in the LE planes and Belhifet could gain from the power vacuum. Hephernaan has promised to introduce Caelar to Zariel, who is (lo and behold) Caelar's ancient ancestor. Caelar knows she doesn't have the power to truly control the Sword Coast, and while she doesn't want to use baatezu for that, she is convinced Zariel has been playing a long game and will support her.

[there needs to be a macguffin - caelar's soul wore down the walls between planes, and Heph's artifact crackled it open. Now the artifact can seal it again... but Caelar must remain on the other side, so that her blood won't continue to weaken the barrier.]
	- er, no. we have moved on from this idea.

*/


//___________________________________________________________________________________
//
// prep journal entries

/*
ADD_JOURNAL TITLE (~The Siege of Dragonspear~) ~The Siege of Dragonspear

Caelar Argent turned evil!!! But she is dead now. I must take the Dragonspear vault key back to Faerun so that the Coalition can close the portal.~ ~The Siege of Dragonspear

Caelar Argent resisted temptation and fought against Belhifet~ ~The Siege of Dragonspear

Caelar Argent is staying on the planes. She gave me the vault key. I must get it back to Faerun at once, so that the Coalition can close the portal.~ 
*/


//___________________________________________________________________________________
//
// amend cut scene w/ caelar and hood - bdcut11
// add new line w/ RESOLVE_STR_REF and then REPLACE_TEXTUALLY ~%eet_2%66071~ with it

OUTER_SET goal_string = RESOLVE_STR_REF (~My goals? Why, only to turn the tide of fate. Most are not aware, but a time of prophecy is upon us. This land is soon to be riven with chaos and strife. But I can prevent it.~)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut11.bcs~) BEGIN
  COPY_EXISTING ~bdcut11.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~%eet_2%66071~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%goal_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// change caelar's cut scene speech to followers
// set unattainable trigger for bdcaelar state 88
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5scry3a.d
ADD_STATE_TRIGGER bdcaelar 88 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~AreaCheck("bd0071")~ BEGIN d5caelar_a1
  SAY ~You know my name! You know where I come from. The Argents are a noble family, blessed long ago with an angel as our ancestor. The drive for justice flows in my veins! But, justice requires order.~
  IF ~~ GOTO d5caelar_a2
END

IF ~~ BEGIN d5caelar_a2
  SAY ~We live in dangerous times. Prophecy tells us that chaos is about to engulf the Sword Coast! The leaders of Waterdeep, Neverwinter, Baldur's Gate and Amn do not care! They care only for the flow of silver and gold into their coffers.~
  IF ~~ GOTO d5caelar_a3
END

IF ~~ BEGIN d5caelar_a3
  SAY ~Corruption has set in to these regimes - and corruption breeds chaos, and chaos brings death! In Baldur's Gate, a child of the dead Lord of Murder, Bhaal himself, was elevated to Grand Duke! He very nearly brought war to the entire region. The rulers of Baldur's Gate let that happen!~
  IF ~~ GOTO d5caelar_a4
END

IF ~~ BEGIN d5caelar_a4
  SAY ~Now more of Bhaal's Children are seeking power and death. We cannot let them succeed! Together, we can change the course of history! We can bring order to the land and make it inhospitable for the agents of murder and chaos!~
  IF ~~ GOTO d5caelar_a5
END

IF ~~ BEGIN d5caelar_a5
  SAY ~Follow me, and we will make this a land of justice and peace, where those who venerate death will have no place to hide! Follow me in this crusade, and we will fight the future itself!~
  IF ~~ 
    DO ~AddJournalEntry(%eet_2%64435,INFO) 
    	SetCutSceneLite(TRUE) 
    	StartCutSceneEx("bdscry04",FALSE)~ 
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5scry3a.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5scry3a.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5scry3a.d~ EVALUATE_BUFFER


//___________________________________________________________________________________
//
// takos' story about crusaders
// just rewrite the state

<<<<<<<< d5/d5takos.d
REPLACE_SAY BDTAKOS 4 ~I'll tell you my story if it means you'll go away. We used to get some... unsavory types here. Everyone was always pretty well-behaved - this is where they get their grog, see? But then this pack of clinking crusaders passed through, and just couldn't leave well enough alone. Acted like they were the law, started a fight - and next thing I know, the whole place is ablaze!~
>>>>>>>>
COPY ~d5/d5takos.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5takos.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5takos.d~


//___________________________________________________________________________________
//
// change dialogue at coast way crossing
// set unattainable trigger for bdcaelar state 10
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5coast.d
ADD_STATE_TRIGGER bdcaelar 10 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF WEIGHT #3 ~GlobalGT("bd_plot","global",160) GlobalLT("bd_plot","global",170) AreaCheck("bd1000")~ BEGIN d5caelar_b1
  SAY ~And so comes the hero of Baldur's Gate. Such as <PRO_HESHE> is. Many call you hero, but I know what you really are. There are few enough godspawn in Faerun - most are born of Bhaal. Am I right?~
  IF ~~ REPLY ~Yes, you are. What of it? I am my own person.~ DO ~SetGlobal("bd_plot","global",170)~ GOTO d5caelar_b2
  IF ~~ REPLY ~What I am is none of your business. What matters is that I am in the employ of the Grand Dukes.~ DO ~SetGlobal("bd_plot","global",170)~ GOTO d5caelar_b2
END

IF ~~ BEGIN d5caelar_b2
  SAY ~They call you hero but I have looked into your background.~
  IF ~~ GOTO d5caelar_b3
END

IF ~~ BEGIN d5caelar_b3
  SAY ~The first reports involve stirring up fights in bars and murdering rivals in the streets. You restored an iron mine for Amn, and destroyed the last functioning mine available to Baldur's Gate - in the process, desecrating an ancient dwarven gravesite. This bankrupted the local chapter of the Iron Throne... just as directors of other trading costers were being assassinated. You weakened the city, even as the drums of war grew louder.~
  IF ~~ REPLY ~Okay that… is actually a pretty fair description of what happened. But that was Sarevok's doing! I put a stop to his schemes.~ GOTO d5caelar_b4
END

IF ~~ BEGIN d5caelar_b4
  SAY ~You are kin to Sarevok. He who almost brought ruin to Baldur's Gate and Amn. I know what you are because unlike many, I have studied Alaundo's prophecy. Sarevok was but one of many Children of Bhaal! Their fate is set; their actions lead to chaos and death whether they mean it or not.~
  IF ~~ REPLY ~Oh, I assure you, Sarevok knew exactly what he was doing, and it was not just about chaos and death. You have no idea what I really stopped.~ GOTO d5caelar_b5
  IF ~~ REPLY ~Some, perhaps. Maybe even most. But there are plenty of agents of evil out there. Zhents, Red Wizards, Dragon Cultists, Cyricists... what matters is our choice to oppose such people. I have proven myself - you cannot judge me based solely on blood.~ GOTO d5caelar_b5
END

IF ~~ BEGIN d5caelar_b5
  SAY ~Judgment is immaterial. Sarevok's death is the spark that will ignite a new era of chaos on the Sword Coast. Alaundo did not hand down suppositions or predictions; he gave us prophecy! He saw fate! Hero you may be, but your approach is doomed regardless of intentions.~
  IF ~~ GOTO d5caelar_b6
END

IF ~~ BEGIN d5caelar_b6
  SAY ~You are reacting. "Adventuring." But you cannot avert what is to come by playing hero. Do you think fate can be changed so easily? No, it is beyond you. Your blood makes you the subject of prophecy - whereas I will be its master! I will BEND history to my will!~
  IF ~~ REPLY ~You may know what I am, Caelar, but I know what you are. A zealot driven by the whisperings of your own blood. At least I don't give in to mine.~ GOTO d5caelar_b7
  IF ~~ REPLY ~What makes you think you can do this, where others cannot? This sounds like delusion to me.~ GOTO d5caelar_b7
END

IF ~~ BEGIN d5caelar_b7
  SAY ~I have the power of the heavens within me. My line was blessed long ago by a Solar - an avenging angel ceaselessly fighting for good. I can bring to bear enough force to change the world. That is the only hope.~
  IF ~~ REPLY ~What force will you bring to bear? Why are you, of all people, based at Dragonspear with its ill history?~ GOTO d5caelar_b8
END

IF ~~ BEGIN d5caelar_b8
  SAY ~I will do what I must to subdue chaos. I will do what others cannot, or will not. The Grand Dukes let Sarevok take control of Baldur's Gate, however briefly. And now they send YOU as their champion. They have demonstrated their inability to contain the danger we face.~
  IF ~~ GOTO d5caelar_b9
END

IF ~~ BEGIN d5caelar_b9
  SAY ~Peace can only prevail if the Sword Coast is under my righteous authority. Go tell the Grand Dukes to submit to me. This transition can be bloodless. From Baldur's Gate, I will be able to marshal enough strength to bring the entire region under my control. Your Bhaal-kin will have no air to breathe, no chance to flourish. Countless lives will be saved.~
  IF ~~ REPLY ~You see things in absolutes only. Nobody's path is set in stone.~ GOTO d5caelar_b10
  IF ~~ REPLY ~What, everything thing will be fine if we just crown you queen? Sorry, I've heard this one before.~ GOTO d5caelar_b10
END

IF ~~ BEGIN d5caelar_b10
  SAY ~Only I see things as they really are! Consider, clerics of gods both good an evil are in both armies. The gods do not know the righteous path any more than most mortals. That is the effect of chaos! The only way through this time is with clarity, and a strong arm. Only I have both.~
  IF ~~ REPLY ~Sarevok wanted war, and we narrowly averted it. And now, you have brought it upon us anyway! Even after his death, you are giving him what he wanted all along!~ GOTO d5caelar_b11
END

IF ~~ BEGIN d5caelar_b11
  SAY ~This crossing is destroyed. My forces are already on the way to take Bridgefort and Boareskyr Bridge. If you truly value peace, stand down your troops and surrender yourself to me there. If not, I will march to Baldur's Gate and root out you and your kin by whatever means necessary. Order WILL prevail. It is the only way.~
  IF ~~ GOTO 34
END

END
>>>>>>>>
COPY ~d5/d5coast.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5coast.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5coast.d~


//___________________________________________________________________________________
//
// rewrite cut scene w/ condolence letters
// write out new cut scene script
// preserve all technical aspects of the original
// mention the 'treatments' working, she feels empowered

<<<<<<<< d5/bdcut20a.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		StorePartyLocations()
		FadeToColor([0.0],0)
		SmallWait(5)
		ApplySpellRES("bdhide",Player1)  // No such index
		ApplySpellRES("bdhide",Player2)  // No such index
		ApplySpellRES("bdhide",Player3)  // No such index
		ApplySpellRES("bdhide",Player4)  // No such index
		ApplySpellRES("bdhide",Player5)  // No such index
		ApplySpellRES("bdhide",Player6)  // No such index
		ActionOverride(Player2,LeaveAreaLUA("bd4100","",[2120.1120],S))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player3,LeaveAreaLUA("bd4100","",[2055.1145],S))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player4,LeaveAreaLUA("bd4100","",[2085.1145],S))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player5,LeaveAreaLUA("bd4100","",[2020.1170],S))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player6,LeaveAreaLUA("bd4100","",[2055.1170],S))  // Dragonspear Castle Keep, First Floor
		LeaveAreaLUAPanic("bd4100","",[2085.1120],S)  // Dragonspear Castle Keep, First Floor
		LeaveAreaLUA("bd4100","",[2085.1120],S)  // Dragonspear Castle Keep, First Floor
		MultiPlayerSync()
		MoveViewPoint([2000.1090],INSTANT)
		CreateCreature("BDSERV",[1960.1110],NW)  // Servant
		CreateCreature("BDCAELAR",[2070.1060],NE)  // Caelar Argent
		SmallWait(1)
		Explore()
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",20)
		SetCutSceneBreakable(TRUE)
		SmallWait(10)
		FadeFromColor([20.0],0)
		SmallWait(10)
		DisplayStringWait("BDCAELAR",%eet_2%66627)  // ...I am sorry for your loss, but rest assured that your husband’s sacrifice will never be forgotten. Signed... Yours ever in faith, Caelar Argent.
		SmallWait(15)
		CreateCreature("BDHEPH2",[1735.835],SW)  // Hephernaan
		SmallWait(20)
		ActionOverride("BDHEPH2",MoveToPoint([1865.1060]))
		SmallWait(45)
		ActionOverride("BDSERV",FaceObject("BDHEPH2"))  // Hephernaan
		ActionOverride("BDCAELAR",FaceObject("BDHEPH2"))  // Hephernaan
		DisplayStringWait("BDHEPH2",~More letters of condolence, my lady? Surely they can wait. It is time for your treatments.~)
		SmallWait(5)
		ActionOverride("BDHEPH2",Face(E))
		DisplayStringWait("BDCAELAR",~I will not argue this again, Hephernaan. The condolences are as important as anything else.~)
		Wait(1)
		DisplayStringWait("BDHEPH2",~Of course. But we have made much progress. Your celestial blood is positively singing out to its origin.~)
		ActionOverride("BDSERV",FaceObject("BDCAELAR"))  // Caelar Argent
		SmallWait(10)
		DisplayStringWait("BDCAELAR",~Yes, I can feel it. I am stronger every day. And... you have been able to use it to contact my ancestor?~)
		SmallWait(10)
		DisplayStringWait("BDHEPH2",~Oh yes. The link is quite strong. Soon you will as well, and we will forge the alliance that can wipe away your opposition.~)
		ActionOverride("BDSERV",FaceObject("BDCAELAR"))  // Caelar Argent
		SmallWait(10)
		DisplayStringWait("BDCAELAR",~Yes... yes. Then we will implement my peace across the land.~)
		PlaySound("cas_p07n")
		SmallWait(2)
		ActionOverride("BDCAELAR",SetSequence(SEQ_CONJURE))
		Wait(1)
		ActionOverride("BDCAELAR",SetSequence(SEQ_CAST))
		SmallWait(5)
		CreateVisualEffectObject("ICPRAYI","BDCAELAR")  // Caelar Argent
		ApplySpellRES("bdcpulse","BDCAELAR")  // <NO TEXT>
		CreateVisualEffectObject("ICPRAYI","BDHEPH2")  // Hephernaan
		ApplySpellRES("bdcpulse","BDHEPH2")  // <NO TEXT>
		CreateVisualEffectObject("ICPRAYI","BDSERV")  // Servant
		ApplySpellRES("bdcpulse","BDSERV")  // <NO TEXT>
		Wait(1)
		DisplayStringWait("BDCAELAR",%eet_2%66633)  // Gather the faithful. We have much to do.
		SmallWait(10)
		FadeToColor([20.0],0)
		SmallWait(10)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		UndoExplore()
		ActionOverride("BDCAELAR",DestroySelf())
		ActionOverride("BDHEPH2",DestroySelf())
		ActionOverride("BDSERV",DestroySelf())
		SmallWait(5)
		SmallWait(10)
		RestorePartyLocations()
		MultiPlayerSync()
		ApplySpellRES("bdunhide",Player1)  // No such index
		ApplySpellRES("bdunhide",Player2)  // No such index
		ApplySpellRES("bdunhide",Player3)  // No such index
		ApplySpellRES("bdunhide",Player4)  // No such index
		ApplySpellRES("bdunhide",Player5)  // No such index
		ApplySpellRES("bdunhide",Player6)  // No such index
		SmallWait(7)
		SetGlobalTimer("bd_bence_delay","bd7100",ONE_ROUND)  // Troll Forest
		FadeFromColor([20.0],0)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/bdcut20a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut20a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut20a.baf~ EVALUATE_BUFFER 


//___________________________________________________________________________________
//
// rewrite cut scene w/ caelar and ashatiel
// write out new cut scene script
// preserve all technical aspects of the original

OUTER_SET ca_string_1 = RESOLVE_STR_REF (~The report about Boareskyr Bridge is troubling. I hoped this Bhaal-child was truly a hero, but the uncontrolled manifestation reinforces all my fears.~)
OUTER_SET ca_string_2 = RESOLVE_STR_REF (~Mere presence would not cause such an event. The area had to have been ritually prepared ahead of time! <CHARNAME> cannot be trusted!~)
OUTER_SET ca_string_3 = RESOLVE_STR_REF (~I am not sure. Sometimes, apparently, blood such as ours calls out to its origin.~)
OUTER_SET ca_string_4 = RESOLVE_STR_REF (~Caelar... do you really think Zariel will answer you? This is a dangerous gambit. But, if she joins us...~)
OUTER_SET ca_string_5 = RESOLVE_STR_REF (~It will shake both the heavens and the earth. Like us she is a crusader against chaos. She must join us. She will answer her kin!~)
OUTER_SET ca_string_6 = RESOLVE_STR_REF (~And the godspawn? <PRO_HESHE> must answer for what happened to my brother.~)
OUTER_SET ca_string_7 = RESOLVE_STR_REF (~You will do what you must. <PRO_HESHE> had a chance to be reasonable; now we are beyond that point. Hero or not, <PRO_HESHE> must be put down like the rest.~)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut30a.bcs~) BEGIN
  COPY_EXISTING ~bdcut30a.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~%eet_2%66080~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_1%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66079~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_1%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66081~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_2%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66082~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_3%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66083~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_4%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66084~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_5%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66085~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_6%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~%eet_2%66086~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%ca_string_7%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
// less moving around
		SPRINT textToReplace ~\(3626.1127\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~3576.1236~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(3679.1169\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~3577.1237~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// remove Belben from DS basement

<<<<<<<< d5/d5belbo.baf
IF
	GlobalLT("chapter","global",11)
	AreaCheck("bd4300")  // Dragonspear Castle Basement
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>
COPY ~d5/d5belbo.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belbo.baf~
EXTEND_TOP ~bdbelben.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belbo.baf~

<<<<<<<< d5/d54300_1.baf
IF
	GlobalLT("bd_plot","global",350)
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",350)
		Continue()
END
>>>>>>>>
COPY ~d5/d54300_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_1.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_1.baf~


//___________________________________________________________________________________
//
// change dialogue between hephernaan and belhifet's voice
// set unattainable trigger for bdhepher state 1
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated

<<<<<<<< d5/d5hefbel.d
ADD_STATE_TRIGGER BDHEPHER 1 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER BDHEPHER 8 ~GlobalGT("D5Intercession","BD4300",0)~
ADD_STATE_TRIGGER BDHEPHER 8 ~GlobalGT("D5Intercession","BD4300",0)~

APPEND BDHEPHER

IF WEIGHT #-1 ~AreaCheck("BD4300") GlobalLT("bd_plot","GLOBAL",360)~ BEGIN d5heph_b1
  SAY ~...work proceeds, master. Each time we perform the ritual, we disentangle one of the threads in the knot that makes up the planar barrier. Its design is quite complex. If we move too fast, the feedback could kill Caelar. The perils of working with mortals, I am afraid. They are quite fragile.~
  IF ~~ GOTO d5heph_b2
END

IF ~~ BEGIN d5heph_b2
  SAY ~Perhaps if we had the Bhaal Child's blood as well...~
  IF ~~ EXTERN BDVOICE d5bel_b1
END

IF ~~ BEGIN d5heph_b3
  SAY ~He escaped my agents, master. We are not sure how. Someone must have known of our approach and intercepted him. I suspect a plot by the Exile...~
  IF ~~ EXTERN BDVOICE d5bel_b2
END

IF ~~ BEGIN d5heph_b4
  SAY ~I do not need help from that snake! I will take care of the Bhaalspawn myself!~
  IF ~~ EXTERN BDVOICE d5bel_b4
END

IF ~~ BEGIN d5heph_b5
  SAY ~Of course, master.~
  IF ~~ GOTO d5heph_b6
END

IF ~~ BEGIN d5heph_b6
  SAY ~Hello, <CHARNAME>. I am afraid you have ventured too far. I thought our little supply station might attract you. Impossible for an army to utilize, but irresistible for an adventurer.~
  IF ~~ REPLY ~I heard you. I know what you are planning. You are just using Caelar!~ GOTO d5heph_b7
  IF ~~ REPLY ~Well, I'm here now, and soon you won't be!~ GOTO d5heph_b8
END

IF ~~ BEGIN d5heph_b7
  SAY ~Yes, yes. And now you will take that knowledge to the next life. There is an army of crusaders above you and below you. You won't escape.~
  IF ~~ GOTO d5heph_b9
END

IF ~~ BEGIN d5heph_b8
  SAY ~We will see about that. Now I will end you. The horde of crusaders above and below you will ensure it.~
  IF ~~ GOTO d5heph_b9
END

IF ~~ BEGIN d5heph_b9
  SAY ~Come now, and feel true power! This is your end!~
  IF ~~ DO ~SetGlobal("D5Intercession","BD4300",1)
  			ActionOverride("bdhepher",Enemy())
			ActionOverride("bdesseri",Enemy())
			ActionOverride("bdolvena",Enemy())
			ActionOverride("cutspy",DestroySelf())
			ActionOverride("bdhepher",ForceSpell(Myself,"WIZARD_ORACLE"))
			AddJournalEntry(%eet_2%59795,QUEST)~ 
  		EXIT
END

END

APPEND BDVOICE

IF ~~ BEGIN d5bel_b1
  SAY ~No - the godspawn is useless for this. Bhaal is a memory, dead and mouldering in the Astral backwash. <CHARNAME>'s blood calls to nothing but the prophecy. You should have obtained the other Argent!~
  IF ~~ EXTERN BDHEPHER d5heph_b3
END

IF ~~ BEGIN d5bel_b2
  SAY ~You see plots everywhere. Never mind the Exile. He is a child chasing shadows. No such shadows will remain to sustain him once my fires burn across the land.~
  IF ~~ GOTO d5bel_b3
END

IF ~~ BEGIN d5bel_b3
  SAY ~Continue the work. Open the door. Subdue the godspawn, but be careful. They are a chaos agent. I am going to send Illaruel to secure the door once it is open.~
  IF ~~ EXTERN BDHEPHER d5heph_b4
END

IF ~~ BEGIN d5bel_b4
  SAY ~Youngling, you have much to learn about fighting chaos. If you think to handle the godspawn, then do so now! Turn around and face your task, and do not fail me!~
  IF ~~ EXTERN BDHEPHER d5heph_b5
END

END
>>>>>>>>
COPY ~d5/d5hefbel.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5hefbel.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5hefbel.d~ EVALUATE_BUFFER

<<<<<<<< d5/d54300_2.baf
IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdolvena")
THEN
	RESPONSE #100
		ApplySpell("bdolvena","WIZARD_SPELL_STRIKE")
		Continue()
END

IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdesseri")
THEN
	RESPONSE #100
		Wait(1)
		ApplySpell("bdesseri","WIZARD_SPELL_STRIKE")
		Continue()
END

IF
	!GlobalTimerNotExpired("D5IntercessionTimer","BD4300")
	Global("D5Intercession","BD4300",2)
	!Dead("bdhepher")
THEN
	RESPONSE #100
		Wait(2)
		ApplySpell("bdhepher","WIZARD_SPELL_STRIKE")
		SetGlobal("D5Intercession","BD4300",3)
		DisplayStringHead("bdhepher",~What! Who dares??~) 
		Continue()
END

IF
	Global("D5Intercession","BD4300",1)
THEN
	RESPONSE #100
		SetGlobal("D5Intercession","BD4300",2)
		SetGlobalTimer("D5IntercessionTimer","BD5300",21)
		Continue()
END
>>>>>>>>
COPY ~d5/d54300_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_2.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_2.baf~


//___________________________________________________________________________________
//
// adjustments to Daeros' expository dialogue

<<<<<<<< d5/d5daero+.d
REPLACE_SAY BDDAEROS 2 ~He plays the pious, kindhearted priest, but he's trying to open a gate to the Nine Hells, the snake. I first saw Hephernaan when he came down here with that aasimar, what's her name—Caelar. You know what an aasimar is? Celestial-blooded, you could say. They need particular blood to open the gate, but Caelar's isn't strong enough to do it quickly. He has had to perform rituals with her every day to slowly pry the portal open.~

REPLACE_SAY BDDAEROS 3 ~Hephernaan has bee frustrated. I saw him use that altar there to speak with some dark creature - possibly he is afraid of failing his master. I was trying to listen in, but he spotted me and bound me right quick. I've been trapped ever since.~

ADD_TRANS_TRIGGER BDDAEROS 2 ~FALSE()~ DO 1

ADD_TRANS_TRIGGER BDDAEROS 3 ~FALSE()~ DO 0
>>>>>>>>
COPY ~d5/d5daero+.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5daero+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5daero+.d~


//___________________________________________________________________________________
//
// remove parlay scene
// once you've been in the underground river, set a variable
// then conditionally set bd_plot to 393 in river exterior
/*
 - ***** add a messenger to dead man's pass if bd_plot=393, saying that caelar's forces were marching out to smash the coalition before it could surround Dragonspear, the main coalition force was out in the field but a contingent of elite crusader fighters had broken off and were moving toward the camp, and Charname needs to get there ASAP!
	...and preclude travel to anywhere but the camp in such case?
	...just add travel triggers that are active if bd_plot=393?
*/
<<<<<<<< d5/d5bd5100.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",1)
		Continue()
END
>>>>>>>>
COPY ~d5/d5bd5100.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5100.baf~
EXTEND_TOP ~bd5100.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5100.baf~

COPY_EXISTING ~BD5000.are~ ~override~
 /* add travel region that links to dead man's pass */
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2 	//travel - kind of trigger area
    fj_box_left     = 1 	// x coordinate
    fj_box_top      = 1024 	// y coordinate
    fj_box_right    = 30 	// x coordinate
    fj_box_bottom   = 3839 	// y coordinate
    fj_cursor_idx   = 30   	//door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 1 + (1024 << 16) 
    fj_vertex_1     = 30 + (1024 << 16)
    fj_vertex_2     = 30 + (3839 << 16)
    fj_vertex_3     = 1 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300a 	//name of this travel region so you can address it via script etc.
    fj_destination_area = BD7300 	//name of the destination area
    fj_destination_name = d5bd5000 	//name of exitpoint in destination area
  END

  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2 	//travel - kind of trigger area
    fj_box_left     = 31 	// x coordinate
    fj_box_top      = 3809 	// y coordinate
    fj_box_right    = 5188 	// x coordinate
    fj_box_bottom   = 3839 	// y coordinate
    fj_cursor_idx   = 30   	//door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 31 + (3809 << 16) 
    fj_vertex_1     = 5188 + (3809 << 16)
    fj_vertex_2     = 5188 + (3839 << 16)
    fj_vertex_3     = 31 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300b 	//name of this travel region so you can address it via script etc.
    fj_destination_area = BD7300 	//name of the destination area
    fj_destination_name = d5bd5000 	//name of exitpoint in destination area
  END

  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2 	//travel - kind of trigger area
    fj_box_left     = 5089 	// x coordinate
    fj_box_top      = 1105 	// y coordinate
    fj_box_right    = 5119 	// x coordinate
    fj_box_bottom   = 3839 	// y coordinate
    fj_cursor_idx   = 30   	//door - mouse cursor symbol
    fj_flags        = 4
    fj_vertex_0     = 5089 + (1105 << 16) 
    fj_vertex_1     = 5119 + (1105 << 16)
    fj_vertex_2     = 5119 + (3839 << 16)
    fj_vertex_3     = 5089 + (3839 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = bd5000_bd7300c 	//name of this travel region so you can address it via script etc.
    fj_destination_area = BD7300 	//name of the destination area
    fj_destination_name = d5bd5000 	//name of exitpoint in destination area
  END
BUT_ONLY

COPY_EXISTING ~BD7300.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 4900 	// x coordinate where the group will spawn
    fj_loc_y       = 600 	// y coordinate where the group will spawn
    fj_orientation = 4 	// W face direction in which group is looking after coming through
    STR_VAR
    fj_structure_type = entrance
    fj_name           = d5bd5000 // name of this new exitpoint so it can be referenced for the travel region of the other area
  END
BUT_ONLY 

<<<<<<<< d5/d5bd5000.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",1)
	Global("D5BD5000toBD7300","BD5000",0)
THEN
	RESPONSE #100
		SetGlobal("D5BD5000toBD7300","BD5000",1)
		TriggerActivation("bd5000_bd7300b",TRUE)
		TriggerActivation("bd5000_bd7300b",TRUE)
		TriggerActivation("bd5000_bd7300c",TRUE)
END

IF
	Global("D5TakeMeToTheRiver","GLOBAL",2)
	Global("D5BD5000toBD7300","BD5000",1)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",3)
		TriggerActivation("bd5000_bd7300b",FALSE)
		TriggerActivation("bd5000_bd7300b",FALSE)
		TriggerActivation("bd5000_bd7300c",FALSE)
END
>>>>>>>> 
COPY ~d5/d5bd5000.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5000.baf~
EXTEND_TOP ~bd5000.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd5000.baf~


<<<<<<<< d5/d5bd7300.baf
IF
	Global("D5TakeMeToTheRiver","GLOBAL",1)
	GlobalLT("bd_plot","GLOBAL",393)
THEN
	RESPONSE #100
		SetGlobal("D5TakeMeToTheRiver","GLOBAL",2)
		SetGlobal("bd_plot","GLOBAL",393)
		Continue()
END
>>>>>>>>
COPY ~d5/d5bd7300.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd7300.baf~
EXTEND_TOP ~bd7300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd7300.baf~


//___________________________________________________________________________________
//
// new portal cut scene w/ just Heph & Caelar & Crusaders
// set unattainable trigger for bdcaelar state 44
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated
/*
have someone comment it's weird she is not there leading the troops (maybe bddelanc #71?)
replace the proximity cut scene bddsgat2.bcs
new dialogue between caelar & heph
then back to opening the inner gate via a new cut scene
*/
<<<<<<<< d5/bddsgat2.baf
IF
	Clicked([ANYONE])
	!Range(LastTrigger,12)
THEN
	RESPONSE #100
		DisplayString(Myself,38224)  // You are too far away to use that.
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,12)
	Global("bd_inner_gate_ot","bd4000",0)  // Dragonspear Castle, Exterior
THEN
	RESPONSE #100
		SetGlobal("bd_inner_gate_ot","bd4000",1)  // Dragonspear Castle, Exterior
		DisplayString(Myself,40901)  // These sturdy gates are very likely to hold off any assault conducted without the use of heavy siege machines. Or a good deal of explosive material.
END

IF
	GlobalLT("bd_plot","global",450)
	GlobalTimerExpired("bd_caelar_delay","bd4000")  // Dragonspear Castle, Exterior
	Dead("bdashati")  // Ashatiel
	TriggerOverride("gate_proxy",Range([PC],23))
	!TriggerOverride("bddelanc",Detect([PC])) 
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("bd_plot","global",450)
		StartCutSceneMode()
		FadeToColor([20.0],0)
		StartCutSceneEx("d5port1",TRUE)
END

IF
	GlobalLT("bd_plot","global",450)
	GlobalTimerExpired("bd_caelar_delay","bd4000")  // Dragonspear Castle, Exterior
	Dead("bdashati")  // Ashatiel
	TriggerOverride("gate_proxy",Range([PC],23))
	TriggerOverride("bddelanc",Detect([PC])) 
THEN
	RESPONSE #100
		ActionOverride("bddelanc",StartDialogueNoSet(Player1))
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,12)
THEN
	RESPONSE #100
		DisplayString(Myself,40901)  // These sturdy gates are very likely to hold off any assault conducted without the use of heavy siege machines. Or a good deal of explosive material.
END
>>>>>>>>
COPY ~d5/bddsgat2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bddsgat2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bddsgat2.baf~

<<<<<<<< d5/d5port1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ClearAllActions()
		StorePartyLocations()
		SmallWait(5)
		ApplySpellRES("bdhide",Player1)  // No such index
		ApplySpellRES("bdhide",Player2)  // No such index
		ApplySpellRES("bdhide",Player3)  // No such index
		ApplySpellRES("bdhide",Player4)  // No such index
		ApplySpellRES("bdhide",Player5)  // No such index
		ApplySpellRES("bdhide",Player6)  // No such index
		ActionOverride(Player2,LeaveAreaLUA("bd4300","",[815.545],S))
		ActionOverride(Player3,LeaveAreaLUA("bd4300","",[845.515],S))
		ActionOverride(Player4,LeaveAreaLUA("bd4300","",[845.545],S))
		ActionOverride(Player5,LeaveAreaLUA("bd4300","",[875.545],S))
		ActionOverride(Player6,LeaveAreaLUA("bd4300","",[845.575],S))
		LeaveAreaLUAPanic("bd4300","",[815.515],S)
		LeaveAreaLUA("bd4300","",[815.515],S)
		MultiPlayerSync()
		MoveViewPoint([615.420],INSTANT)
		CreateCreature("bdcaelar",[520.400],SE)  // Caelar Argent
		CreateCreature("bdhepher",[720.470],S)  // Hephernaan
		SmallWait(5)
		CreateCreature("bdcrue50",[440.520],NE)  // Crusader Elite
		CreateCreature("bdcrue53",[470.295],SE)  // Crusader Elite
		CreateCreature("bdlieutg",[745.300],SW)  // Crusader Elite
		CreateCreature("bdlieutc",[340.395],E)  // Crusader Elite
		CreateCreature("bdlieute",[505.220],S)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdlieuta",[500.570],NE)  // Caelar's Lieutenant
		CreateCreature("bdcrue52",[425.340],SE)  // Crusader Elite
		CreateCreature("bdcrue55",[645.270],S)  // Crusader Elite
		CreateCreature("bdlieutd",[390.280],SE)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdcrue51",[410.435],E)  // Crusader Elite
		CreateCreature("bdcrue54",[550.255],S)  // Crusader Elite
		CreateCreature("bdlieutb",[375.520],NE)  // Caelar's Lieutenant
		CreateCreature("bdlieutf",[645.230],S)  // Crusader Elite
		SmallWait(1)
		Explore()
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SmallWait(10)
		FadeFromColor([20.0],0)
		SmallWait(10)
		ActionOverride("bdcaelar",SetSequence(SEQ_READY))
		DisplayStringWait("BDCAELAR",~They are at the gates, Hephernaan. We must perform the summoning now! You said Zariel would answer my call!~)
		SmallWait(45)
		ActionOverride("bdhepher",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5port1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5port1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5port1.baf~

<<<<<<<< d5/d5port2.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetAreaScript("cutskip",OVERRIDE)
		ActionOverride("bdhepher",ForceSpellRES("bdhepher",Myself))
		ActionOverride("bdhepher",MoveToPoint([590.400]))
		SmallWait(23)
		ReallyForceSpellRES("bdglowgr","bdlieuta")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue50")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue51")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue52")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue53")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue54")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue55")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutg")  // No such index
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		SmallWait(3)
		ReallyForceSpellRES("bdglowgr","bdlieutb")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutc")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutd")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieute")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutf")  // No such index
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		ActionOverride("bdhepher",Face(W))
		SmallWait(25)
		ActionOverride("bdhepher",SetSequence(SEQ_CAST))
		SmallWait(5)
		CreateVisualEffectObject("spfleshs","bdhepher")
		SmallWait(25)
		CreateVisualEffect("bdwardgr",[550.360])
		PlaySound("EFF_M08")
		SmallWait(10)
		PlaySound("EFF_P13")
		SmallWait(2)
		CreateVisualEffect("spcallli",[615.415])
		ScreenShake([15.15],10)
		PlaySound("EFF_M13")
		SmallWait(10)
		ActionOverride("bdcaelar",SetSequence(SEQ_DAMAGE))
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(SEQ_DIE))
		DisplayStringWait("bdhepher",~Goodbye Caelar, I go to meet my master. Feel free to follow me, if you can.~)
		SmallWait(35)
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		CreateVisualEffect("spfleshs",[615.415])
		SmallWait(5)
		AmbientActivate("Portal_Activate",TRUE)
		SmallWait(5)
		CreateVisualEffect("spfirepi",[615.415])
		SmallWait(5)
		AmbientActivate("glyph1",TRUE)
		AmbientActivate("glyph2",TRUE)
		AmbientActivate("glyph3",TRUE)
		AmbientActivate("glyph4",TRUE)
		AmbientActivate("glyph5",TRUE)
		AmbientActivate("glyph6",TRUE)
		AmbientActivate("glyph7",TRUE)
		AmbientActivate("glyph8",TRUE)
		AmbientActivate("glyph9",TRUE)
		AmbientActivate("glyph10",TRUE)
		AmbientActivate("glyph11",TRUE)
		AmbientActivate("glyph12",TRUE)
		AmbientActivate("glyph13",TRUE)
		AmbientActivate("glyph14",TRUE)
		AmbientActivate("glyph15",TRUE)
		AmbientActivate("glyph16",TRUE)
		SmallWait(10)
		OpenDoor("Door04")
		AmbientActivate("portal_visuals",TRUE)
		AmbientActivate("portal_webm",TRUE)
		AmbientActivate("Portal_Activate",FALSE)
		SmallWait(5)
		AmbientActivate("glyph1",FALSE)
		AmbientActivate("glyph2",FALSE)
		AmbientActivate("glyph3",FALSE)
		AmbientActivate("glyph4",FALSE)
		AmbientActivate("glyph5",FALSE)
		AmbientActivate("glyph6",FALSE)
		AmbientActivate("glyph7",FALSE)
		AmbientActivate("glyph8",FALSE)
		AmbientActivate("glyph9",FALSE)
		AmbientActivate("glyph10",FALSE)
		AmbientActivate("glyph11",FALSE)
		AmbientActivate("glyph12",FALSE)
		AmbientActivate("glyph13",FALSE)
		AmbientActivate("glyph14",FALSE)
		AmbientActivate("glyph15",FALSE)
		AmbientActivate("glyph16",FALSE)
		SmallWait(5)
		ActionOverride("bdlieuta",SetSequence(SEQ_READY))
		ActionOverride("bdcrue50",SetSequence(SEQ_READY))
		ActionOverride("bdcrue51",SetSequence(SEQ_READY))
		ActionOverride("bdcrue52",SetSequence(SEQ_READY))
		ActionOverride("bdcrue53",SetSequence(SEQ_READY))
		ActionOverride("bdcrue54",SetSequence(SEQ_READY))
		ActionOverride("bdcrue55",SetSequence(SEQ_READY))
		ActionOverride("bdlieutg",SetSequence(SEQ_READY))
		ActionOverride("bdlieutb",SetSequence(SEQ_READY))
		ActionOverride("bdlieutc",SetSequence(SEQ_READY))
		ActionOverride("bdlieutd",SetSequence(SEQ_READY))
		ActionOverride("bdlieute",SetSequence(SEQ_READY))
		ActionOverride("bdlieutf",SetSequence(SEQ_READY))
		ActionOverride("bdhepher",MoveToPoint([630.430]))
		SmallWait(6)
		CreateVisualEffectObject("shair","bdhepher")
		ActionOverride("bdhepher",DestroySelf())
		FadeToColor([20.0],0)
		SmallWait(10)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SmallWait(5)
		UndoExplore()
		SmallWait(10)
		RestorePartyLocations()
		MultiPlayerSync()
		ApplySpellRES("bdunhide",Player1)  // No such index
		ApplySpellRES("bdunhide",Player2)  // No such index
		ApplySpellRES("bdunhide",Player3)  // No such index
		ApplySpellRES("bdunhide",Player4)  // No such index
		ApplySpellRES("bdunhide",Player5)  // No such index
		ApplySpellRES("bdunhide",Player6)  // No such index
		SetGlobal("D5BackToGate","BD4000",1)
		FadeFromColor([20.0],0)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5port2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5port2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5port2.baf~

<<<<<<<< d5/d5bd4000.baf
IF
	Global("D5BackToGate","BD4000",1)
THEN
	RESPONSE #100
		SetGlobal("D5BackToGate","BD4000",2)
		MoveViewPoint([2680.1885],INSTANT)
		CreateCreature("bdcutid",[2795.1790],S)  // No such index
		CreateCreature("bdphosse",[2380.2050],NE)  // Phossey Dugdeep
		ClearAllActions()
		StartCutSceneMode()
		StartCutSceneEx("bdcut43",TRUE)
END
>>>>>>>>
COPY ~d5/d5bd4000.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd4000.baf~
EXTEND_TOP ~bd4000.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5bd4000.baf~

<<<<<<<< d5/bdcut43.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		SetGlobal("bd_cant_rest","myarea",0)
		SmallWait(5)
		CreateCreature("cutspy",[2680.1885],S)  // No such index
		ActionOverride("bdphosse",MoveToPoint([2760.1845]))
		SmallWait(30)
END

IF
	GlobalGT("bd_wall_explosion","bd4000",0)  // Dragonspear Castle, Exterior
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		DisplayStringHead("bdphosse",%eet_2%38366)  // This one'll be a touch easier—fewer arrows flying at my bloody head!
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		SmallWait(35)
		ActionOverride("bdphosse",Face(NE))
		ActionOverride("bdphosse",SetSequence(SEQ_CONJURE))
		SmallWait(45)
		ActionOverride("bdphosse",SetSequence(SEQ_READY))
		SmallWait(10)
		ActionOverride("bdphosse",MoveToPoint([2525.2005]))
		SmallWait(35)
		ActionOverride("bdphosse",Face(NE))
		SmallWait(20)
		ScreenShake([100.100],10)
		CreateVisualEffect("bdFLAST1",[2750.1785])
		CreateVisualEffect("SPFLAMES",[2765.1830])
		CreateVisualEffect("bdFLAST1",[2825.1825])
		CreateVisualEffect("SPFLAMES",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		PlaySound("EFF_M36B")
		SmallWait(3)
		ActionOverride("dsc_gate_inner",ReallyForceSpellPoint([2795.1800],WIZARD_FIREBALL))  // SPWI304.SPL (Fireball)
		SmallWait(2)
		CreateVisualEffect("SPFLAMES",[2750.1785])
		CreateVisualEffect("bdFLAST1",[2765.1830])
		CreateVisualEffect("SPFLAMES",[2825.1825])
		CreateVisualEffect("bdFLAST1",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		CreateVisualEffect("SPFLAST2",[3155.3430])
		PlaySound("EFF_M36B")
		SmallWait(3)
		ActionOverride("dsc_gate_inner",ReallyForceSpellPoint([2795.1800],WIZARD_FIREBALL))  // SPWI304.SPL (Fireball)
		Unlock("door01")
		SmallWait(2)
		OpenDoor("door01")
		ScreenShake([100.100],10)
		CreateVisualEffect("bdFLAST1",[2750.1785])
		CreateVisualEffect("SPFLAMES",[2765.1830])
		CreateVisualEffect("bdFLAST1",[2825.1825])
		CreateVisualEffect("SPFLAMES",[2810.1765])
		CreateVisualEffect("SPFIREPI",[2795.1800])
		PlaySound("EFF_M36B")
		SmallWait(5)
		CreateVisualEffect("SPFLAMES",[2750.1785])
		CreateVisualEffect("bdFLAST1",[2765.1830])
		CreateVisualEffect("SPFLAMES",[2825.1825])
		CreateVisualEffect("bdFLAST1",[2810.1765])
		SmallWait(10)
		TriggerActivation("dsc_gate_inner",FALSE)
		DisplayStringHead("bdphosse",%eet_2%38367)  // All right boys and girls, have at 'em. I'll be back at the camp drinking myself into a stupor. *hic* Haha!
		SmallWait(50)
		ActionOverride("bdphosse",MoveToObject("courtyard"))
		SmallWait(30)
		ActionOverride("bddelanc",SetGlobal("bd_delancie_moves","locals",2))
		ActionOverride("bdbence",MoveToObject("inner_courtyard"))
		ActionOverride("bdnederl",MoveToObject("inner_courtyard"))
		ActionOverride("bddelanc",MoveToPoint([3030.1465]))
		ActionOverride("bdsorali",MoveToPoint([2925.1430]))
		ActionOverride("bdwarma1",MoveToPoint([2965.1395]))
		ActionOverride("bdwarma2",MoveToPoint([3035.1380]))
		ActionOverride("bdwarma3",MoveToPoint([3095.1400]))
		ActionOverride("bddosia",MoveToPoint([3130.1445]))
		ActionOverride("bdhensle",MoveToPoint([3140.1490]))
		ActionOverride("bdstoneh",MoveToPoint([4300.975]))
		SmallWait(20)
		FadeToColor([20.0],0)
		SmallWait(10)
		ActionOverride("bdphosse",DestroySelf())
		SmallWait(15)
		ActionOverride("cutspy",DestroySelf())
		ActionOverride("cutspy2",DestroySelf())
		ActionOverride("bdbence",DestroySelf())
		ActionOverride("bdnederl",DestroySelf())
		ActionOverride("bddelanc",JumpToPoint([3030.1465]))
		ActionOverride("bddelanc",Face(S))
		ActionOverride("bddelanc",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
		CreateCreature("bddagoff",[4325.1025],W)  // Daggerford Officer
		CreateCreature("bdwtrx37",[1850.2285],NW)  // Waterdhavian Sergeant
		CreateCreature("bdwtr39",[950.2550],NE)  // Waterdhavian Soldier
		CreateCreature("bdwtr37",[1590.2885],E)  // Waterdhavian Soldier
		CreateCreature("bdfist35",[2005.1685],NW)  // Flaming Fist Mercenary
		CreateCreature("bdwtr35",[2405.1865],S)  // Waterdhavian Soldier
		CreateCreature("bdwtr38",[2440.2260],NW)  // Waterdhavian Soldier
		CreateCreature("bdwtr35",[2805.1170],NW)  // Waterdhavian Soldier
		CreateCreature("bdwtr36",[2965.1120],N)  // Waterdhavian Soldier
		CreateCreature("bdwtrx36",[2690.757],SE)  // Waterdhavian Sergeant
		CreateCreature("bddagf37",[2790.735],S)  // Daggerford Militia
		CreateCreature("bdfist35",[2980.675],NE)  // Flaming Fist Mercenary
		CreateCreature("bdwtr38",[3015.735],NE)  // Waterdhavian Soldier
		CreateCreature("bdfist36",[3380.1730],SW)  // Flaming Fist Mercenary
		CreateCreature("bdwtr37",[3435.1755],S)  // Waterdhavian Soldier
		CreateCreature("bddagf38",[3810.2070],NW)  // Daggerford Militia
		CreateCreature("bdfisx35",[3860.2040],N)  // Flaming Fist Sergeant
		CreateCreature("bdfist38",[3905.2055],NE)  // Flaming Fist Mercenary
		CreateCreature("bdwtr35",[4405.1875],SE)  // Waterdhavian Soldier
		CreateCreature("bddagf35",[4405.1945],NE)  // Daggerford Militia
		CreateCreature("bddagf36",[3910.1660],SW)  // Daggerford Militia
		CreateCreature("bddagf37",[3555.1155],SW)  // Daggerford Militia
		CreateCreature("bddagf38",[4005.905],NE)  // Daggerford Militia
		CreateCreature("bddagf35",[4375.1035],SW)  // Daggerford Militia
		CreateCreature("bddagf36",[4840.865],SE)  // Daggerford Militia
		CreateCreature("bdfist35",[4405.700],N)  // Flaming Fist Mercenary
		CreateCreature("bddagf37",[4455.725],NE)  // Daggerford Militia
		CreateCreature("bdfist38",[4565.630],NW)  // Flaming Fist Mercenary
		CreateCreature("bdfist35",[3735.1925],SE)  // Flaming Fist Mercenary
		ActionOverride("bdfist35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bddagf35",[3640.1925],SE)  // Daggerford Militia
		ActionOverride("bddagf35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfist36",[2965.1695],E)  // Flaming Fist Mercenary
		ActionOverride("bdfist36",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfisx35",[2120.2235],NE)  // Flaming Fist Sergeant
		ActionOverride("bdfisx35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdwtr35",[2040.2210],NE)  // Waterdhavian Soldier
		ActionOverride("bdwtr35",SetGlobal("bd_move_into_keep","locals",1))
		CreateCreature("bdfist37",[1190.2815],NE)  // Flaming Fist Mercenary
		ActionOverride("bdfist37",SetGlobal("bd_move_into_keep","locals",1))
END

IF
	!Dead("bdwarma1")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma1",JumpToPoint([2965.1395]))
		ActionOverride("bdwarma1",Face(SE))
END

IF
	!Dead("bdwarma2")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma2",JumpToPoint([3035.1380]))
		ActionOverride("bdwarma2",Face(S))
END

IF
	!Dead("bdwarma3")  // Waterdhavian Warmage
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdwarma3",JumpToPoint([3095.1400]))
		ActionOverride("bdwarma3",Face(SW))
END

IF
	!Dead("bdhensle")  // Dahk Hensleigh
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdhensle",JumpToPoint([3130.1445]))
		ActionOverride("bdhensle",Face(W))
END

IF
	!Dead("bdsorali")  // Soralis
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdsorali",JumpToPoint([3140.1490]))
		ActionOverride("bdsorali",Face(W))
END

IF
	!Dead("bdstoneh")  // General Stonehand
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bdstoneh",JumpToPoint([4300.975]))
		ActionOverride("bdstoneh",Face(SW))
		ActionOverride("bdstoneh",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
END

IF
	!Dead("bddosia")  // Dosia
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		ActionOverride("bddosia",JumpToPoint([4360.820]))
		ActionOverride("bddosia",Face(SW))
		AddMapNoteColor([4360.820],%eet_2%67718,SALMON)  // Theodosia Immartyred
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid")  // No such index
		SmallWait(20)
		FadeFromColor([20.0],0)
		AddJournalEntry(%eet_2%71126,QUEST)  // The Siege of Dragonspear I should speak with Torsin deLancie. Dragonspear Castle's inner courtyard has been penetrated by the forces aligned against the crusade. I would do well to consult a coalition commander about our troops' progress before entering the castle itself.
		SmallWait(10)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/bdcut43.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut43.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut43.baf~ EVALUATE_BUFFER

<<<<<<<< d5/d5caehef.d
ADD_STATE_TRIGGER bdcaelar 44 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 50 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 52 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDHEPHER

IF WEIGHT #5 ~GlobalGT("bd_plot","global",449) GlobalLT("bd_plot","global",480) AreaCheck("bd4300")~ BEGIN d5heph_c1
  SAY ~I’m sorry, I was not entirely clear about the nature of the bridge we have built. We are the ones being summoned - to Avernus. Don’t worry though, we will not stay long. I will shortly reconvene with my true master, and then return to Faerun with every bit the army you hoped for.~
  IF ~~ EXTERN BDCAELAR d5caelar_c1
END

IF ~~ BEGIN d5heph_c2
  SAY ~Foolish child. The godspawn are nothing. Candles, sputtering in a breeze. My liege is a hurricane! What do we care for Bhaal? We were ancient when Bhaal was a mewling babe, mortal as you. Bhaal seized power briefly, and inspired a few followers to murder. As if they would not have done so anyway! And now he is gone, soon to be forgotten. Pathetic!~
  IF ~~ GOTO d5heph_c3
END

IF ~~ BEGIN d5heph_c3
  SAY ~You want order? We will give you order. The children of Bhaal will be brought to heel, or exterminated. The chaos in their wake will be stamped out. Your entire world will be enslaved, made to serve our campaign against the old enemies, the TRUE chaos.~
  IF ~~ GOTO d5heph_c4
END

IF ~~ BEGIN d5heph_c4
  SAY ~It was meant to be, surely. Why else would this door be so conveniently placed here?~
  IF ~~ EXTERN BDCAELAR d5caelar_c2
END

IF ~~ BEGIN d5heph_c5
  SAY ~They are not my concern. You would do well to convince them to join our cause, lest they be the first ones ground under our feet!~
  IF ~~ GOTO d5heph_c7
END

IF ~~ BEGIN d5heph_c6
  SAY ~Worms. They would serve better a reanimated shock troops - shame that our little gambit in Kanaglym was interrupted.~
  IF ~~ GOTO d5heph_c7
END

IF ~~ BEGIN d5heph_c7
  SAY ~Goodbye Caelar, I go to meet my master. Feel free to follow me, if you can.~
  IF ~~
    DO ~StartCutSceneMode()
		StartCutSceneEx("d5port2",FALSE)~
    EXIT
END

END

APPEND BDCAELAR

IF ~~ BEGIN d5caelar_c1
  SAY ~But, the Bhaal crisis -~
  IF ~~ EXTERN BDHEPHER d5heph_c2
END

IF ~~ BEGIN d5caelar_c2
  SAY ~How could you? You betray all of the good men and women fighting and dying out there!~
  IF ~Global("bd_halata_freed","global",0)~ EXTERN BDHEPHER d5heph_c5
  IF ~Global("bd_halata_freed","global",1)~ EXTERN BDHEPHER d5heph_c6
END

END

APPEND BDDELANC

IF WEIGHT #-1 ~AreaCheck("bd4000") GlobalGT("bd_plot","global",430) GlobalLT("bd_plot","global",450)~ BEGIN d5delanc_c1
  SAY ~Good job so far! But I am concerned - Caelar's lieutenants have been leading the fight out here, but she herself has been nowhere to be found. It is unlike her to lead from the rear.~
  IF ~~ REPLY ~We need to get into the castle!~ GOTO d5delanc_c2
END

IF ~~ BEGIN d5delanc_c2
  SAY ~I agree. Where is Phosse? Someone bring her here!~
  IF ~~ 
    DO ~ClearAllActions()
		SetGlobal("bd_plot","global",450)
		StartCutSceneMode()
		FadeToColor([20.0],0)
		StartCutSceneEx("d5port1",TRUE)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5caehef.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5caehef.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5caehef.d~


//___________________________________________________________________________________
//
// coalition mage Detect Magic
// just rewrite the state

<<<<<<<< d5/d5pordor.d
REPLACE_SAY BDFFMAGE 0 ~I sense powerful alteration magic, tinged with evil. And... conjuration magic, as well? Gods - Corporal, I think the portal is behind this door!~

REPLACE_SAY BDANDRUS 0 ~I sense powerful alteration magic, tinged with evil. And... conjuration magic, as well? Gods - Corporal, I think the portal is behind this door!~

REPLACE_SAY BDBENCE 105 ~Well then, pray it has not been opened. What do you say, <CHARNAME>? Are you ready for this? Make sure you are equipped with every tool available. What awaits us may be worse than anything we have ever faced.~
>>>>>>>>
COPY ~d5/d5pordor.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5pordor.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5pordor.d~


//___________________________________________________________________________________
//
// new portal cut scene for Charname
// adapt from bdcut45b
// caelar frees herself, closes the door, goes into the portal "as long as I control the portal, the day is not lost!"
// fiends appear
// pay attention to the bd_plot variable!

<<<<<<<< d5/bdcut45.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ClearAllActions()
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",60)
		SetCutSceneBreakable(TRUE)
		SmallWait(5)
		MoveViewPoint([1060.685],BD_NORMAL)
		ActionOverride("bdffsol1",FaceObject("door03"))
		ActionOverride("bdffsol2",FaceObject("door03"))
		ActionOverride("bdffmage",FaceObject("door03"))
		ActionOverride("bdbence",FaceObject("door03"))
		SmallWait(5)
		Unlock("door03")
		OpenDoor("door03")
		SmallWait(5)
		CreateCreature("bdcaelar",[520.400],SE)  // Caelar Argent
		CreateCreature("bdcrue50",[440.520],NE)  // Crusader Elite
		CreateCreature("bdcrue53",[470.295],SE)  // Crusader Elite
		CreateCreature("bdlieutg",[745.300],SW)  // Crusader Elite
		CreateCreature("bdlieutc",[340.395],E)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdlieute",[505.220],S)  // Crusader Elite
		CreateCreature("bdlieuta",[500.570],NE)  // Caelar's Lieutenant
		CreateCreature("bdcrue52",[425.340],SE)  // Crusader Elite
		CreateCreature("bdcrue55",[645.270],S)  // Crusader Elite
		CreateCreature("bdlieutd",[390.280],SE)  // Crusader Elite
		SmallWait(5)
		CreateCreature("bdcrue51",[410.435],E)  // Crusader Elite
		CreateCreature("bdcrue54",[550.255],S)  // Crusader Elite
		CreateCreature("bdlieutb",[375.520],NE)  // Caelar's Lieutenant
		CreateCreature("bdlieutf",[645.230],S)  // Crusader Elite
		ActionOverride("bdcaelar",SetSequence(16))
		SmallWait(5)
		ReallyForceSpellRES("bdglowgr","bdlieuta")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue50")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue51")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue52")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue53")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue54")  // No such index
		ReallyForceSpellRES("bdglowgr","bdcrue55")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutg")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutb")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutc")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutd")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieute")  // No such index
		ReallyForceSpellRES("bdglowgr","bdlieutf")  // No such index
		SmallWait(5)
		ActionOverride("bdffsol1",MoveToPoint([995.665]))
		ActionOverride("bdffsol2",MoveToPoint([1055.605]))
		ActionOverride("bdffmage",MoveToPoint([1050.625]))
		ActionOverride("bdbence",MoveToPoint([1005.630]))
		ActionOverride(Player1,MoveToPoint([820.530]))
		ActionOverride(Player2,MoveToPoint([855.480]))
		ActionOverride(Player3,MoveToPoint([870.555]))
		ActionOverride(Player4,MoveToPoint([905.505]))
		ActionOverride(Player5,MoveToPoint([915.580]))
		ActionOverride(Player6,MoveToPoint([950.530]))
		SmallWait(10)
		FadeToColor([10.0],0)
		SmallWait(10)
		ActionOverride("bdffsol1",JumpToPoint([995.665]))
		ActionOverride("bdffsol2",JumpToPoint([1055.605]))
		ActionOverride("bdffmage",JumpToPoint([1050.625]))
		ActionOverride("bdbence",JumpToPoint([1005.630]))
		ActionOverride(Player1,JumpToPoint([820.530]))
		ActionOverride(Player2,JumpToPoint([855.480]))
		ActionOverride(Player3,JumpToPoint([870.555]))
		ActionOverride(Player4,JumpToPoint([905.505]))
		ActionOverride(Player5,JumpToPoint([915.580]))
		ActionOverride(Player6,JumpToPoint([950.530]))
		MoveViewPoint([590.430],INSTANT)
		SmallWait(10)
		FadeFromColor([10.0],0)
		ActionOverride(Player1,MoveToPoint([675.485]))
		ActionOverride(Player2,MoveToPoint([710.435]))
		ActionOverride(Player3,MoveToPoint([725.510]))
		ActionOverride(Player4,MoveToPoint([760.460]))
		ActionOverride(Player5,MoveToPoint([775.535]))
		ActionOverride(Player6,MoveToPoint([815.485]))
		ActionOverride("bdffsol1",MoveToPoint([885.600]))
		ActionOverride("bdffsol2",MoveToPoint([945.505]))
		ActionOverride("bdffmage",MoveToPoint([950.580]))
		ActionOverride("bdbence",MoveToPoint([865.540]))
		SmallWait(40)
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(1))
		SmallWait(10)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcaelar")  // No such index
		SmallWait(5)
		ActionOverride("bdcaelar",SetSequence(SEQ_ATTACK_BACKSLASH))
		SmallWait(10)
		ActionOverride("bdlieuta",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue51",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue53",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue55",SetSequence(SEQ_DAMAGE))
		SmallWait(3)
		PlaySound("EFF_E06")
		ActionOverride("bdcrue50",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue52",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdcrue54",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutg",SetSequence(SEQ_DAMAGE))
		SmallWait(3)
		PlaySound("EFF_E06")
		ActionOverride("bdlieutb",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutc",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutd",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieute",SetSequence(SEQ_DAMAGE))
		ActionOverride("bdlieutf",SetSequence(SEQ_DAMAGE))
		ReallyForceSpellRES("bddispel","bdlieuta")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue51")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue53")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue55")  // No such index
		SmallWait(3)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcrue50")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue52")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue54")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutg")  // No such index
		SmallWait(3)
		ReallyForceSpellRES("bddispel","bdlieutb")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutc")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutd")  // No such index
		ReallyForceSpellRES("bddispel","bdlieute")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutf")  // No such index
		DisplayStringHead("bdcaelar",~Hephernaan... what have you done? I will have your head! Crusaders, with me!~)
		SmallWait(10)
		EndCutSceneMode()
		ActionOverride("bdcaelar",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/bdcut45.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut45.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut45.baf~

<<<<<<<< d5/d5nuport.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ClearAllActions()
		DisplayStringHead("bdbence",~Hold this room open!~)
		ActionOverride("bdbence",MoveToPoint([1100.725]))
		Wait(2)
		DisplayStringHead("bdcaelar",~No! Stay out! I must see this through!~)
		ActionOverride("bdbence",JumpToPoint([1100.725]))
		SmallWait(1)
		ActionOverride("bdcaelar",SetSequence(SEQ_ATTACK_BACKSLASH))
		SmallWait(5)
		ReallyForceSpellRES("bddispel","bdlieuta")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue51")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue53")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue55")  // No such index
		SmallWait(3)
		PlaySound("EFF_E06")
		ReallyForceSpellRES("bddispel","bdcrue50")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue52")  // No such index
		ReallyForceSpellRES("bddispel","bdcrue54")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutg")  // No such index
		SmallWait(3)
		ReallyForceSpellRES("bddispel","bdlieutb")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutc")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutd")  // No such index
		ReallyForceSpellRES("bddispel","bdlieute")  // No such index
		ReallyForceSpellRES("bddispel","bdlieutf")  // No such index
		SmallWait(5)
		CloseDoor("door03")
		ActionOverride("bdcaelar",Face(N))
		SmallWait(10)
		ActionOverride("bdcaelar",Face(NW))
		SmallWait(1)
		ActionOverride("bdcaelar",Face(W))
		SmallWait(1)
		ActionOverride("bdcaelar",Face(SW))
		DisplayStringHead("bdcaelar",~Crusaders, with me! We must pursue Hephernaan!~)
		SmallWait(10)
		ActionOverride("bdcaelar",MoveToPoint([630.430]))
		SmallWait(5)
		ActionOverride("bdlieuta",MoveToPoint([630.430]))
		ActionOverride("bdcrue51",MoveToPoint([630.430]))
		ActionOverride("bdcrue53",MoveToPoint([630.430]))
		ActionOverride("bdcrue55",MoveToPoint([630.430]))
		SmallWait(3)
		ActionOverride("bdcrue50",MoveToPoint([630.430]))
		ActionOverride("bdcrue52",MoveToPoint([630.430]))
		ActionOverride("bdcrue54",MoveToPoint([630.430]))
		ActionOverride("bdlieutg",MoveToPoint([630.430]))
		SmallWait(3)
		ActionOverride("bdlieutb",MoveToPoint([630.430]))
		ActionOverride("bdlieutc",MoveToPoint([630.430]))
		ActionOverride("bdlieutd",MoveToPoint([630.430]))
		ActionOverride("bdlieute",MoveToPoint([630.430]))
		ActionOverride("bdlieutf",MoveToPoint([630.430]))
		CreateVisualEffectObject("shair","bdcaelar")  // Caelar Argent
		ActionOverride("bdcaelar",DestroySelf())
		SmallWait(11)
		CreateVisualEffectObject("shair","bdlieuta")  // Caelar's Lieutenant
		CreateVisualEffectObject("shair","bdcrue51")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue53")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue55")  // Crusader Elite
		ActionOverride("bdlieuta",DestroySelf())
		ActionOverride("bdcrue51",DestroySelf())
		ActionOverride("bdcrue53",DestroySelf())
		ActionOverride("bdcrue55",DestroySelf())
		SmallWait(6)
		CreateVisualEffectObject("shair","bdcrue50")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue52")  // Crusader Elite
		CreateVisualEffectObject("shair","bdcrue54")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutg")  // Crusader Elite
		ActionOverride("bdcrue50",DestroySelf())
		ActionOverride("bdcrue52",DestroySelf())
		ActionOverride("bdcrue54",DestroySelf())
		ActionOverride("bdlieutg",DestroySelf())
		SmallWait(7)
		CreateVisualEffectObject("shair","bdlieutb")  // Caelar's Lieutenant
		CreateVisualEffectObject("shair","bdlieutc")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutd")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieute")  // Crusader Elite
		CreateVisualEffectObject("shair","bdlieutf")  // Crusader Elite
		ActionOverride("bdlieutb",DestroySelf())
		ActionOverride("bdlieutc",DestroySelf())
		ActionOverride("bdlieutd",DestroySelf())
		ActionOverride("bdlieute",DestroySelf())
		ActionOverride("bdlieutf",DestroySelf())
		SmallWait(10)
		Lock("door03")
		CreateCreature("BDSKIE",[920.360],SW)
		SmallWait(10)
		DisplayStringHead("BDSKIE",~Not so fast! Where do you think you're going?~)
		ActionOverride("BDSKIE",MoveToPoint([630.430]))
		SmallWait(30)
		CreateVisualEffectObject("shair","BDSKIE")
		ActionOverride("BDSKIE",DestroySelf())
		SetGlobal("D5SkieInHell","global",1)
		Wait(3)
		ActionOverride("bdbence",DestroySelf())
		TriggerActivation("portal",TRUE)
		CreateCreatureEffect("BDIMP","SPFLESHS",[615.420],E)  // Imp
		SmallWait(5)
		CreateCreatureEffect("bdlemure","SPFLESHS",[490.470],NW)  // Lemure
		SmallWait(5)
		CreateCreatureEffect("bdabibla","SPFLESHS",[630.300],SW)  // Black Abishai
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5nuport.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.baf~

<<<<<<<< d5/d5nuport.d
APPEND BDCAELAR

IF ~GlobalGT("bd_plot","global",479) GlobalLT("bd_plot","global",500) AreaCheck("bd4300")~ BEGIN d5caelar_d1
  SAY ~Hephernaan... I will not let him get away with this!~
  IF ~~ REPLY ~Caelar? What happened here? Where is Hephernaan?~ EXTERN BDBENCE d5benc_d1
END

END

APPEND BDBENCE

IF ~~ BEGIN d5benc_d1
  SAY ~Don't you see?? She opened the portal to the Nine Hells! This must have been her plan all along!~
  IF ~~ GOTO d5benc_d2
END

IF ~~ BEGIN d5benc_d2
  SAY ~We feared this eventuality but we are prepared for it. Stay here and at all costs, hold this vault open! We must bring Torsin to the portal, he has the means to close it again!~
  IF ~~
    DO ~SetGlobal("bd_plot","global",490)
    	StartCutSceneMode()
		StartCutSceneEx("d5nuport",FALSE)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5nuport.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5nuport.d~


//___________________________________________________________________________________
//
// add skie to avernus, right near portal - 930.1150
// have her start dialogue if nearby and no combat happening
// she is freaked out - be either supportive or dismissive
// she moves to portal after dialogue

<<<<<<<< d5/d54400_1.baf
IF
	Global("D5SkieInHell","global",1)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",2)
		CreateCreature("bdskie",[930.1150],E)
		Continue()
END
>>>>>>>>
COPY ~d5/d54400_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_1.baf~
EXTEND_TOP ~bd4400.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_1.baf~

<<<<<<<< d5/d5skie_1.baf
IF
	AreaCheck("BD4400") 
	Global("D5SkieInHell","global",2)
	!ActuallyInCombat()
	Range([PC],9)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",3)
		Wait(1)
		StartDialogueNoSet(Player1)
END	

IF
	AreaCheck("BD4400") 
	Global("D5SkieInHell","global",4)
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",5)
		Wait(1)
		MoveToPoint([1290.1375])
END	
>>>>>>>>
COPY ~d5/d5skie_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skie_1.baf~
EXTEND_BOTTOM ~bdskie.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skie_1.baf~


<<<<<<<< d5/d5skaver.d
APPEND BDSKIE

IF ~AreaCheck("BD4400") GlobalGT("D5SkieInHell","global",1) GlobalLT("D5SkieInHell","global",4)~ BEGIN d5bdskie_a1
  SAY ~<CHARNAME>! Thank the gods you're here! I thought I should follow those crusaders to stop their plan but... they're already dying! The monsters here are really scary!~
  IF ~~ GOTO d5bdskie_a2
END

IF ~~ BEGIN d5bdskie_a2
  SAY ~There are cats that are invisible! I tried to stay hidden too, but I think some of the fiends could see me! A couple chased me. Other ones looked right at me... but didn't chase me? Why would they do that? ~
  IF ~~ GOTO d5bdskie_a3
END

IF ~~ BEGIN d5bdskie_a3
  SAY ~I'm so scared! This place isn't natural! I think... I think I'm out of my depth...~
  IF ~~ REPLY ~Okay Skie, calm down. This is bad, but the way to survive is to keep cool. Can you do that?~ GOTO d5bdskie_a4
  IF ~~ REPLY ~Oh do you think that? Really? Skie, you've been out of your depth for weeks, it's about time you finally realized it.~ GOTO d5bdskie_a5
END

IF ~~ BEGIN d5bdskie_a4
  SAY ~Okay. Yes. I think I can do that. I- I want to stay alive. Do you have a plan? What should we do?~
  IF ~~ REPLY ~I need to follow Caelar. She has the vault key and we need to open the vault to let the Coalition close the portal. But someone needs to keep an eye on the portal. Can you do that for me? Stay hidden if you can. I'll be back and then we'll go home together.~ GOTO d5bdskie_a6
END

IF ~~ BEGIN d5bdskie_a5
  SAY ~Hey! Didn't I just say that? Can you please just... help? Just tell me what to do, okay? Please?~
  IF ~~ REPLY ~Well, I'm following Caelar. I assume she went to that big tower over there? I suggest you stay as close to the portal as you can, and try to avoid the fiends who will kill you and steal your soul. If you'r lucky, I'll be back soon and we'll close the portal from the other side.~ GOTO d5bdskie_a6
END

IF ~~ BEGIN d5bdskie_a6
  SAY ~Okay. Yeah. I think I can do that. I'll scout this side of the portal! I'm good at scouting. Just, come back soon, okay?~
  IF ~~ 
    DO ~SetGlobal("D5SkieInHell","global",4)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5skaver.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5skaver.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5skaver.d~


//___________________________________________________________________________________
//
// darnas' dialogue in avernus
// just rewrite the states
<<<<<<<< d5/d5darnas.d
REPLACE_SAY BDDARNAS 2 ~It has all gone wrong! Caelar has pushed forward, she thinks she can find... redemption, or power, chasing after Hephernaan. We have always been loyal to her, but look around us! How did it come to this? We never expected she would charge into darkness alone and unaided...~

ALTER_TRANS BDDARNAS // file name
  BEGIN 2 END // state number
  BEGIN 1 END // transition number
  BEGIN "REPLY" ~I do no think anyone expected this day to turn out as it has.~ END

REPLACE_SAY BDDARNAS 4 ~We should not be set against each other any longer. Please, help us stop Hephernaan and save Caelar. If that portal remains open, none of use will survive!~
>>>>>>>>
COPY ~d5/d5darnas.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5darnas.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5darnas.d~

// if !enemy, have him walk over to the portal after dialogue

<<<<<<<< d5/bdavern_.baf
IF
	AreaCheck("BD4400") 
	GlobalGT("bd_darnas_plot","global",2)
	GlobalLT("bd_darnas_plot","global",9)
	Name("bddarnas",Myself)
THEN
	RESPONSE #100
		Wait(1)
		MoveToPoint([1185.1330])
END	
>>>>>>>>
COPY ~d5/bdavern_.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdavern_.baf~
EXTEND_BOTTOM ~bdavernu.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/bdavern_.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdavernu.bcs~) BEGIN
  COPY_EXISTING ~bdavernu.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Switch("bd_darnas_plot","global")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !Name("bddarnas",Myself)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

//___________________________________________________________________________________
//
// remove umbral accord minutes from illaruel
COPY_EXISTING ~bdillaru.cre~ ~override~
  REMOVE_CRE_ITEM ~bdmisc65~
IF_EXISTS BUT_ONLY


//___________________________________________________________________________________
//
// change cut scene line when you catch up to caelar in avernus - bdcut50

OUTER_SET promise_string = RESOLVE_STR_REF (~You promised a celestial alliance, but now we are in Hell, surrounded by evil and naught but madness in your eyes!~)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut50.bcs~) BEGIN
  COPY_EXISTING ~bdcut50.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(%eet_2%38863\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%promise_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// change caelar's dialogue when you catch up to her in avernus
// set unattainable trigger for bdcaelar state 53
// append new state w/ new speech
// make sure any variables and exit conditions from original speech get recreated
<<<<<<<< d5/d5cavern.d
ADD_STATE_TRIGGER bdcaelar 53 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~Global("bd_plot","global",530) AreaCheck("bd4500")~ BEGIN d5caelar_e1
  SAY ~I'll not kill an innocent man. I've not fallen so far as that. But I must keep going. I must know whether there is any hope of finding my ancestor.~
  IF ~~ REPLY ~Caelar, give me the vault key and come back with me! Hephernaan has lied to you, there is nothing for us here!~ GOTO d5caelar_e6
  IF ~~ REPLY ~What is this obsession of yours? What are you really after?~ GOTO d5caelar_e2
  IF ~~ REPLY ~I don't care about your family drama. I only care about getting the key to the vault. If I need to go through you to get it, so be it.~ GOTO d5caelar_e6
END

IF ~~ BEGIN d5caelar_e2
  SAY ~Long ago, one of my forefathers was blessed by a union with Zariel, one of the most righteous and powerful solars in all the heavens. Since then by family has benefited from that union - we have tended to be strong, vigorous, charismatic. Natural leaders. The Order of the Aster to celebrate our good fortune and spread the blessings of Lathander to others.~
  IF ~~ GOTO d5caelar_e3
END

IF ~~ BEGIN d5caelar_e3
  SAY ~The very heavens are in our blood, you see? It does not diminish over generations. It is carried by mortals, but the power itself is beyond mortality. When I told you I could bend the path of fate, I was not being boastful! It is this blessing that makes me uniquely able to save the Sword Coast.~
  IF ~~ GOTO d5caelar_e4
END

IF ~~ BEGIN d5caelar_e4
  SAY ~But my ancestor, Zariel... she was lost, long ago. Some sages speculate she was imprisoned, others say fallen. Or maybe traveling the planes in some hidden guise. Wherever she is... inside me is a link to her! Hephernaan told me that certain rituals would penetrate the planar boundaries between us. If imprisoned, I could free her! If fallen, I could redeem her! With every application of his spells, I felt closer to her! The link was real, I tell you!~
  IF ~~ GOTO d5caelar_e5
END

IF ~~ BEGIN d5caelar_e5
  SAY ~But... when his work was completed the only path that was opened led here. To Avernus. Where the Dragonspear portal always led! The conduit should have moved! I need to understand why it did not!~
  IF ~~ REPLY ~It doesn't matter!~ GOTO d5caelar_e6
  IF ~CheckStatGT(Myself,16,CHR) CheckStatGT(Myself,15,INT)~ REPLY ~Caelar, I will not dissuade you from your mission. But do not put the Sword Coast at risk! Give me the vault key, let us close the portal, and... you can continue on your own path, wherever it leads.~ GOTO d5caelar_e8
END

IF ~~ BEGIN d5caelar_e6
  SAY ~The Sword Coast may be lost to your chaos. Whatever your intent, you and Hephernaan's betrayal have defeated my plan. But I must go onward! I will pursue Hephernaan and wring the truth from him!~
  IF ~~ REPLY ~The priority must be to close the portal!~ GOTO d5caelar_e7
  IF ~~ REPLY ~Just give me the key, damn it, I don't care what you do after that!~ GOTO d5caelar_e7
END

IF ~~ BEGIN d5caelar_e7
  SAY ~If Zariel is here I must find her! The portal must remain open to allow for our return!~
  IF ~~ DO ~SetGlobal("bd_plot","global",530)
  			StartCutSceneMode()
  			StartCutSceneEx("bdcut51",FALSE)~
    EXIT
END

IF ~~ BEGIN d5caelar_e8
  SAY ~You... you are right. My determination must not put any more innocents at risk. Very well, I will give you the vault key. Return to Dragonspear and close the portal! Do not wait for me. I will find my fate here on the planes.~
  IF ~~ DO ~SetGlobal("bd_plot","global",530)
  			SetGlobal("D5SkipBel","GLOBAL",1)
  			GiveItemCreate("bdkey02",Player1,1,0,0)
  			StartCutSceneEx("bdcut51",FALSE)~ // maybe make new cut scene for this...?
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5cavern.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cavern.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cavern.d~


//___________________________________________________________________________________
//
// change cut scene in advance of belhifet talk - bdcut57

OUTER_SET caebel_string = RESOLVE_STR_REF (~I know who you are, fiend! I have seen your likeness. You are Belhifet! What did you put Hephernaan up to? Where is Zariel?~)

OUTER_SET belcae1_string = RESOLVE_STR_REF (~I am so glad you made it here, aasimar. Zariel is not here. But I may yet grant your desire, after a fashion.~)

OUTER_SET belcae2_string = RESOLVE_STR_REF (~Now, without those bothersome gnats, let us speak.~)

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut57.bcs~) BEGIN
  COPY_EXISTING ~bdcut57.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(%eet_2%39110\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%caebel_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(%eet_2%39111\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%belcae1_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(%eet_2%39112\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~%belcae2_string%~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// rewrite behlifet's dialogue
// set unattainable trigger for bdbelhif states 0/1
// append entirely new dialogue

<<<<<<<< d5/d5belcae.d
ADD_STATE_TRIGGER bdbelhif 0 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdbelhif 1 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDBELHIF

IF ~GlobalLT("bd_plot","global",565)~ BEGIN d5belhif_f1
  SAY #%eet_2%39113 // ~What is this? The spawn of Bhaal, already? I'll have Thrix's hide for this!~
  IF ~~ REPLY ~Caelar, enough! Did you really expect to find a solar here?~ DO ~SetGlobal("bd_plot","global",565)~ EXTERN BDCAELAR d5caelar_f1
  IF ~~ REPLY ~He really is a treacherous weasel. Then again, he is a devil and his name is Thrix the Profane.~ DO ~SetGlobal("bd_plot","global",565)~ GOTO d5belhif_f2
  IF ~~ REPLY ~The Profane One is the last thing you should be concerned about, fiend.~ DO ~SetGlobal("bd_plot","global",565)~ GOTO d5belhif_f2
END

IF ~~ BEGIN d5belhif_f2
  SAY ~I will deal with him in time. But first, let me welcome Caelar Argent. I am pleased that you made it this far. Your combination of arrogance and desperation is... delectable. Marred only by the stench of your celestial blood. But, we can fix that.~
  IF ~~ GOTO d5belhif_f4
END

IF ~~ BEGIN d5belhif_f3
  SAY ~Indeed, arrogance and desperation. Quite a delectable combination. Marred only by the stench of your celestial blood. But, we can fix that.~
  IF ~~ GOTO d5belhif_f4
END

IF ~~ BEGIN d5belhif_f4
  SAY ~Zariel is not here, child. You will not speak with her. In fact I have taken care to shield our doings from her perception, temporarily. Oh, what cruelty perspective can inflict upon us! Your heritage, stemming from a momentary tryst centuries ago - it is the most important thing in your tiny life. But Zariel? She is as a god. You are less than a speck of dust to her.~
  IF ~~ GOTO d5belhif_f5
END

IF ~~ BEGIN d5belhif_f5
  SAY ~On the other hand, some of us can appreciate details lost to such lofty perspective. This little godspawn, for instance, is quite interesting. Now that I see you with my own eyes, I see you are more than the runt of some fecund avatar. You have been... enhanced. Somehow you have the ability to convert slain souls into power.~
  IF ~~ GOTO d5belhif_f6
END

IF ~~ BEGIN d5belhif_f6
  SAY ~With the right... modifications... you could be quite the weapon against the hated enemies. We could capture and utilize the souls of the generals of chaos, instead of merely discoporating their bodies. Oh, what interesting things occur when up-jumped gods frolic in the Prime!~
  IF ~~ EXTERN BDCAELAR d5caelar_f2
END

IF ~~ BEGIN d5belhif_f7
  SAY ~Hephernaan did what was necessary to open the door to your realm and bring you to me. No more, no less. It was a necessary deception, which we can now dispense with. That said, little Argent, what if I told you your goal is still within reach?~
  IF ~~ REPLY ~Never mind that, Belhifet. Caelar, we need to g-~ EXTERN BDCAELAR d5caelar_f3
END

IF ~~ BEGIN d5belhif_f8
  SAY ~You want to prevent the chaos represented by Bhaal's offspring? You want order? In truth, I want these things too. The forces of Law - MY forces of Law - can stamp out chaos in your Sword Coast. Serve as my lieutenant, and together we will bring order to your realm.~
  IF ~~ GOTO d5belhif_f9
END

IF ~~ BEGIN d5belhif_f9
  SAY ~The Bhaalspawn will be found and sent to the Blood War. I cannot enter the Prime - not yet - so I will name you regent of our dominion there. Sure you will be able to moderate my legions' excesses, should you desire. Consider it: everything you have strived for, all this time - will be achieved! It will be the greatest success of any in the Argent line! It might even make you truly worthy of Zariel's notice. You might be surprised at what attracts her notice these days...~
  IF ~~ EXTERN BDCAELAR d5caelar_f5
END

IF ~~ BEGIN d5belhif_f10
  SAY ~Nevertheless, your world is exposed. I will claim it, with or without you. This is not the deal you hoped to make, but it is the deal that is available to you. Success is still within your grasp! What is your alternative? Return in shame, and spend the rest of your days in chains? Rebel with this Bhaal child? I know your blood - failure and rebellion are not in your nature.~
  IF ~~ GOTO d5belhif_f11
END

IF ~~ BEGIN d5belhif_f11
  SAY ~The righteousness of Law drives you. And though we express it in different ways, it drives me as well. That flaming sword you wield, so like the one Zariel once bore... swear it to my service, and my army will be at your back when you return to the mortal realm. You will be a true crusader, just like your forebear...~
  IF ~~ REPLY ~Caelar, no! He is evil, and still manipulating you! Maybe your crusade was a mistake, maybe Zariel can never be saved. But YOU can be redeemed! This moment, right now, is what defines heroes! Stand with me against Belhifet, and together we will close the portal and safeguard the residents of the Sword Coast!~ EXTERN BDCAELAR d5caelar_f6
  IF ~~ REPLY ~Caelar, law and order are not worth your soul. I know it must be difficult four you, but this is the time to rebel! Trust me when I tell you: you are not bound by your lineage. I am my own person and I do not let my father's influence control me!~ EXTERN BDCAELAR d5caelar_f6
  IF ~~ REPLY ~This is a disaster! Your crusade only led to this: a chance for the fiends of Avernus to overrun the Sword Coast. Whatever you feared from Bhaal's children is nothing compared to the evil you have unleashed today. Every death, every bit of devastation that arises from this - it is laid at your feet!~ EXTERN BDCAELAR d5caelar_f8
  IF ~~ REPLY ~Fool! You have been so deluded by the ravings of your blood that you could not use common sense! Now you have snared the entire region in a devil's trap! Your folly must end, for good, here and now!~ EXTERN BDCAELAR d5caelar_f8
END

IF ~~ BEGIN d5belhif_f12
  SAY ~Bah! I should have known things would go awry when one of Bhaal's whelps became involved. "Chaos shall follow in their wake..." Alaundo spoke true. She was on the cusp of giving herself over to me, willingly! An Argent, in the service of Belhifet - do you know how long I have worked for this prize? If only to see the look on Zariel's face!~
  IF ~~ GOTO d5belhif_f13
END

IF ~~ BEGIN d5belhif_f13
  SAY ~Well. You have ruined my little game, and there can only be one consequence for that. Your souls will be my playthings for eternity. The argent will be raised as a death knight, but you, little godling, for you there is only oblivion. Come now, meet your doom!~
  IF ~~ 
    DO ~
    	AddJournalEntry(%eet_2%59847,QUEST)
    	SetGlobal("BDFinHep","GLOBAL",1)~
    EXIT
END

IF ~~ BEGIN d5belhif_f14
  SAY ~YES! I will grant you the power you need! Swear yourself to my service, and it shall be yours!~
  IF ~~ REPLY ~What? Caelar, this is not the answer!~ EXTERN BDCAELAR d5caelar_f10
END

IF ~~ BEGIN d5belhif_f15
  SAY ~Ha ha ha! Very well, the loss of one agent is worth having Zariel's spawn as my blackguard. Hephernaan, you have served me tolerably well. In the eons hence you may rise back through the ranks, and I will respect you all the more for it. ~
  IF ~~ EXTERN BDHEPHER d5hepher_f2
END

END

APPEND BDCAELAR

IF ~~ BEGIN d5caelar_f1
  SAY ~I am here for Hephernaan! He will tell me where I need to go next. I am not surprised to find him cowering between the legs of a true devil.~
  IF ~~ EXTERN BDHEPHER d5hepher_f1
END

IF ~~ BEGIN d5caelar_f2
  SAY ~Enough, Belhifet! We are not interested in your Blood War! Hephernaan's treatments - the link was real! I FELT it!~
  IF ~~ EXTERN BDBELHIF d5belhif_f7
END

IF ~~ BEGIN d5caelar_f3
  SAY ~Quiet!~
  IF ~~ GOTO d5caelar_f4
END

IF ~~ BEGIN d5caelar_f4
  SAY ~What are you talking about, fiend?~
  IF ~~ EXTERN BDBELHIF d5belhif_f8
END

IF ~~ BEGIN d5caelar_f5
  SAY ~I- no- this is not what I wanted. I did not come all this way to lend aid to devils, to expose my world to the cruelty of the Nine Hells!~
  IF ~~ EXTERN BDBELHIF d5belhif_f10
END

IF ~~ BEGIN d5caelar_f6
  SAY #%eet_2%39160
  IF ~~ REPLY ~I don't know what Alaundo's prophecy truly portends, but if you and I can stand firm here against Belhifet, there will be heroes to overcome any crisis sown by Bhaal. We may be mortals, but we lack for neither courage nor power. Are you with me?~ GOTO d5caelar_f7
END

IF ~~ BEGIN d5caelar_f7
  SAY #%eet_2%59720
  IF ~~
    DO ~SetGlobal("bd_caelar_fate","global",2)	//	stays good
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdbelhif",DestroyItem("minhp1"))
		DestroyItem("minhp1")
		DestroyItem("monhp1")
		GiveItemCreate("bdkey02",Myself,1,0,0)~
	EXTERN BDBELHIF d5belhif_f12
  IF ~NumInPartyLT(6)~
    DO ~SetGlobal("bd_caelar_fate","global",2)	//	stays good
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdbelhif",DestroyItem("minhp1"))
		DestroyItem("minhp1")
		DestroyItem("monhp1")
		GiveItemCreate("bdkey02",Myself,1,0,0)
		JoinParty()~ 
	EXTERN BDBELHIF d5belhif_f12
END

IF ~~ BEGIN d5caelar_f8
  SAY ~You... you are wrong. You must be wrong. You and your ilk are the cause of all of this! All I have endeavored to do was give people justice. You raised an army against me, pushed back time and again, amassed power of the god or murder to use against ME?~
  IF ~~ GOTO d5caelar_f9
END

IF ~~ BEGIN d5caelar_f9
  SAY ~I see clearly now: I am a crusader against chaos! That is all I have ever been! The Order of the Aster was founded for it - but has lacked the power to achieve its goals. No longer! with Belhifet behind me, the last remains of Bhaal will finally be extinguished. Then peace - MY peace - will reign!~
  IF ~~ EXTERN BDBELHIF d5belhif_f14
END

IF ~~ BEGIN d5caelar_f10
  SAY ~This has always been the answer. I see that now. I swear myself to you, Belhifet, on one condition: that you destroy Hephernaan for his treachery against me.~
  IF ~~ EXTERN BDBELHIF d5belhif_f15
END

END

APPEND BDHEPHER

IF ~~ BEGIN d5hepher_f1
  SAY ~Your arrogance is truly impressive, Caelar. I am pleased that you have made it this far.~
  IF ~~ EXTERN BDBELHIF d5belhif_f3
END

IF ~~ BEGIN d5hepher_f2
  SAY ~Whaaat?? No!~
  IF ~~ 
    DO ~StartCutSceneMode()
    	AddJournalEntry(%eet_2%59849,QUEST)
		ActionOverride("bdhepher",DestroyItem("minhp1"))
		ActionOverride("bdcaelar",DestroyItem("minhp1"))
		ActionOverride("bdcaelar",DestroyItem("monhp1"))
//		GiveItemCreate("bdkey02","bdcaelar",1,0,0)
    	SetGlobal("bd_caelar_fate","global",1)	//	turns evil
    	StartCutSceneEx("bdcut57a",FALSE)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5belcae.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5belcae.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5belcae.d~ EVALUATE_BUFFER

COPY_EXISTING ~bdcaelar.cre~ ~override~
	ADD_CRE_ITEM ~bdkey02~ #1 #0 #0 ~IDENTIFIED~ ~INV~
IF_EXISTS


//___________________________________________________________________________________
//
// scrub aun argent from the game

<<<<<<<< d5/d54700_1.baf
IF
	InMyArea("BDAUN")	
	Global("D5PeaceOutAun","BD4700",0)
THEN
	RESPONSE #100
		SetGlobal("D5PeaceOutAun","BD4700",1)
		ActionOverride("BDAUN",DestroySelf())
		Continue()
END
>>>>>>>>
COPY ~d5/d54700_1.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_1.baf~
EXTEND_TOP ~bd4700.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_1.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut57b.bcs~) BEGIN
  COPY_EXISTING ~bdcut57b.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(DisplayStringHead("bdaun",%eet_2%39174)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
/*
		SPRINT textToReplace ~\(DisplayStringHead("bdaun",239174)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
*/
    END
  BUT_ONLY
END


//___________________________________________________________________________________
//
// new post-belhifet behavior
// new dialogue for bdcaelar if alive
// set variable if bdcaelar is dead
// cut scene(s) to portal

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcaelar.bcs~) BEGIN
  COPY_EXISTING ~bdcaelar.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("bd_plot","global",570)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("bd_plot","global",999) AreaCheck("BD4700")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d54700_2.baf
IF
	GlobalGT("bd_plot","global",569)
	GlobalLT("bd_plot","global",577)
	InParty("bdcaelar")
	!Dead("bdcaelar")
THEN
	RESPONSE #100
		ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
		SetGlobal("bd_plot","global",577)
		Continue()
END

IF
	GlobalGT("bd_plot","global",569)
	GlobalLT("bd_plot","global",577)
	!InParty("bdcaelar")
	!Dead("bdcaelar")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",577)
		Continue()
END

IF
	Global("bd_plot","global",570)
	InPartyAllowDead("bdcaelar")
	Dead("bdcaelar")
THEN
	RESPONSE #100
		ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
		SetGlobal("bd_plot","global",579)
END

IF
	Global("bd_plot","global",570)
	!InPartyAllowDead("bdcaelar")
	Dead("bdcaelar")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",579)
END

IF	
	GlobalGT("bd_plot","global",577)
	PartyHasItem("bdkey02")
THEN
	RESPONSE #100
		StartCutSceneMode()
    	AddJournalEntry(%eet_2%59849,QUEST)
    	Wait(2)
    	StartCutSceneEx("bdcut58",TRUE)
END
>>>>>>>>
COPY ~d5/d54700_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_2.baf~
EXTEND_TOP ~bd4700.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54700_2.baf~ EVALUATE_BUFFER

/* maybe let's just use bdcut58
<<<<<<<< d5/d5cut58a.baf
IF
	
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",579)
END
>>>>>>>>
COPY ~d5/d5cut58a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut58a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut58a.baf~ EVALUATE_BUFFER
*/

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut58.bcs~) BEGIN
  COPY_EXISTING ~bdcut58.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CreateCreature("bdaun2",\).+$~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~ActionOverride("bdaun".+$~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~EndCutSceneMode()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!Dead("bdcaelar")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("D5SkieInHell","global",1)
	GlobalLT("D5SkieInHell","global",6)
	!InMyArea("bdskie")
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",6)
		CreateCreature("bdskie",[1290.1375],NE)
END

IF
	!Dead("bdcaelar")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d5postb.d
ADD_STATE_TRIGGER bdcaelar 68 ~GlobalGT("bd_plot","GLOBAL",999)~
ADD_STATE_TRIGGER bdcaelar 83 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

IF ~Global("bd_plot","global",577) AreaCheck("bd4700") !ActuallyInCombat()~ BEGIN d5caelar_g1
  SAY ~Belhifet is gone. In his own place of power, our combined might overcame him. He will be reborn, no doubt, but it will be eons before can rise to a station high enough to bother mortals.~
  IF ~~ GOTO d5caelar_g2
END

IF ~~ BEGIN d5caelar_g2
  SAY ~I owe you a debt of gratitude. Not only for fighting by my side - but I almost gave in. I was on the cusp of betraying everything I ever held dear. And it seemed so right! He made it seem like betraying my soul for him was the only way to be true to myself.~
  IF ~~ GOTO d5caelar_g3
END
  
IF ~~ BEGIN d5caelar_g3
  SAY ~That you could believe in me still, could see past our opposition - it gave me the strength to resist his entreaties. Heavens know what evil might have been wrought if I agreed.~
  IF ~~ REPLY ~So you don't mean to marshal this power for yourself? We were allies a moment ago, but if you still mean to conquer the Sword Coast, I will oppose you here and now.~ GOTO d5caelar_g4
  IF ~~ REPLY ~The words of one who knows she is defeated. I hope you don't think to seize Belhifet's power and march fiends through the gate.~ GOTO d5caelar_g4
END

IF ~~ BEGIN d5caelar_g4
  SAY ~No. My crusade is finished. But we should not tarry here. The portal remains open, the fiends of Avernus could find it and spill through. It will not happen quickly without Belhifet and Hephernaan to lead them; we should take advantage of this window of opportunity to close the conduit.~
  IF ~~ GOTO d5caelar_g5
END

IF ~~ BEGIN d5caelar_g5
  SAY ~Here: take the key to the Dragonspear Vault. I know the forces of Waterdeep have the means to close the portal again.~
  IF ~~ 
    DO ~SetGlobal("bd_caelar_fate","global",5) // ***** and AddJournalEntry
    	SetGlobal("bd_plot","global",578)
    	GiveItemCreate("bdkey02",Player1,1,0,0)~
    EXIT
  IF ~InParty("bdcaelar")~ 
    DO ~ActionOverride("bdcaelar",DestroyAllFragileEquipment(ADAMANTINE))
		ActionOverride("bdcaelar",LeaveParty())
    	SetGlobal("bd_plot","global",578)
		SetGlobal("bd_caelar_fate","global",5) // ***** and AddJournalEntry
    	GiveItemCreate("bdkey02",Player1,1,0,0)~
    EXIT
END

END
>>>>>>>>
COPY ~d5/d5postb.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5postb.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5postb.d~ EVALUATE_BUFFER



//___________________________________________________________________________________
//
// new portal scene 
// new portal dialogue from caelar if she is alive
// note triggers for new dialogue
// new portal behavior, have to actually click it
// have skie & darnas go through with you
// rewrite BDINFO12.bcs, BDCUT59.bcs

<<<<<<<< d5/d54400_2.baf
IF
	PartyHasItem("bdkey02")
	GlobalLT("bd_plot","global",580)
THEN
	RESPONSE #100
		Wait(2)
		SetGlobal("bd_plot","global",580)
		Continue()
END
/*
IF
	GlobalGT("bd_plot","global",580)
	GlobalLT("D5SkieInHell","global",6)
	!InMyArea("bdskie")
THEN
	RESPONSE #100
		SetGlobal("D5SkieInHell","global",6)
		CreateCreature("bdskie",[1290.1375],NE)
		Continue()
END
*/
>>>>>>>>
COPY ~d5/d54400_2.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_2.baf~
EXTEND_TOP ~bd4400.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54400_2.baf~

<<<<<<<< d5/d5gohome.d
ADD_STATE_TRIGGER bdcaelar 84 ~GlobalGT("bd_plot","GLOBAL",999)~

APPEND BDCAELAR

// longer "what will you do now?" and GiveItemCreate("bdkey02",Player1,1,0,0)

IF ~Global("bd_plot","global",580) AreaCheck("BD4400") !ActuallyInCombat()~ BEGIN d5caelar_h1
  SAY ~Here we must part ways. I am not going back. ~
  IF ~~ REPLY ~You have been so driven, for so long. Would you really give up your crusade?~ DO ~SetGlobal("bd_plot","global",581)~ GOTO d5caelar_h2
END

IF ~~ BEGIN d5caelar_h2
  SAY ~Not really. I still think I had the right of it. The prophecy of Bhaal's children - of you - is incredibly dangerous for the Sword Coast. Many will suffer. You think you can avert that suffering, but you are not equipped to do so.~
  IF ~~ GOTO d5caelar_h3
END

IF ~~ BEGIN d5caelar_h3
  SAY ~But, if I succeeded? If I subdued the region and stamped out Bhaal's children, maybe with the help of fiendish power? Maybe it would be worse. There is no way to know - there is no prophecy detailing that future. So, I will let it go. You, on the other hand, are still bound to your fate. I almost feel sorry for you...~
  IF ~~ REPLY ~Would you really prefer to brave the Hells than return to Dragonspear and face justice?~ GOTO d5caelar_h6
  IF ~~ REPLY ~I cannot allow you to stay here. You thanked me but you owe justice to many more back at Dragonspear Castle.~ GOTO d5caelar_h5
  IF ~~ REPLY ~You think I am going to just let you go? You are coming with me - if I have to drag your corpse with me and raise you to stand trial, so be it!~ GOTO d5caelar_h4
END

IF ~~ BEGIN d5caelar_h4
  SAY ~We were effective fighting together against Belhifet, but you are a fool if you think you can take me against my will!~
  IF ~~ DO ~Enemy()~ EXIT
END

IF ~~ BEGIN d5caelar_h5
  SAY ~With respect: what will you do? Chase me? I will simply lead you away, and you would have to leave the portal unguarded against invasion. Your whole world will be lost.~
  IF ~~ REPLY ~It is your world too! Would you really risk that?~ GOTO d5caelar_h6
END

IF ~~ BEGIN d5caelar_h6
  SAY ~Faerun is no longer my home. Maybe it never was. You have friends there, comrades, family. You are regarded as a hero. Go there. Close the portal. Be the hero. But I... I must still heed the calling of my blood. You claimed it can be ignored, but I have doubts.~
  IF ~~ GOTO d5caelar_h7
END

IF ~~ BEGIN d5caelar_h7
  SAY ~I will guard the portal from this side, while it is open. And then I will find Zariel. Hephernaan's rituals were a way of controlling me - truly fiendish magic - but they exploited a real connection It called me here for a reason. I must find out the truth.~
  IF ~~ REPLY ~Very well, I guess. Good luck.~ GOTO d5caelar_h9
  IF ~~ REPLY ~After all this, you haven't changed. Good luck finding your way out of this literal hellhole.~ GOTO d5caelar_h9
  IF ~GlobalGT("D5DaustonQuest","GLOBAL",6)~ REPLY ~I met your cousin once. Dauston? He was like you - sort of. He said the calling of your blood is not real. Just an echo of the past.~ GOTO d5caelar_h8
END

IF ~~ BEGIN d5caelar_h8
  SAY ~Maybe that is so. I do not know. But out here, everything is an echo of the past, a reflection of some story or other. Maybe this is where I belong. Certainly many have become Planewalkers for worse reasons than echoes of the past singing in their blood!~
  IF ~~ GOTO d5caelar_h9
END

IF ~~ BEGIN d5caelar_h9
  SAY ~Listen: the other children of Bhaal are awakening, just as you have. Scores of them. No matter your actions, most of them will give in to their dark urges. You averted Sarevok's war but I say again: you cannot avert the war to come. You are not like me - not a crusader.~
  IF ~~ GOTO d5caelar_h10
END

IF ~~ BEGIN d5caelar_h10
  SAY ~Bhaal's war will come, but maybe if there are enough heroes, its worst effects can be mitigated. Maybe that is the best we can hope for. Good fortune to you, in any event.~
  IF ~~ EXIT
END

IF ~GlobalGT("bd_plot","global",582) AreaCheck("BD4400")~ BEGIN d5caelar_h11
  SAY ~Enter the portal! Go back to Faerun, and let the Waterdeep forces into the vault. They can close the conduit.~
  IF ~~ EXIT
END

END

APPEND BDSKIE

IF ~GlobalGT("bd_plot","global",579) AreaCheck("bd4400")~ BEGIN d5skie_h1
  SAY ~Can we go home now? Please??~
  IF ~~ EXIT
END

END

APPEND BDDARNAS

IF ~GlobalGT("bd_plot","global",579) AreaCheck("bd4400")~ BEGIN d5darnas_h1
  SAY ~If you have the vault key, we should go at once. We must ensure no more fiends get through.~
  IF ~~ EXIT
END

END
>>>>>>>>
COPY ~d5/d5gohome.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5gohome.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5gohome.d~ EVALUATE_BUFFER

// ***** add waves of fiends appearing? like the end of BP1

ACTION_IF (FILE_EXISTS_IN_GAME ~bdinfo12.bcs~) BEGIN
  COPY_EXISTING ~bdinfo12.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Name("portal",Myself)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !PartyHasItem("bdkey02")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/nuinfo12.baf
IF
	Clicked([ANYONE])
	Name("portal",Myself)
	AreaCheck("bd4400")
	PartyHasItem("bdkey02")
THEN
	RESPONSE #100
		SetGlobal("bd_plot","global",583)
		StartCutSceneMode()
    	StartCutSceneEx("d5cut59",TRUE)
END
>>>>>>>>
COPY ~d5/nuinfo12.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/nuinfo12.baf~
EXTEND_TOP ~bdinfo12.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/nuinfo12.baf~

<<<<<<<< d5/d5cut59.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		SetAreaScript("cutskip",OVERRIDE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",59)
		SetCutSceneBreakable(TRUE)
		SmallWait(15)
END

IF
	InMyArea("bdskie")
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride("bdskie",MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride("bdskie",SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh","bdskie")
		SmallWait(2)
		ApplySpellRES("bdhide","bdskie")  // No such index
		SmallWait(5)
		ActionOverride("bdskie",JumpToPoint([1455.1175]))
END

IF
	InMyArea("bddarnas")
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride("bddarnas",MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride("bddarnas",SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh","bddarnas")
		SmallWait(2)
		ApplySpellRES("bdhide","bddarnas")  // No such index
		SmallWait(5)
		ActionOverride("bddarnas",JumpToPoint([1485.1175]))
END

IF
	InMyArea(Player2)
	!StateCheck(Player2,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player2,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player2,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player2)
		SmallWait(2)
		ApplySpellRES("bdhide",Player2)  // No such index
		SmallWait(5)
		ActionOverride(Player2,JumpToPoint([1485.1270]))
END

IF
	InMyArea(Player3)
	!StateCheck(Player3,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player3,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player3,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player3)
		SmallWait(2)
		ApplySpellRES("bdhide",Player3)  // No such index
		SmallWait(5)
		ActionOverride(Player3,JumpToPoint([1465.1300]))
END

IF
	InMyArea(Player4)
	!StateCheck(Player4,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player4,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player4,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player4)
		SmallWait(2)
		ApplySpellRES("bdhide",Player4)  // No such index
		SmallWait(5)
		ActionOverride(Player4,JumpToPoint([1430.1315]))
END

IF
	InMyArea(Player5)
	!StateCheck(Player5,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player5,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player5,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player5)
		SmallWait(2)
		ApplySpellRES("bdhide",Player5)  // No such index
		SmallWait(5)
		ActionOverride(Player5,JumpToPoint([1390.1320]))
END

IF
	InMyArea(Player6)
	!StateCheck(Player6,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player6,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player6,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player6)
		SmallWait(2)
		ApplySpellRES("bdhide",Player6)  // No such index
		SmallWait(5)
		ActionOverride(Player6,JumpToPoint([1340.1325]))
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		ActionOverride(Player1,MoveToPoint([1337.1213]))
		SmallWait(15)
		ActionOverride(Player1,SetSequence(SEQ_READY))
		SmallWait(3)
		CreateVisualEffectObject("ddoorh",Player1)
		SmallWait(2)
		ApplySpellRES("bdhide",Player1)  // No such index
		SmallWait(5)
		ActionOverride(Player1,JumpToPoint([1485.1240]))
END

IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bdcutid2")  // No such index
		SmallWait(5)
		ActionOverride("cutspy",DestroySelf())
		FadeToColor([40.0],0)
		SmallWait(25)
		ApplySpellRES("bdunhide",Player1)  // No such index
		ApplySpellRES("bdunhide",Player2)  // No such index
		ApplySpellRES("bdunhide",Player3)  // No such index
		ApplySpellRES("bdunhide",Player4)  // No such index
		ApplySpellRES("bdunhide",Player5)  // No such index
		ApplySpellRES("bdunhide",Player6)  // No such index
		ApplySpellRES("bdunhide","bdskie")  // No such index
		ApplySpellRES("bdunhide","bddarnas")  // No such index
		SetCutSceneBreakable(FALSE)
		SetGlobal("BD_CUTSCENE_BREAKABLE","GLOBAL",0)
		SetAreaScript("",OVERRIDE)
		SmallWait(5)
		StartCutScene("bdcut59a")
END
>>>>>>>>
COPY ~d5/d5cut59.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59.baf~

<<<<<<<< d5/d5cut59a.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SmallWait(5)
//		ActionOverride("bdskie",LeaveAreaLUA("bd4300","",[600.350],S))
//		ActionOverride("bddarnas",LeaveAreaLUA("bd4300","",[535.375],SE))
		ActionOverride(Player2,LeaveAreaLUA("bd4300","",[600.350],S))
		ActionOverride(Player3,LeaveAreaLUA("bd4300","",[535.375],SE))
		ActionOverride(Player4,LeaveAreaLUA("bd4300","",[500.425],E))
		ActionOverride(Player5,LeaveAreaLUA("bd4300","",[520.475],NE))
		ActionOverride(Player6,LeaveAreaLUA("bd4300","",[580.495],NE))
		LeaveAreaLUAPanic("bd4300","",[660.365],SW)  // Dragonspear Castle Basement
		LeaveAreaLUA("bd4300","",[660.365],SW)  // Dragonspear Castle Basement
		MultiPlayerSync()
		Wait(1)
		StartCutSceneEx("bdcut59b",TRUE)
END
>>>>>>>>
COPY ~d5/d5cut59a.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/bdcut59a.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/bdcut59a.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bdcut59b.bcs~) BEGIN
  COPY_EXISTING ~bdcut59b.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(!Dead("bdcaelar")\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~GlobalGT("bd_plot","global",999)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(AmbientActivate("portal_visuals",FALSE)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(AmbientActivate("portal_webm",FALSE)\)~ 
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
    END
  BUT_ONLY
END

<<<<<<<< d5/d5cut59b.baf
IF
	GlobalGT("D5SkieInHell","global",1)
	GlobalLT("D5SkieInHell","global",7)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SetGlobal("D5SkieInHell","global",7)
		CreateCreature("bdskie",[600.350],E)  // Skie
		Continue()
END
>>>>>>>>
COPY ~d5/d5cut59b.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59b.baf~
EXTEND_TOP ~bdcut59b.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59b.baf~


//___________________________________________________________________________________
//
// new return to dragonspear scene
// portal script sends you to the vault
// some new dialogue from Skie/Darnas?
// make sure everyone in place (just set bd_plot var?)
// make sure the vault door can be opened

// add portal closing animation
COPY_EXISTING ~bd4300.are~ ~override~
  LPF fj_are_structure
    INT_VAR
      fj_loc_x = 370
      fj_loc_y = 290
      fj_flags = 12608
      fj_width = 512
      fj_height = 256
    STR_VAR
      fj_structure_type = ~animation~
      fj_name = ~Portal_Closing~
      fj_bam_resref = ~BD4400PC~
  END
BUT_ONLY

<<<<<<<< d5/d54300_3.baf
IF
	Global("bd_plot","global",590)
	Global("D5ClosePortal","BD4300",0)
THEN
	RESPONSE #100
		SetGlobal("D5ClosePortal","BD4300",1)
		StartCutSceneMode()
    	StartCutSceneEx("d5cut59z",FALSE)
END
>>>>>>>>
COPY ~d5/d54300_3.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_3.baf~
EXTEND_TOP ~bd4300.bcs~ ~weidu_external/%MOD_FOLDER%/compile/200/d54300_3.baf~

<<<<<<<< d5/d5cut59z.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("bddelanc")
		SetCutSceneBreakable(FALSE)
		SetGlobal("D5ClosePortal","BD4300",1)
// make bdskie go away?
		TakePartyItem("bdkey02")
	    DestroyItem("bdkey02")
		MoveViewPoint([720.480],VERY_FAST)
		MoveToPoint([735.495])
		DisplayStringHead(Myself,~Uses Rod of Disjunction~)
		SmallWait(5)
		ForceSpell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
		ForceSpell(Myself,CLERIC_HOLY_POWER)
		SmallWait(100)
		AmbientActivate("Portal_Closing",TRUE)
		AmbientActivate("portal_visuals",FALSE)
		AmbientActivate("portal_webm",FALSE)
		SmallWait(60)
		AmbientActivate("Portal_Closing",FALSE)
		SmallWait(10)
		StartDialogueNoSet(Player1)
END
>>>>>>>>
COPY ~d5/d5cut59z.baf~ ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59z.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5cut59z.baf~

<<<<<<<< d5/d5imback.d
REPLACE_SAY BDBENCE 66 ~Now if you'll excuse me, I need to check on Skie. She seems extremely disturbed. Between you and me I will credit her for being brave - brave to the point of foolhardiness. But she was not made for excusions to the Hells.~

ADD_STATE_TRIGGER BDDELANC 95 ~Global("D5ClosePortal","BD4300",2)~

// replace the text of bddelanc state 77 transions 0-1
ALTER_TRANS BDDELANC BEGIN 77 END BEGIN 0 END BEGIN "REPLY" ~Caelar Argent is dead. Her advisor, Hephernaan, was secretly working for an archfiend. They have been defeated, but we must close the portal.~ END 

ALTER_TRANS BDDELANC BEGIN 77 END BEGIN 1 END BEGIN "REPLY" ~The Crusade was secretly inspired and manipulated by an archfiend. They have been defeated. Now we must close the portal to Avernus.~ END 

APPEND BDDELANC

IF ~Global("bd_plot","global",590) AreaCheck("bd4300") Global("D5ClosePortal","BD4300",1)~ BEGIN d5delanc_i1
  SAY ~That will bind the portal shut temporarily. We will task a team of magic users to re-weave the seals that Caelar undid, in order to seal the portal permanently. In time, we must devise a way to dislodge the conduit behind the portal from its connection to Toril. But that is a task for another day. For now, we are safe.~
  IF ~~ DO ~SetGlobal("D5ClosePortal","BD4300",2)~ EXIT
END

END

APPEND BDSKIE

IF ~GlobalGT("bd_plot","global",581) AreaCheck("bd4300") GlobalGT("D5SkieInHell","global",1)~ BEGIN d5skie_i1
  SAY ~I need to get upstairs, away from this thing! I- I can't talk right now.~
  IF ~~ EXIT
END

END
>>>>>>>>
COPY ~d5/d5imback.d~ ~weidu_external/%MOD_FOLDER%/compile/200/d5imback.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/200/d5imback.d~ EVALUATE_BUFFER


//___________________________________________________________________________________
//


END	//	end define function

/*

IF ~~ BEGIN d5_
  SAY ~~
  IF ~~ GOTO d5_
END

*/

////////////////////
////////////////////
////////////////////
