
DEFINE_ACTION_FUNCTION entar_accuse BEGIN

/*
- plan: add to Belt's dialogue (and Eltan's? and Liia's?) saying the undercity was sealed off long ago, but mention rumors that an entrance once existed in the thieves guild. for what purpose, none can say, and it is unknown whether it still exists. but worth investigating.

- have entar appear and rave about how you murdered him
 -- ...everyone is like "no, you're not caught up, it was doppelganger's see?"
 -- ...entar, reasonably, is not quick to calm down

- add Skie interjections

- change the trigger to unlock the maze entrance from  ("DukeThanks","GLOBAL",1) to something else

- have Denkod talk about the stairs
 -- ...and probably then have to disable the trigger that creates him after Sarevok is dead.

- bring Imoen back at this time. creates complications tho.
 -- ...only if not in party
 -- ...need to check when she was last in party
 -- ...and check if she is dead
 -- ...and give dialogue appropriate for each ("I had such an adventure! Been hearing a lot about you too, though.")
 -- ...and another cut scene to introduce her
 -- ...and some comments by Liia ("best to keep any of your associates from Candlekeep in protective custody here until the threat of Sarevok is truly passed.")
 -- ...don't let her join the party in this case...?

*/

//___________________________________________________________________________________
//

// prep journal entries

ADD_JOURNAL TITLE (@23036) @25035 @25079

// add a game check to record the last time Imoen is in the party

<<<<<<<< d5/baldur+.baf
IF
	Global("CHAPTER","GLOBAL",1)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",1)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",2)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",2)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",3)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",3)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",4)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",4)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",5)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",5)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",6)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",6)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",6)
		Continue()
END

IF
	Global("CHAPTER","GLOBAL",7)
	InParty("IMOEN")
	GlobalLT("D5ImoenChap","GLOBAL",7)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenChap","GLOBAL",7)
		Continue()
END
>>>>>>>>
COPY ~d5/baldur+.baf~ ~weidu_external/%MOD_FOLDER%/compile/baldur+.baf~
EXTEND_TOP ~baldur.BCS~ ~weidu_external/%MOD_FOLDER%/compile/baldur+.baf~


<<<<<<<< d5/ar108+5.baf
/* Palace (ground floor): Duke Belt should be alive */
/* Palace ground floor. revive Duke Belt and move him in the middle of the room */

IF
	Dead("BELT")
	Global("D5KorlaszDone","GLOBAL",1)
	Global("D5ReviveBelt","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("SPRITE_IS_DEADBELT","GLOBAL",0)
		CreateCreature("BELT",[735.371],S)
		SmallWait(1)
		Continue()
END

IF
	Global("D5KorlaszDone","GLOBAL",1)
	Global("D5ReviveBelt","MYAREA",0)
THEN
	RESPONSE #100
		SmallWait(1)
		ActionOverride("BELT",JumpToPoint([735.371]))
		ActionOverride("BELT",Face(0))
		SmallWait(1)
		Continue()
END

IF
	Global("D5KorlaszDone","GLOBAL",1)
	Global("D5ReviveBelt","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("D5ReviveBelt","MYAREA",1)
END

IF
	Global("D5EntarCut","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5EntarCut","GLOBAL",2)
		CreateCreature("BDENTAR",[520.615],E)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5ENCUT1")
END

IF
	Global("D5EntarCut","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("D5EntarCut","GLOBAL",5)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5ENCUT2")
END

IF
	Global("D5MazeOpen","GLOBAL",1)
	Global("D5ImoenPalace","GLOBAL",0)
	!InParty("%IMOEN_DV%")
THEN
	RESPONSE #100
		SetGlobal("D5ImoenPalace","GLOBAL",1)
		CreateCreature("BDIMOEN",[230.380],SE)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5IMCUT1")
END

IF
	Global("D5MazeOpen","GLOBAL",1)
	Global("D5ImoenPalace","GLOBAL",0)
	InParty("%IMOEN_DV%")
	InMyArea("%IMOEN_DV%")	
	!StateCheck("%IMOEN_DV%",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
		SetGlobal("D5ImoenPalace","GLOBAL",3)	//	is this necessary?
		Continue()
END
>>>>>>>>
COPY ~d5/ar108+5.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar108+5.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0108.bcs~) BEGIN
  EXTEND_TOP ~%bg1_area_prefix%0108.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar108+5.baf~ EVALUATE_BUFFER
END

<<<<<<<< d5/d5encut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("BDENTAR")
		DisplayStringHead(Myself,@25019)
		MoveToPoint([705.560])
		SmallWait(1)
		SetGlobal("D5EntarCut","GLOBAL",3)
		SmallWait(2)
		EndCutSceneMode()
		ActionOverride("BDENTAR",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5encut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5encut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5encut1.baf~

<<<<<<<< d5/d5encut2.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("BDENTAR")
		DisplayStringHead(Myself,@25030)
		MoveToPoint([520.615])
		SetGlobal("D5EntarCut","GLOBAL",6)
		SmallWait(1)
		EndCutSceneMode()
		ActionOverride("BDENTAR",EscapeArea())
/*		ActionOverride("BELT",StartDialogueNoSet(Player1)) */
END

>>>>>>>>
COPY ~d5/d5encut2.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5encut2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5encut2.baf~

<<<<<<<< d5/belt+.baf
IF
	!See([ENEMY])
	CombatCounter(0)
	Global("D5EntarCut","GLOBAL",6)
THEN
	RESPONSE #100
		Dialogue([PC])
END
>>>>>>>>
COPY ~d5/belt+.baf~ ~weidu_external/%MOD_FOLDER%/compile/belt+.baf~
EXTEND_TOP ~BELT.BCS~ ~weidu_external/%MOD_FOLDER%/compile/belt+.baf~

<<<<<<<< d5/d5imcut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("%IMOEN_DV%")
		DisplayStringHead(Myself,@25040)
		MoveToPoint([530.360])
		SmallWait(1)
		SetGlobal("D5ImoenPalace","GLOBAL",2)
		SmallWait(2)
		EndCutSceneMode()
		ActionOverride("LIIA",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5imcut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5imcut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5imcut1.baf~ EVALUATE_BUFFER

<<<<<<<< d5/palace+5.d
APPEND BELT

IF ~Global("D5KorlaszDone","GLOBAL",1) Global("D5MazeOpen","GLOBAL",0) Global("D5EntarCut","GLOBAL",0)~ BEGIN d5beltkd_0
  SAY @25001
  IF ~Dead("BDKORLAS")~ GOTO d5beltkd_2
  IF ~!Dead("BDKORLAS")~ GOTO d5beltkd_3
END

IF ~Global("D5KorlaszDone","GLOBAL",2) Global("D5MazeOpen","GLOBAL",0)~ BEGIN d5beltkd_1
  SAY @25004
  IF ~!PartyHasItem("D5SCRLSK") Global("D5SarevokFoundJournal","GLOBAL",0)~ REPLY @25005 GOTO d5beltkd_5
  IF ~!PartyHasItem("D5SCRLSK") Global("D5SarevokFoundJournal","GLOBAL",1)~ REPLY @25006 GOTO d5beltkd_6
  IF ~PartyHasItem("D5SCRLSK")~ REPLY @25006 GOTO d5beltkd_6
END

IF ~~ BEGIN d5beltkd_2
  SAY @25002
  IF ~~ GOTO d5beltkd_4
END

IF ~~ BEGIN d5beltkd_3
  SAY @25003
  IF ~~ GOTO d5beltkd_4
END

IF ~~ BEGIN d5beltkd_4
  SAY @25004
  IF ~!PartyHasItem("D5SCRLSK") Global("D5SarevokFoundJournal","GLOBAL",0)~ REPLY @25005 GOTO d5beltkd_5
  IF ~!PartyHasItem("D5SCRLSK") Global("D5SarevokFoundJournal","GLOBAL",1)~ REPLY @25006 GOTO d5beltkd_6
  IF ~PartyHasItem("D5SCRLSK")~ REPLY @25006 GOTO d5beltkd_6
END

IF ~~ BEGIN d5beltkd_5
  SAY @25007
  IF ~~ DO ~SetGlobal("D5KorlaszDone","GLOBAL",2)~ EXIT
END

IF ~~ BEGIN d5beltkd_6
  SAY @25008
  IF ~~ EXTERN LIIA d5liiakd_6
END

IF ~Global("D5EntarCut","GLOBAL",6)~ BEGIN d5beltkd_7
  SAY @25031
  IF ~~ DO ~SetGlobal("D5EntarCut","GLOBAL",7)~ GOTO d5beltkd_8
END

IF ~~ BEGIN d5beltkd_8
  SAY @25032
  IF ~~ DO ~SetGlobal("D5MazeOpen","GLOBAL",1) AddJournalEntry(@25035,QUEST)~ EXIT
END

IF ~Global("D5EntarCut","GLOBAL",7) !Dead("SAREVOK")~ BEGIN d5beltkd_9
  SAY @25033
  IF ~~ EXIT
END

END

APPEND LIIA

IF ~Global("D5KorlaszDone","GLOBAL",1) Global("D5MazeOpen","GLOBAL",0) Global("D5EntarCut","GLOBAL",0)~ BEGIN d5liiakd_0
  SAY @25001
  IF ~Dead("BDKORLAS")~ GOTO d5liiakd_2
  IF ~!Dead("BDKORLAS")~ GOTO d5liiakd_3
END

IF ~Global("D5KorlaszDone","GLOBAL",2) Global("D5MazeOpen","GLOBAL",0)~ BEGIN d5liiakd_1
  SAY @25004
  IF ~!PartyHasItem("D5SCRLSK")~ REPLY @25005 GOTO d5liiakd_5
  IF ~PartyHasItem("D5SCRLSK")~ REPLY @25006 GOTO d5liiakd_6
END

IF ~~ BEGIN d5liiakd_2
  SAY @25002
  IF ~~ GOTO d5liiakd_4
END

IF ~~ BEGIN d5liiakd_3
  SAY @25003
  IF ~~ GOTO d5liiakd_4
END

IF ~~ BEGIN d5liiakd_4
  SAY @25004
  IF ~!PartyHasItem("D5SCRLSK")~ REPLY @25005 GOTO d5liiakd_5
  IF ~PartyHasItem("D5SCRLSK")~ REPLY @25006 GOTO d5liiakd_6
END

IF ~~ BEGIN d5liiakd_5
  SAY @25007
  IF ~~ DO ~SetGlobal("D5KorlaszDone","GLOBAL",2)~ EXIT
END

IF ~~ BEGIN d5liiakd_6
  SAY @25009
  IF ~~ GOTO d5liiakd_7
END

IF ~~ BEGIN d5liiakd_7
  SAY @25010
  IF ~~ GOTO d5liiakd_8
END

IF ~~ BEGIN d5liiakd_8
  SAY @25011
  IF ~~ GOTO d5liiakd_9
END

IF ~~ BEGIN d5liiakd_9
  SAY @25012
  IF ~~ DO ~SetGlobal("D5EntarCut","GLOBAL",1)~ EXIT
END

IF ~Global("D5EntarCut","GLOBAL",3)~ BEGIN d5liiakd_10
  SAY @25021
  IF ~~ EXTERN BDENTAR d5entarkd_1
END

IF ~~ BEGIN d5liiakd_11
  SAY @25023
  IF ~~ EXTERN BDENTAR d5entarkd_2
END

IF ~~ BEGIN d5liiakd_12
  SAY @25025
  IF ~~ EXTERN BDENTAR d5entarkd_3
END

IF ~~ BEGIN d5liiakd_13
  SAY @25027
  IF ~~ EXTERN BDENTAR d5entarkd_4
END

IF ~~ BEGIN d5liiakd_14
  SAY @25029
  IF ~~ DO ~SetGlobal("D5EntarCut","GLOBAL",4)~ JOURNAL @25034 EXIT
END

IF ~Global("D5EntarCut","GLOBAL",7) !Global("D5ImoenPalace","GLOBAL",2) !Dead("SAREVOK")~ BEGIN d5liiakd_15
  SAY @25033
  IF ~~ EXIT
END

IF ~Global("D5ImoenPalace","GLOBAL",2) Global("D5CloneCut","GLOBAL",0)~ BEGIN d5liiakd_16
  SAY @25041
  IF ~!Dead("IMOEN") GlobalLT("D5ImoenChap","GLOBAL",2)~ EXTERN BDIMOEN d5imoenkd_0
  IF ~!Dead("IMOEN") GlobalGT("D5ImoenChap","GLOBAL",1) GlobalLT("D5ImoenChap","GLOBAL",5)~ EXTERN BDIMOEN d5imoenkd_1
  IF ~!Dead("IMOEN") GlobalGT("D5ImoenChap","GLOBAL",4)~ EXTERN BDIMOEN d5imoenkd_2
  IF ~Dead("IMOEN")~ EXTERN BDIMOEN d5imoenkd_3
END

IF ~~ BEGIN d5liiakd_17
  SAY @25048
  IF ~~ EXTERN BDIMOEN d5imoenkd_5
END

IF ~~ BEGIN d5liiakd_18
  SAY @25050
  IF ~~ REPLY @25051 EXTERN BDIMOEN d5imoenkd_6
  IF ~~ REPLY @25052 GOTO d5liiakd_19
  IF ~~ REPLY @25053 GOTO d5liiakd_19
END

IF ~~ BEGIN d5liiakd_19
  SAY @25055
  IF ~~ DO ~SetGlobal("D5ImoenPalace","GLOBAL",5)~ JOURNAL @25039 EXIT
END

END

ADD_TRANS_TRIGGER BDLIIA 9 ~Global("D5EntarCut","GLOBAL",0)~ DO 2

APPEND BDENTAR

IF ~Global("D5EntarCut","GLOBAL",3)~ BEGIN d5entarkd_0
  SAY @25020
  IF ~~ EXTERN LIIA d5liiakd_10
END

IF ~~ BEGIN d5entarkd_1
  SAY @25022
  IF ~~ EXTERN LIIA d5liiakd_11
END

IF ~~ BEGIN d5entarkd_2
  SAY @25024
  IF ~!IsValidForPartyDialogue("SKIE")~ EXTERN LIIA d5liiakd_12
  IF ~IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_GOOD)~ EXTERN SKIE d5skiekd_0
  IF ~IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_GENEUTRAL)~ EXTERN SKIE d5skiekd_0
  IF ~IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_EVIL)~ EXTERN SKIE d5skiekd_1
END

IF ~~ BEGIN d5entarkd_3
  SAY @25026
  IF ~~ EXTERN LIIA d5liiakd_13
END

IF ~~ BEGIN d5entarkd_4
  SAY @25028
  IF ~~ EXTERN LIIA d5liiakd_14
END

END

APPEND SKIE

IF ~~ BEGIN d5skiekd_0
  SAY @25017
  IF ~~ EXTERN LIIA d5liiakd_12
END

IF ~~ BEGIN d5skiekd_1
  SAY @25018
  IF ~~ EXTERN LIIA d5liiakd_12
END

END

APPEND BDIMOEN

IF ~Global("D5ImoenPalace","GLOBAL",2)~ BEGIN d5imoenkd_0
  SAY @25042
  IF ~~ GOTO d5imoenkd_4
END

IF ~~ BEGIN d5imoenkd_1
  SAY @25043
  IF ~~ GOTO d5imoenkd_4
END

IF ~~ BEGIN d5imoenkd_2
  SAY @25044
  IF ~~ GOTO d5imoenkd_4
END

IF ~~ BEGIN d5imoenkd_3
  SAY @25045
  IF ~~ GOTO d5imoenkd_4
END

IF ~~ BEGIN d5imoenkd_4
  SAY @25047
  IF ~~ EXTERN LIIA d5liiakd_17
END

IF ~~ BEGIN d5imoenkd_5
  SAY @25049
  IF ~~ EXTERN LIIA d5liiakd_18
END

IF ~~ BEGIN d5imoenkd_6
  SAY @25054
  IF ~~ EXTERN LIIA d5liiakd_19
END

IF WEIGHT #-1 ~Global("D5ImoenPalace","GLOBAL",5) !Dead("SAREVOK")~ BEGIN d5imoenkd_7
  SAY @25056
  IF ~~ REPLY @25057 GOTO d5imoenkd_8
  IF ~~ REPLY @25059 GOTO d5imoenkd_9
END

IF ~~ BEGIN d5imoenkd_8
  SAY @25058
  IF ~~ EXIT
END

IF ~~ BEGIN d5imoenkd_9
  SAY @25060
  IF ~~ EXIT
END

IF ~~ BEGIN d5imoenkd_10
  SAY @25061
  IF ~~ EXIT
END

END
>>>>>>>> 
COPY ~d5/palace+5.d~ ~weidu_external/%MOD_FOLDER%/compile/palace+5.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/palace+5.d~

//modify trigger to activate entrance to maze

ACTION_IF (FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0153.bcs~) BEGIN
  COPY_EXISTING ~%bg1_area_prefix%0153.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("DukeThanks","GLOBAL",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Global("D5MazeOpen","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(Global("DukeThanks","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Global("D5MazeOpen","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!Exists("DENKOD")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~!InMyArea("DENKOD")~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!Dead("DENKOD")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ApplySpellRES("ohdest","Rogue_\).+$~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("DENKOD",DestroySelf())\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_DenkodTalk","MYAREA",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END

	END
  BUT_ONLY
END

<<<<<<<< d5/area0153.baf
IF
	Allegiance("DENKOD",[EVILCUTOFF])
	Global("AttackedThieves","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("AttackedThieves","GLOBAL",1)
		Continue()
END
>>>>>>>>
COPY ~d5/area0153.baf~ ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0153.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0153.bcs~) BEGIN
  EXTEND_TOP ~%bg1_area_prefix%0153.bcs~ ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0153.baf~
END
ACTION_IF !(FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0153.bcs~) BEGIN
  COMPILE ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0153.baf~
END

// change Denkod's dialogue

<<<<<<<< d5/denkod+5.d
REPLACE_STATE_TRIGGER DENKOD 0 ~Global("D5MazeOpen","GLOBAL",99)~

APPEND DENKOD

IF WEIGHT #-1 ~Global("D5MazeOpen","GLOBAL",1) !Dead("SAREVOK")~ BEGIN d5denkod_0
  SAY @25063
  IF ~~ REPLY @25064 DO ~Enemy()~ DO ~SetGlobal("AttackedThieves","GLOBAL",1)~ EXIT
  IF ~~ REPLY @25065 GOTO d5denkod_1
END

IF ~~ BEGIN d5denkod_1
  SAY @25066
  IF ~~ GOTO d5denkod_2
END

IF ~~ BEGIN d5denkod_2
  SAY @25067
  IF ~~ REPLY @25068 GOTO d5denkod_3
END

IF ~~ BEGIN d5denkod_3
  SAY @25069
  IF ~~ REPLY @25070 GOTO d5denkod_4
END

IF ~~ BEGIN d5denkod_4
  SAY @25071
  IF ~~ REPLY @25072 GOTO d5denkod_5
END

IF ~~ BEGIN d5denkod_5
  SAY @25073
  IF ~~ REPLY @25074 GOTO d5denkod_6
END

IF ~~ BEGIN d5denkod_6
  SAY @25075
  IF ~~ DO ~AddJournalEntry(@25079,QUEST)~ EXIT
END

IF WEIGHT #-1 ~Global("D5MazeOpen","GLOBAL",2)~ BEGIN d5denkod_7
  SAY @25076
  IF ~~ EXIT
END

END
>>>>>>>> 
COPY ~d5/denkod+5.d~ ~weidu_external/%MOD_FOLDER%/compile/denkod+5.d~


// now compile the dialogue

COMPILE ~weidu_external/%MOD_FOLDER%/compile/denkod+5.d~


// remove voleta stiletto

COPY_EXISTING ~voleta.cre~ ~override~
	WRITE_ASCII 0x280 ~VOLETA~ #20
IF_EXISTS BUT_ONLY

<<<<<<<< d5/area0146.baf
IF
	GlobalGT("D5MazeOpen","GLOBAL",0)
	Global("D5VoletaGone","%bg1_area_prefix%0146",0)
THEN
	RESPONSE #100
		ActionOverride("VOLETA",DestroySelf())		
		SetGlobal("D5VoletaGone","%bg1_area_prefix%0146",1)
		Continue()
END

IF
	Global("D5MazeOpen","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5MazeOpen","GLOBAL",2)
		Continue()
END
>>>>>>>>
COPY ~d5/area0146.baf~ ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0146.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0146.bcs~) BEGIN
  EXTEND_TOP ~%bg1_area_prefix%0146.bcs~ ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0146.baf~ EVALUATE_BUFFER
END
ACTION_IF !(FILE_EXISTS_IN_GAME ~%bg1_area_prefix%0146.bcs~) BEGIN
  COMPILE ~weidu_external/%MOD_FOLDER%/compile/%bg1_area_prefix%0146.baf~ EVALUATE_BUFFER
END

// ***** write out journal entries (34, 35, 39, 79)


// remove imoen from the world if she is hanging out somwehere

<<<<<<<< d5/imoen+5.baf
IF
	OR(2)
		Global("D5ImoenPalace","GLOBAL",2)
		Global("D5ImoenPalace","GLOBAL",5)
	!AreaCheck("%NBaldursGate_DucalPalace_L1%")
	!InPartyAllowDead(Myself)
THEN
	RESPONSE #100
		EscapeArea()
END
>>>>>>>>
COPY ~d5/imoen+5.baf~ ~weidu_external/%MOD_FOLDER%/compile/imoen+5.baf~

ACTION_IF (GAME_IS ~eet~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~imoen_.bcs~) BEGIN
    EXTEND_TOP ~imoen_.bcs~ ~weidu_external/%MOD_FOLDER%/compile/imoen+5.baf~
  END
END
ACTION_IF !(GAME_IS ~eet~) /* AND (FILE_EXISTS_IN_GAME ~imoen_.bcs~) huh? */ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~imoen.bcs~) BEGIN
    EXTEND_TOP ~imoen.bcs~ ~weidu_external/%MOD_FOLDER%/compile/imoen+5.baf~
  END
END

//___________________________________________________________________________________
//

END	//	end define function