
DEFINE_ACTION_FUNCTION clone_saga BEGIN

/*

plan: change the skie killing scene, change Bence's dialogue, jump to the palace, then join the comp 80 scene in the jail

/ - add new scene where Skie is told she must go back to BG
  
/ - and write the dialogue for it

/ - remove skie and bdcrum30 peeps from bd4000 if necessary
 
/ - remove skie from bd4100 if necessary
 
/ - have Dazzos' dialogue shoot you to a rewritten bdcut60
 
/ - new dialogue with bence - someone got sending saying skie was killed (joke about teleporting bhaalspawn)
 
/ - that triggers rewritten bdcut61, starting at door of palace and walking to belt
 
/ - new area for palace scene
 
 - ***** then new belt dialogue, tracking #70, which triggers d5grcut1 and leads into #80
 
/ - install #80 if not already done

 - ***** add journal entries!!!

 
Later on, maybe add more:

 -- ***** maybe add a cut scene to the scrying pool where you can see Charclone casing the Silvershield estate

 -- ***** add new cut scene for skie's death
 		...dream version of the silvershield estate
 			...OR, out in public in the street
 		...charname approaches skie, some talking via DisplayStringHead
 		...then charname transforms into creature, and kills her.

sequence with new skie death scene: 

modify Dragonspear rest action	
	...bdcut60
	...change it to have player1 move to the new custom area AFTER it sets DreamArea on bd4100
add new skie death cut scene
	...then in bdcut60x, need to change coordinates to work in the new area
	...then replace bdcut60y: don't let go of cutscene mode, have Player1 change to bdskiedr and attack skie; have skie fight back; then skie dies, change back to normal form, and jump back to bd4100
	...need to rewrite bdcut60a: lose the ambientactivates, change the coordinates of confrontation, lose bence's StringHead, set a variable
cut scene to wake up in Dragonspear and modify Bence dialogue
	...change bdbence's dialogue - new state instead of state 6
	...replace bdcut61.bcs with one that moves to the palace: start at the door and walk to Belt; have bence walk with you. then set gab variable and have belt talk
palace dialogue to replace the stuff from comp 70
	...need to make this jump ahead of the #80 lines. I think.
	...write the dialogue

*/

//___________________________________________________________________________________
//

// prep journal entries ...?

//ADD_JOURNAL TITLE (@9001) @9002


// copy in stuff

ACTION_IF !(GAME_IS ~eet~) BEGIN
  COPY ~%MOD_FOLDER%/data/switcheroo/d50108.are~ ~override~
END
ACTION_IF (GAME_IS ~eet~) BEGIN
  COPY ~%MOD_FOLDER%/data/switcheroo/d50108_eet.are~ ~override/d50108.are~
END

COPY ~%MOD_FOLDER%/data/switcheroo/d50108.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/d50108.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/290/d50108.baf~


// have Skie leave the Dragonspear map if you don't save her

<<<<<<<< d5/d5skie+.baf
IF
	GlobalGT("bd_skie_plot","global",15)
	OR(2)
		AreaCheck("bd4000")  // Dragonspear Castle, Exterior
		AreaCheck("BD4100")  // Dragonspear Castle Keep, First Floor
THEN
	RESPONSE #100
		EscapeArea()
END
>>>>>>>>
COPY ~d5/d5skie+.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/d5skie+.baf~
EXTEND_BOTTOM ~bdskie.bcs~ ~weidu_external/%MOD_FOLDER%/compile/290/d5skie+.baf~

<<<<<<<< d5/d5crum+.baf
IF
	GlobalGT("bd_skie_plot","global",15)
	AreaCheck("bd4000")  // Dragonspear Castle, Exterior
THEN
	RESPONSE #100
		EscapeArea()
END
>>>>>>>>
COPY ~d5/d5crum+.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/d5crum+.baf~
EXTEND_BOTTOM ~bdcrum30.bcs~ ~weidu_external/%MOD_FOLDER%/compile/290/d5crum+.baf~


// insert skie leaving scene 
/*
COPY_EXISTING ~BD3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(RemoveMapNote([1950.1565]\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~CreateCreature("bdbence",[1350.230],NE) CreateCreature("bdskie",[1330.210],NE) Wait(1) ActionOverride("BDBENCE",StartDialogueNoSet(Player1)) RemoveMapNote([1950.1565]~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
*/
<<<<<<<< d5/d53000+.baf
IF
	Global("bd_skie_plot","global",99)
	InMyArea("BDSKIE")
THEN
	RESPONSE #100
		ActionOverride("BDSKIE",EscapeArea())
		Continue()
END

IF
	Global("bd_plot","global",385)
	GlobalLT("bd_skie_plot","global",16)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("bd_skie_plot","global",16)
		CreateCreature("bdbence",[1350.230],NE)
		CreateCreature("bdskie",[1330.210],NE)
		Wait(1) 
		ActionOverride("BDBENCE",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d53000+.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/d53000+.baf~
EXTEND_TOP ~BD3000.bcs~ ~weidu_external/%MOD_FOLDER%/compile/290/d53000+.baf~


// delay being told about the parlay

<<<<<<<< d5/d5trust2.baf
IF
	Global("bd_skie_plot","global",99)
	NumberOfTimesTalkedTo(0)
	Range([PC],25)
THEN
	RESPONSE #100
		Wait(2)
		StartDialogueNoSet(Player1)
END
>>>>>>>>
COPY ~d5/d5trust2.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/bdtrust2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/290/bdtrust2.baf~


// VERSION A: simplified ending sequence

<<<<<<<< d5/bdcut60.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ClearAllActions()
		SetGlobal("D5AltEndSoD","GLOBAL",1)
		FadeToColor([30.0],0)
		Wait(2)
		DisplayStringPointLog([1945.795],%eet_2%66919)  // The celebrations last long into the night. Between the feasting and the drinking, you hardly have time to shake hands with all those who want to thank the hero of Baldur's Gate and savior of Dragonspear. One by one, the celebrants stagger into the reclaimed castle to find a place to rest. It is the wee hours of the morning when you climb into your own bedroll, falling instantly into a deep sleep.
		Wait(6)
		ActionOverride(Player2,LeaveAreaLUA("bd4100","",[1155.720],NW))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player3,LeaveAreaLUA("bd4100","",[1080.560],SE))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player4,LeaveAreaLUA("bd4100","",[950.625],SE))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player5,LeaveAreaLUA("bd4100","",[1320.665],NW))  // Dragonspear Castle Keep, First Floor
		ActionOverride(Player6,LeaveAreaLUA("bd4100","",[880.690],SE))  // Dragonspear Castle Keep, First Floor
		LeaveAreaLUAPanic("bd4100","",[1235.685],NW)  // Dragonspear Castle Keep, First Floor
		LeaveAreaLUA("bd4100","",[1235.685],NW)  // Dragonspear Castle Keep, First Floor
		MultiPlayerSync()
		Wait(1)
		ReallyForceSpellRES("bdremove",Myself)  // No such index
		ApplySpellRES("bdrejuve",Myself)  // No such index
		ApplySpellRES("bdrejuve",Player2)  // No such index
		ApplySpellRES("bdrejuve",Player3)  // No such index
		ApplySpellRES("bdrejuve",Player4)  // No such index
		ApplySpellRES("bdrejuve",Player5)  // No such index
		ApplySpellRES("bdrejuve",Player6)  // No such index
		SetSequence(SEQ_SLEEP)
		ActionOverride(Player2,SetSequence(SEQ_SLEEP))
		ActionOverride(Player3,SetSequence(SEQ_SLEEP))
		ActionOverride(Player4,SetSequence(SEQ_SLEEP))
		ActionOverride(Player5,SetSequence(SEQ_SLEEP))
		ActionOverride(Player6,SetSequence(SEQ_SLEEP))
		Wait(4)
		FadeFromColor([30.0],0)
		CreateCreature("bdbence",[1300.910],S)  // Bence Duncan
		ActionOverride("BDBENCE",MoveToPoint([1100.825]))
		Wait(4)
		ActionOverride("BDBENCE",FaceObject(Player1))
		SetSequence(SEQ_AWAKE)
		SmallWait(5)
		FaceObject("bdbence")  // Bence Duncan
		Wait(1)
		EndCutSceneMode()
		ActionOverride("BDBENCE",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/bdcut60.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/bdcut60.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/290/bdcut60.baf~ EVALUATE_BUFFER

<<<<<<<< d5/d5cut61t.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(1)
		FadeToColor([30.0],0)
		Wait(2)
		ActionOverride("bdimoitm",MoveBetweenAreas("d50108",[875.560],S))
		ActionOverride(Player2,LeaveAreaLUA("d50108","",[830.490],S)) 
		ActionOverride(Player3,LeaveAreaLUA("d50108","",[785.515],S)) 
		ActionOverride(Player4,LeaveAreaLUA("d50108","",[890.485],S)) 
		ActionOverride(Player5,LeaveAreaLUA("d50108","",[840.515],S)) 
		ActionOverride(Player6,LeaveAreaLUA("d50108","",[815.540],S)) 
		LeaveAreaLUAPanic("d50108","",[775.475],SW)  // palace
		LeaveAreaLUA("d50108","",[775.475],SW)  // palace
/*		ActionOverride(Player2,LeaveAreaLUA("d50108","",[415.615],S)) 
		ActionOverride(Player3,LeaveAreaLUA("d50108","",[405.675],S)) 
		ActionOverride(Player4,LeaveAreaLUA("d50108","",[455.705],S)) 
		ActionOverride(Player5,LeaveAreaLUA("d50108","",[485.735],S)) 
		ActionOverride(Player6,LeaveAreaLUA("d50108","",[515.765],S)) 
		LeaveAreaLUAPanic("d50108","",[465.635],SW)  // palace
		LeaveAreaLUA("d50108","",[465.635],SW)  // palace
*/		MultiPlayerSync()
		Wait(1)
		FadeFromColor([30.0],0)
/*		MoveViewPoint([775.475],VERY_FAST)
		Wait(1)
		MoveToPoint([775.475])
		Wait(1)
		ActionOverride(Player2,MoveToPoint([830.490])) 
		SmallWait(6)
		ActionOverride(Player3,MoveToPoint([785.515])) 
		SmallWait(6)
		ActionOverride(Player4,MoveToPoint([890.485])) 
		SmallWait(6)
		ActionOverride(Player5,MoveToPoint([840.515])) 
		SmallWait(6)
		ActionOverride(Player6,MoveToPoint([815.540])) 
*/		Wait(5)
	//	SetGlobal("bd_npc_camp_chapter","global",7)
	// might need to set other variables
	//	IncrementChapter("chptxt13")
		SetCursorState(TRUE)
		EndCutSceneMode()
		ActionOverride("Belt",StartDialogNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5cut61t.baf~ ~weidu_external/%MOD_FOLDER%/compile/290/bdcut61t.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/290/bdcut61t.baf~


// dialogue changes: bence in camp, bence in dragonspear, belt in palace

// ***** add palace dialogue, ending with belt setting the variable for gregson to appear:


<<<<<<<< d5/sodfinal+.d
/*
ADD_STATE_TRIGGER BDBENCE 33 ~Global("SomeVariable","GLOBAL",0)~	//	set variable to avoid this

ADD_STATE_TRIGGER BDBENCE 39 ~Global("SomeVariable","GLOBAL",0)~	//	set variable to avoid this
*/

ADD_STATE_TRIGGER BDBENCE 6 ~GlobalLT("bd_skie_plot","global",16)~	//	set variable to avoid this

APPEND BDBENCE

IF WEIGHT #-1 ~GlobalGT("bd_plot","global",349) GlobalLT("bd_plot","global",388) GlobalLT("bd_skie_plot","global",99) AreaCheck("BD3000")~ BEGIN d5bdbence_0
  SAY @29001
  IF ~~ EXTERN BDSKIE d5bdskie_0
END

IF ~~ BEGIN d5bdbence_1
  SAY @29003
  IF ~~ EXTERN BDSKIE d5bdskie_1
END

IF ~~ BEGIN d5bdbence_2
  SAY @29005
  IF ~~ EXTERN BDSKIE d5bdskie_2
END

IF ~~ BEGIN d5bdbence_3
  SAY @29007
  IF ~~ EXTERN BDSKIE d5bdskie_3
END

IF ~~ BEGIN d5bdbence_4
  SAY @29011
  IF ~~ GOTO d5bdbence_5
END

IF ~~ BEGIN d5bdbence_5
  SAY @29012
  IF ~~ DO ~SetGlobal("bd_skie_plot","global",99) ActionOverride("BDSKIE",EscapeArea()) ActionOverride("BDBENCE",EscapeAreaNoSee())~ EXIT
END

IF WEIGHT #-1 ~GlobalGT("bd_skie_plot","global",15) AreaCheck("BD4100")~ BEGIN d5bdbence_6
  SAY @29021
  IF ~~ REPLY @29022 GOTO d5bdbence_7
END

IF ~~ BEGIN d5bdbence_7
  SAY @29023
  IF ~~ REPLY @29024 GOTO d5bdbence_8
  IF ~~ REPLY @29025 GOTO d5bdbence_8
END

IF ~~ BEGIN d5bdbence_8
  SAY @29026
  IF ~~ REPLY @29027 GOTO d5bdbence_9
END

IF ~~ BEGIN d5bdbence_9
  SAY @29028
  IF ~~ REPLY @29029 GOTO d5bdbence_10
END

IF ~~ BEGIN d5bdbence_10
  SAY @29030
  IF ~~ GOTO d5bdbence_11
END

IF ~~ BEGIN d5bdbence_11
  SAY @29031
  IF ~~ REPLY @29032 GOTO d5bdbence_12
END

IF ~~ BEGIN d5bdbence_12
  SAY @29033
  IF ~~ DO ~StartCutSceneMode() StartCutSceneEx("bdcut61",TRUE)~ EXIT
END

END

ALTER_TRANS BDBENCE // file name
  BEGIN 65 END // state number (can be more than one)
  BEGIN 0 END // transition number (can be more than one)
  BEGIN "EPILOGUE" ~EXIT~ END

APPEND BDSKIE

IF ~~ BEGIN d5bdskie_0
  SAY @29002
  IF ~~ EXTERN BDBENCE d5bdbence_1
END

IF ~~ BEGIN d5bdskie_1
  SAY @29004
  IF ~~ EXTERN BDBENCE d5bdbence_2
END

IF ~~ BEGIN d5bdskie_2
  SAY @29006
  IF ~~ EXTERN BDBENCE d5bdbence_3
END

IF ~~ BEGIN d5bdskie_3
  SAY @29008
  IF ~~ REPLY @29009 EXTERN BDBENCE d5bdbence_4
  IF ~~ REPLY @29010 EXTERN BDBENCE d5bdbence_4
END

END

ADD_STATE_TRIGGER BELT 0 ~GlobalLT("bd_skie_plot","global",16)~	//	set variable to avoid this

APPEND BELT

IF WEIGHT #-1 ~Global("D5AltEndSoD","GLOBAL",1) AreaCheck("D50108") Global("D5GregsonPalace","GLOBAL",0)~ BEGIN d5beltfin_0
  SAY @29041
  IF ~~ GOTO d5beltfin_1
END

IF ~~ BEGIN d5beltfin_1
  SAY @29042
  IF ~~ EXTERN BDENTAR d5entarfin_0
END

IF ~~ BEGIN d5beltfin_2
  SAY @29056
  IF ~~ REPLY @29057 DO ~SetGlobal("D5GregsonPalace","GLOBAL",1)~ EXIT
  IF ~~ REPLY @29058 DO ~SetGlobal("D5GregsonPalace","GLOBAL",1)~ EXIT
END

END

APPEND LIIA

IF ~~ BEGIN d5liiafin_0
  SAY @29044
  IF ~~ REPLY @29045 EXTERN BDENTAR d5entarfin_1
  IF ~Global("D5PartyHadShard","GLOBAL",1)~ REPLY @29046 GOTO d5liiafin_1
  IF ~Global("D5PartyHadSoultaker","GLOBAL",2)~ REPLY @29047 GOTO d5liiafin_2
END

IF ~~ BEGIN d5liiafin_1
  SAY @29050
  IF ~~ EXTERN BDENTAR d5entarfin_1
END

IF ~~ BEGIN d5liiafin_2
  SAY @29051
  IF ~~ GOTO d5liiafin_3
END

IF ~~ BEGIN d5liiafin_3
  SAY @29053
  IF ~~ EXTERN BDENTAR d5entarfin_1
END

END

APPEND BDENTAR

IF ~~ BEGIN d5entarfin_0
  SAY @29043
  IF ~~ EXTERN LIIA d5liiafin_0
END

IF ~~ BEGIN d5entarfin_1
  SAY @29054
  IF ~~ GOTO d5entarfin_2
END

IF ~~ BEGIN d5entarfin_2
  SAY @29055
  IF ~~ REPLY @29059 EXTERN BELT d5beltfin_2
END

END
>>>>>>>> 
COPY ~d5/sodfinal+.d~ ~weidu_external/%MOD_FOLDER%/compile/290/sodfinal+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/290/sodfinal+.d~



// if not already installed comp 200

ACTION_IF !(MOD_IS_INSTALLED ~Reflections.tp2~ ~200~) BEGIN

  INCLUDE ~%MOD_FOLDER%/comp/comp_80.tpa~
  LAF mirror_man END

END	//	end if #200 is not installed

//___________________________________________________________________________________
//

END	//	end define function

////////////////////
////////////////////
////////////////////
