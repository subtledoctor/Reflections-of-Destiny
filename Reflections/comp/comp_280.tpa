
DEFINE_ACTION_FUNCTION mirror_man BEGIN

/*  

TO DO:

// *****
	- make everyone unselectable in AR0108 ...via CRE
	- if name, make selectable, then take equipment, then leave party, in D50104 ...via CRE
	- if no name, make selectable, then moveglobal in D50105 ...via area script

// ***** maybe... add/change an info trigger in AR0602 to say one of the tank people looks like you?

*/

//___________________________________________________________________________________
//

// prep journal entries ...?

//ADD_JOURNAL TITLE (@28001) @28002


// copy in stuff

ACTION_IF !(GAME_IS ~eet~) BEGIN
  COPY ~%MOD_FOLDER%/data/switcheroo/d50104.are~ ~override~
  COPY ~%MOD_FOLDER%/data/switcheroo/d50105.are~ ~override~
  COPY ~%MOD_FOLDER%/data/switcheroo/d56000.are~ ~override~
END
ACTION_IF (GAME_IS ~eet~) BEGIN
  COPY ~%MOD_FOLDER%/data/switcheroo/d50104_eet.are~ ~override/d50104.are~
  COPY ~%MOD_FOLDER%/data/switcheroo/d50105_eet.are~ ~override/d50105.are~
  COPY ~%MOD_FOLDER%/data/switcheroo/d56000_eet.are~ ~override/d56000.are~
END
COPY ~%MOD_FOLDER%/data/switcheroo/d5bdimo.cre~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5chrnam.cre~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5telbgt.cre~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5_div.spl~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5pause.spl~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5pausx.spl~ ~override~

COPY ~%MOD_FOLDER%/data/switcheroo/d50104.baf~ ~weidu_external/%MOD_FOLDER%/compile/d50104.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d50104.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d50105.baf~ ~weidu_external/%MOD_FOLDER%/compile/d50105.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d50105.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d56000.baf~ ~weidu_external/%MOD_FOLDER%/compile/d56000.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d56000.baf~ EVALUATE_BUFFER
COPY ~%MOD_FOLDER%/data/switcheroo/d5bdimo.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5bdimo.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5bdimo.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d5cut64.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5cut64.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5cut64.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d5telbgt.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5telbgt.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5telbgt.baf~ EVALUATE_BUFFER



// gregson cut scene in palace 

<<<<<<<< d5/ar108+8.baf
IF
	Global("D5GregsonCut","GLOBAL",0)
	Global("D5GregsonPalace","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5GregsonCut","GLOBAL",1)
		SetGlobal("SOD_fromimport","global",0)
		SetGlobal("ENDOFBG1","GLOBAL",1)
		CreateCreature("D5GREGS",[520.615],E)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5GRCUT1")
END

IF
	Global("D5CloneCut","GLOBAL",1)
	Global("D5GregsonPalace","GLOBAL",1)
THEN
	RESPONSE #100
		CreateCreature("D5TELBGT",[670.465],W)
		SetGlobal("D5CloneCut","GLOBAL",2)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5CLCUT1")
END
>>>>>>>>
COPY ~d5/ar108+8.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar108+8.baf~
EXTEND_TOP ~%bg1_area_prefix%0108.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar108+8.baf~

// grcut1 = gregson run in and say charname is in custody

<<<<<<<< d5/d5grcut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("D5GREGS")  // Captain Gregson
		DisplayStringHead(Myself,@28005)  // Lord Entar! You must come quickly! Oh, hello everyone...
		MoveToPoint([640.465])
		Wait(1)
		SetGlobal("D5CloneCut","GLOBAL",1)
		EndCutSceneMode()
		ActionOverride("D5GREGS",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5grcut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5grcut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5grcut1.baf~

// clcut1 = everyone leave the palace
 
<<<<<<<< d5/d5clcut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride("D5GREGS",EscapeAreaObject("Door0200"))
		ActionOverride("BDENTAR",EscapeAreaObject("Door0200"))
		ActionOverride("BELT",EscapeAreaObject("Door0200"))
		ActionOverride("LIIA",EscapeAreaObject("Door0200"))
		Wait(2)
		MoveToPoint([460.715])
		FadeToColor([20.0],0)
		LeaveAreaLUAPanic("d50104","",[805.760],S)
		LeaveAreaLUA("d50104","",[805.760],S)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut1.baf~

// clcut2 = arrive at FF jail, go to cell

<<<<<<<< d5/d5clcut2.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		OpenDoor("door03")
		CreateCreatureCopyPoint("D5CHRNAM",Player1,[410.340]) 
		CreateCreature("D5GREGS",[745.680],W)  
		CreateCreature("BDENTAR",[775.700],W) 
		CreateCreature("BELT",[855.705],W) 
		CreateCreature("LIIA",[810.720],W) 
//		GiveItemCreate("D5BRACS","D5CHRNAM",1,0,0) 
//		XEquipItem("D5BRACS","D5CHRNAM",SLOT_GAUNTLETS,EQUIP) 
		FadeFromColor([30.0],0)
//		MoveViewPoint([805.760],INSTANT)
		SmallWait(9)
		MoveViewPoint([345.450],VERY_FAST)
		ActionOverride("D5GREGS",MoveToPoint([315.425]))
		SmallWait(7)
		ActionOverride("BDENTAR",MoveToPoint([260.455]))
		SmallWait(10)
		ActionOverride("LIIA",MoveToPoint([345.445]))
		Wait(1)
		ActionOverride("BELT",MoveToPoint([380.460]))
		Wait(1)
		MoveToPoint([330.470])
		Wait(1)
		EndCutSceneMode()
		ActionOverride("BELT",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut2.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut2.baf~

// clcut3 = liia casts a divination spell

<<<<<<<< d5/d5clcut3.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(2)
		ActionOverride("LIIA",ForceSpellRES("D5_DIV","D5CHRNAM"))  // No such index
		Wait(2)
		CreateVisualEffectObject("SPSTRENH","D5CHRNAM")  // No such index
		Wait(2)
		EndCutSceneMode()
		ActionOverride("LIIA",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut3.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut3.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut3.baf~

// clcut4 = timestop and switch places, followed by another liia divination
/*
<<<<<<<< d5/d5clcut4.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(1)
		ApplySpellRES("D5PAUSE",Myself) 
		ApplySpellRES("D5PAUSE","BDFF1002") 
		ApplySpellRES("D5PAUSE","D5GREGS") 
		ApplySpellRES("D5PAUSE","BELT") 
		ApplySpellRES("D5PAUSE","LIIA") 
		ApplySpellRES("D5PAUSE","BDENTAR") 
		Wait(1)
		AddAreaFlag(DREAMAREA)
		ActionOverride(Player2,MakeUnselectable(1))
		ActionOverride(Player3,MakeUnselectable(1))
		ActionOverride(Player4,MakeUnselectable(1))
		ActionOverride(Player5,MakeUnselectable(1))
		ActionOverride(Player6,MakeUnselectable(1))
		Wait(3)
		JumpToPoint([420.340])
		MoveViewPoint([405.360],INSTANT)
		ActionOverride("D5CHRNAM",JumpToPoint([320.490]))
		ActionOverride("K#ImportContainer",TakeCreatureItems(Player1,ALL))
		Wait(2)
		RemoveAreaFlag(DREAMAREA)
		Wait(1)
		ApplySpellRES("D5PAUSX",Myself) 
		ApplySpellRES("D5PAUSX","BDFF1002") 
		ApplySpellRES("D5PAUSX","D5GREGS") 
		ApplySpellRES("D5PAUSX","BELT") 
		ApplySpellRES("D5PAUSX","LIIA") 
		ApplySpellRES("D5PAUSX","BDENTAR") 
		Wait(1)
		ActionOverride("LIIA",DisplayStringHead(Myself,@28035))  // Apologies, whoever you are, this might sting a bit.
		Wait(1)
		ActionOverride("LIIA",ForceSpellRES("D5_DIV",Player1))
		Wait(2)
		CreateVisualEffectObject("ICGLYPTI",Myself)
		Wait(1)
		PlayDead(15)
		Wait(1)
		CreateCreature("D5TELBGT",[445.320],W)
		EndCutSceneMode()
		ActionOverride("LIIA",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut4.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut4.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut4.baf~
*/

// clcut5 = everyone leaves, you are stuck

<<<<<<<< d5/d5clcut5.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(1)
		ActionOverride("D5GREGS",EscapeAreaNoSee())
		ActionOverride("BDENTAR",EscapeAreaNoSee())
		ActionOverride("BELT",EscapeAreaNoSee())
		ActionOverride("LIIA",EscapeAreaNoSee())
		ActionOverride("D5CHRNAM",EscapeAreaNoSee())
		Wait(1)
		ActionOverride("witness",DestroySelf())
		ActionOverride("witness_guard",DestroySelf())
		ActionOverride("bdbence",DestroySelf())
		ActionOverride("bence_guard",DestroySelf())
		ActionOverride("bdeltan",DestroySelf())
		ActionOverride("Entar Guard 1",DestroySelf())
		ActionOverride("Entar Guard 2",DestroySelf())
		PlaySound("AMBDLBOO")
		Wait(2)
		MultiPlayerSync()
		Wait(1)
		CloseDoor("door01")
		CloseDoor("door02")
		CloseDoor("door07")
		CloseDoor("door08")
		Lock("door02")
		Lock("door07")
		Lock("door08")
		TriggerActivation("InfoBDStairs",FALSE)
		TriggerActivation("Infobdstairs_epilogue",TRUE)
		TriggerActivation("TrD50105",TRUE)
		AddAreaType(128)
		ReallyForceSpellRES("bdremove",Myself)  // No such index
		SmallWait(5)
		CreateCreature("bdrefuge",[344.619],NE)  // Refugee
		CreateCreature("bdrefgm2",[472.677],NE)  // Refugee
		CreateCreature("bdclara",[600.738],NE)  // Clara
		CreateCreature("bdrefgf1",[648.474],SW)  // Refugee
		CreateCreature("bdrefgf2",[762.545],SW)  // Refugee
		SetGlobal("bd_plot","global",605)
		SetGlobal("D5CloneCut","GLOBAL",10)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut5.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut5.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut5.baf~

// imoen in sewer cave

<<<<<<<< d5/d5clcut6.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SmallWait(9)
		ActionOverride("D5BDIMO",ForceSpellRES("SPWI206",Myself))  // Invisibility
		Wait(1)
		SetGlobal("bd_plot","global",641)
		ActionOverride("D5BDIMO",EscapeAreaNoSee())
		Wait(1)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut6.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut6.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut6.baf~


// dialogues

<<<<<<<< d5/switch+8.d
ADD_STATE_TRIGGER BELT 0 ~Global("D5CloneCut","GLOBAL",0)~

APPEND BELT

IF ~~ BEGIN d5beltsw_0
  SAY @28007
  IF ~Global("D5AltEndSoD","GLOBAL",0)~ EXTERN D5GREGS d5gregsw_2
  IF ~Global("D5AltEndSoD","GLOBAL",1)~ EXTERN D5GREGS d5gregsw_4
END

IF ~~ BEGIN d5beltsw_1
  SAY @28011
  IF ~~ EXTERN BDENTAR d5entarsw_1
END

IF ~~ BEGIN d5beltsw_2
  SAY @28014
  IF ~~ REPLY @28015 DO ~SetGlobal("D5CloneCut","GLOBAL",1)~ EXIT
END

/*
IF ~~ BEGIN d5beltsw_3
  SAY @28018
  IF ~~ REPLY @28019 DO ~SetGlobal("D5CloneCut","GLOBAL",1)~ EXIT
  IF ~~ REPLY @28020 DO ~SetGlobal("D5AllEnemy","GLOBAL",1)~ EXIT
END
*/

IF ~Global("D5GregsonPalace","GLOBAL",1) Global("D5CloneCut","GLOBAL",3) AreaCheck("D50104")~ BEGIN d5beltsw_4
  SAY @28024
  IF ~~ EXTERN BDENTAR d5entarsw_2
END

IF ~~ BEGIN d5beltsw_5
  SAY @28028
  IF ~~ EXTERN LIIA d5liiasw_1
END

IF ~~ BEGIN d5beltsw_6
  SAY @28030
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",4)~ EXIT
END

IF ~~ BEGIN d5beltsw_7
  SAY @28037
  IF ~~ EXTERN D5CHRNAM d5chrnam_1
END

IF ~~ BEGIN d5beltsw_8
  SAY @28040
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",8)~ EXIT
END

IF ~~ BEGIN d5beltsw_9
  SAY @38011
  IF ~~ EXTERN BDENTAR d5entarsw_3
END

END

APPEND BDENTAR

IF ~~ BEGIN d5entarsw_0
  SAY @28009
  IF ~Global("D5AltEndSoD","GLOBAL",0)~ EXTERN D5GREGS d5gregsw_3
  IF ~Global("D5AltEndSoD","GLOBAL",1)~ EXTERN D5GREGS d5gregsw_5
END

IF ~~ BEGIN d5entarsw_1
  SAY @28012
  IF ~~ EXTERN LIIA d5liiasw_0
END

IF ~~ BEGIN d5entarsw_2
  SAY @28025
  IF ~~ REPLY @28026 EXTERN D5CHRNAM d5chrnam_0
END

IF ~~ BEGIN d5entarsw_3
  SAY @38012
  IF ~~ EXTERN LIIA d5liiasw_0
END

END

APPEND LIIA

IF ~~ BEGIN d5liiasw_0
  SAY @28013
  IF ~~ EXTERN BELT d5beltsw_2
END

IF ~~ BEGIN d5liiasw_1
  SAY @28029
  IF ~~ EXTERN BELT d5beltsw_6
END

IF WEIGHT #-1 ~AreaCheck("D50104") Global("D5CloneCut","GLOBAL",5)~ BEGIN d5liiasw_2
  SAY @28031
  IF ~Global("D5heritageRevealed","GLOBAL",0)~ REPLY @28032 GOTO d5liiasw_3
  IF ~Global("D5heritageRevealed","GLOBAL",1)~ REPLY @28033 GOTO d5liiasw_3
END

IF ~~ BEGIN d5liiasw_3
  SAY @28034
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",6)~ EXIT
END

IF WEIGHT #-1 ~AreaCheck("D50104") Global("D5CloneCut","GLOBAL",7)~ BEGIN d5liiasw_4
  SAY @28036
  IF ~~ EXTERN BELT d5beltsw_7
END

END

APPEND D5GREGS

IF ~Global("D5GregsonCut","GLOBAL",1)~ BEGIN d5gregsw_0
  SAY @28005
  IF ~~ GOTO d5gregsw_1
END

IF ~~ BEGIN d5gregsw_1
  SAY @28006
  IF ~~ EXTERN BELT d5beltsw_0
END

IF ~~ BEGIN d5gregsw_2
  SAY @28008
  IF ~~ EXTERN BDENTAR d5entarsw_0
END

IF ~~ BEGIN d5gregsw_3
  SAY @28010
  IF ~~ EXTERN BELT d5beltsw_1
END

IF ~~ BEGIN d5gregsw_4
  SAY @38008
  IF ~~ EXTERN BDENTAR d5entarsw_0
END

IF ~~ BEGIN d5gregsw_5
  SAY @38010
  IF ~~ EXTERN BELT d5beltsw_9
END

END

BEGIN ~D5CHRNAM~

IF ~~ BEGIN d5chrnam_0
  SAY @28027
  IF ~~ EXTERN BELT d5beltsw_5
END

IF ~~ BEGIN d5chrnam_1
  SAY @28038
  IF ~~ REPLY @28039 EXTERN BELT d5beltsw_8
END

BEGIN ~D5BDIMO~

IF ~Global("D5CloneCut","GLOBAL",11)~ BEGIN d5imoensw_0
  SAY @28050
  IF ~Alignment(Player1,MASK_GOOD)~ REPLY @28051 GOTO d5imoensw_1
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ REPLY @28051 GOTO d5imoensw_1
  IF ~Alignment(Player1,MASK_EVIL)~ REPLY @28051 GOTO d5imoensw_2
END

IF ~~ BEGIN d5imoensw_1
  SAY @28052
  IF ~~ REPLY @28054 GOTO d5imoensw_3
END

IF ~~ BEGIN d5imoensw_2
  SAY @28053
  IF ~~ REPLY @28055 GOTO d5imoensw_3
END

IF ~~ BEGIN d5imoensw_3
  SAY @28056
  IF ~Global("D5AltEndSoD","GLOBAL",0)~ REPLY @28057 GOTO d5imoensw_4
  IF ~Global("D5AltEndSoD","GLOBAL",1)~ REPLY @28057 GOTO d5imoensw_4
END

IF ~~ BEGIN d5imoensw_4
  SAY @28058
  IF ~~ REPLY @28059 GOTO d5imoensw_6
END

IF ~~ BEGIN d5imoensw_5
  SAY @38058
  IF ~~ REPLY @38059 GOTO d5imoensw_6
END

IF ~~ BEGIN d5imoensw_6
  SAY @28060
  IF ~~ GOTO d5imoensw_7
END

IF ~~ BEGIN d5imoensw_7
  SAY @28061
  IF ~~ REPLY @28062 GOTO d5imoensw_8
END

IF ~~ BEGIN d5imoensw_8
  SAY @28063
  IF ~~ DO ~OpenDoor("Door04") SetGlobal("D5CloneCut","GLOBAL",12)~ EXIT
END

IF ~Global("D5CloneCut","GLOBAL",12) AreaCheck("d50104")~ BEGIN d5imoensw_9
  SAY @28064
  IF ~~ EXIT
END

IF ~GlobalLT("bd_plot","global",625) Global("D5CloneCut","GLOBAL",12) AreaCheck("d50105")~ BEGIN d5imoensw_10
  SAY @28065
  IF ~~ EXIT
END

IF ~GlobalLT("bd_plot","global",641) Global("D5CloneCut","GLOBAL",12) AreaCheck("d56000")~ BEGIN d5imoensw_11
  SAY @28066
  IF ~~ DO ~SetGlobal("D5ImoenCaveCut","GLOBAL",1)~ EXIT
END

/* do this a different way
ALTER_TRANS BDIMOEN // file name
  BEGIN 92 END // state number (can be more than one)
  BEGIN 3 END // transition number (can be more than one)
  BEGIN "TRIGGER" ~Global("D5SkipSoD","GLOBAL",0)~ END

ALTER_TRANS BDIMOEN // file name
  BEGIN 100 END // state number (can be more than one)
  BEGIN 0 1 2 3 4 END // transition number (can be more than one)
  BEGIN "TRIGGER" ~Global("D5SkipSoD","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ END
*/

ADD_TRANS_TRIGGER BDIMOEN 92 ~Global("D5SkipSoD","GLOBAL",0)~ DO 3

ADD_TRANS_TRIGGER BDIMOEN 100 ~Global("D5SkipSoD","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ DO 0 1 2 3 4

>>>>>>>>
COPY ~d5/switch+8.d~ ~weidu_external/%MOD_FOLDER%/compile/switch+8.d~


// compatibility with EBG1 

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~0~) BEGIN

 OUTER_SET liia_ebg1_10 = STATE_WHICH_SAYS 10 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~LIIA~

 PRINT ~the state we want NOW is # %liia_ebg1_10%~

 APPEND_OUTER ~weidu_external/%MOD_FOLDER%/compile/switch+8.d~ ~~~~~
 ADD_STATE_TRIGGER LIIA %liia_ebg1_10% ~Global("D5GregsonExist","GLOBAL",0)~
~~~~~
 
END	//	end EBG1 #0


ACTION_IF !(MOD_IS_INSTALLED ~c#anotherfinehell.tp2~ ~0~) BEGIN

 APPEND_OUTER ~weidu_external/%MOD_FOLDER%/compile/switch+8.d~ ~~~~~
ALTER_TRANS BDIMOEN 	// file name
  BEGIN 89 END 			// state number (can be more than one)
  BEGIN 0 END 		// transition number (can be more than one)
  BEGIN "TRIGGER" ~Global("D5SkipSoD","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ END
~~~~~
 
END	//	end no AFH #0

// compatibility with AFH 

ACTION_IF (MOD_IS_INSTALLED ~c#anotherfinehell.tp2~ ~0~) BEGIN

 APPEND_OUTER ~weidu_external/%MOD_FOLDER%/compile/switch+8.d~ ~~~~~
ALTER_TRANS BDIMOEN 	// file name
  BEGIN 89 END 			// state number (can be more than one)
  BEGIN 0 5 END 	// transition number (can be more than one)
  BEGIN "TRIGGER" ~Global("D5SkipSoD","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ END
~~~~~
 
END	//	end AFH #0


//  now compile the dialogues

COMPILE ~weidu_external/%MOD_FOLDER%/compile/switch+8.d~


// clean up people's dialogue at ambush

ACTION_IF (GAME_IS ~bgee~) BEGIN
  STRING_SET 39703 @28070
  STRING_SET 39716 @28071
  STRING_SET 39706 @28072
END
ACTION_IF (GAME_IS ~eet~) BEGIN
  STRING_SET 239703 @28070
  STRING_SET 239716 @28071
  STRING_SET 239706 @28072
END


// make sure charname is not overly fatigued in Chateau Irenicus

ACTION_IF (GAME_IS ~eet~) BEGIN
 <<<<<<<< d5/d5nu602.baf
 IF
	CheckStatGT(Player1,30,0)
	Global("D5P1RestFix","GLOBAL",0)
 THEN
	RESPONSE #100
		SetGlobal("D5P1RestFix","GLOBAL",1)
		ActionOverride(Player1,Rest())
		Continue()
 END
 >>>>>>>>
 COPY ~d5/d5nu602.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5nu602.baf~

 ACTION_IF (FILE_EXISTS_IN_GAME ~ar0602.bcs~) BEGIN
  EXTEND_TOP ~ar0602.bcs~ ~weidu_external/%MOD_FOLDER%/compile/d5nu602.baf~
 END
END

//___________________________________________________________________________________
//

END	//	end define function