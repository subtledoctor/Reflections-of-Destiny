
DEFINE_ACTION_FUNCTION campaign_election BEGIN

/*

TO DO: 

- belt & liia are not happy that you confronted sarevok, and that you don't have his body
 -- doesn't help your credibility, at all

- entar is there and demands you address his accusation

- also, dialogue about sarevok, how the danger of alaundo's prophecy is now real - a chance for charname to come clean to the dukes about this

- they give you a choice: SoD, or investigate Entar's murder?

- if choose SoD, need to arrange the transition to that campaign

- if player chooses SoD and EBG1 'short public hero tribute' (LABEL EBG1_HeroCutscene), then play that cut scene
 -- ...if that component is installed, add a variable via AR0108.bcs
 -- ...if that variable = 1, have a transition lead to a state from Belt with this trigger, then after cut scene back into dialogue 
 -- ...if variable = 0, have a transition jump to a Belt state that picks up after the tribute scene

- copy c#st2so2.baf (the transition to SoD upstairs) to a d5 version; play this cut scene if player chooses SoD
 -- ...transition after last state from belt:
    SetInterrupt(FALSE)
    ClearAllActions()
    StartCutSceneMode()
    StartCutSceneEx("d5st2so2",TRUE)
    SetInterrupt(TRUE)

- if choose to stay, then cut scene with capt. gregson bursts in and says Charname is in custody! but, wait, how are you here?
 -- ...then jump to the FF cells, the SoD map. confront the clone

// ***** connect Belt dlg to clone cut scene

*/

//___________________________________________________________________________________
//

// prep journal entries

ADD_JOURNAL TITLE (@3036) @7001 @7050

<<<<<<<< .../c#st2sod.baf
IF
	True()
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        CutSceneId(Player1)
		FadeToColor([30.0],0)
		SaveGame(2)
		SmallWait(1)
		StartCutSceneEx("BDSODTRN",TRUE)
		SetInterrupt(TRUE)
END
>>>>>>>>
COPY ~.../c#st2sod.baf~ ~weidu_external/%MOD_FOLDER%/compile/c#st2sod.baf~
COMPILE EVALUATE_BUFFER ~weidu_external/%MOD_FOLDER%/compile/c#st2sod.baf~

// SoD + EET: patch bdsodtrn.bcs so the BG1 Endmovie doesn't show twice 
COPY_EXISTING ~bdsodtrn.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(StartMovie("ENDMOVIE")[%newline%]*FadeToColor(\[1\.0\],0)\)~ // maybe also SODCIN01
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(StartMovie("SODCIN01")[%newline%]*FadeToColor(\[1\.0\],0)\)~ // maybe also SODCIN01
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


// cutscene for transition to SoD, including handling of Imoen's stuff. 
<<<<<<<< .../d5st2so2.baf
/* move the group to some area so Imoen's stuff can be handled. I'll take the PC's room for now. (There is an Imoen in bd0103 already so we can't do it there.) */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L1%")
THEN
	RESPONSE #100
        CutSceneId(Player1)
		FadeToColor([30.0],0)
		SaveGame(2)
		SmallWait(1)
		ActionOverride(Player2,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player3,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player4,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player5,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		ActionOverride(Player6,LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],S))
		LeaveAreaLUAPanic("%NBaldursGate_DucalPalace_L3%","",[315.1390],SW)
		LeaveAreaLUA("%NBaldursGate_DucalPalace_L3%","",[315.1390],SW)
		MultiPlayerSync()
		Wait(1)		
		StartCutSceneEx("d5st2so2",TRUE)
END

/* if in group, let her leave the group */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InPartyAllowDead("%IMOEN_DV%")
	InMyArea("%IMOEN_DV%")	
	Global("C#EBG1_MoveImoenStuff","MYAREA",0)
THEN
	RESPONSE #100
        CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",1)
		ActionOverride("%IMOEN_DV%",LeaveParty())
		SmallWait(10)
		StartCutSceneEx("d5st2so2",TRUE)
END

/* for all cases: move her stuff into the chest and let her disappear */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InMyArea("%IMOEN_DV%")
	GlobalLT("C#EBG1_MoveImoenStuff","MYAREA",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",2)
		ActionOverride("Container 4",TakeCreatureItems("%IMOEN_DV%",ALL)) //BACKPACK
		Wait(1)
		StartCutSceneEx("d5st2so2",TRUE)
END

/* for all cases: let Imoen disappear */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")
	InMyArea("%IMOEN_DV%")
	Global("C#EBG1_MoveImoenStuff","MYAREA",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
        CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",3)
		ActionOverride("%IMOEN_DV%",DestroySelf())
		SmallWait(10)
		StartCutSceneEx("d5st2so2",TRUE)
END

/* after Imoen is done, start transition to SoD */
IF
	AreaCheck("%NBaldursGate_DucalPalace_L3%")	
	OR(2)
		Global("C#EBG1_MoveImoenStuff","MYAREA",0)
		Global("C#EBG1_MoveImoenStuff","MYAREA",3)
THEN
	RESPONSE #100
        CutSceneId(Player1)
		SetGlobal("C#EBG1_MoveImoenStuff","MYAREA",4)
		SetGlobal("bd_npc_camp_chapter","global",0)
		SetGlobal("chapter","global",7)
		SetGlobal("DREAM","GLOBAL",7)
		SetGlobal("sprite_is_deadsafana","GLOBAL",0)
		SetGlobal("sprite_is_deaddynaheir","GLOBAL",0)
		SetGlobal("sprite_is_deadminsc","GLOBAL",0)
		SetGlobal("sprite_is_deadrasaad","GLOBAL",0)
		TakeObjectGoldGlobal("BD_TAKEN_GOLD","GLOBAL",Player1)
		SetGlobal("BD_EXTRA_GOLD","GLOBAL",17)
		SetGlobal("BD_SAFEHOUSE_DONE","GLOBAL",1)
		ChangeAIScript("bdplayer",OVERRIDE)
		SmallWait(2)
		StartCutSceneEx("BDSODTRN",TRUE)
		SetGlobal("d5st2so2","GLOBAL",2)
END
>>>>>>>>
COPY ~.../d5st2so2.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5st2so2.baf~
COMPILE EVALUATE_BUFFER ~weidu_external/%MOD_FOLDER%/compile/d5st2so2.baf~ 

/* this is commented out of EBG1... not sure why

/* make sure Imoen talks with the correct dialogue after joining the group at the end of SoD */
COPY_EXISTING ~bdcut65.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("IMOEN2?",\)StartDialogNoSet(Player1))~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 StartDialogOverride("bdimoen",Player1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

*/


// add entar to the palace

<<<<<<<< d5/ar108+7.baf
IF
	Dead("Sarevok")
	!InMyArea("BDENTAR")
THEN
	RESPONSE #100
		CreateCreature("BDENTAR",[805.360],SW)
		Continue()
END
>>>>>>>>
COPY ~d5/ar108+7.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar108+7.baf~
EXTEND_TOP ~%bg1_area_prefix%0108.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar108+7.baf~


// dialogues in palace

<<<<<<<< d5/transit+.d
APPEND BELT

IF ~Global("D5EntarCut","GLOBAL",7) Dead("SAREVOK") Global("D5CloneCut","GLOBAL",0) Global("C#st_HeroCutScene","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ BEGIN d5beltsd_0
  SAY @7002
  IF ~~ REPLY @7003 GOTO d5beltsd_1
  IF ~~ REPLY @7004 GOTO d5beltsd_1
END

IF ~~ BEGIN d5beltsd_1
  SAY @7005
  IF ~~ REPLY @7006 GOTO d5beltsd_2
  IF ~~ REPLY @7007 GOTO d5beltsd_2
END

IF ~~ BEGIN d5beltsd_2
  SAY @7008
  IF ~~ EXTERN LIIA d5liiasd_1
END

IF ~~ BEGIN d5beltsd_3
  SAY @7017
  IF ~~ GOTO d5beltsd_5
END

IF ~~ BEGIN d5beltsd_4
  SAY @7018
  IF ~~ GOTO d5beltsd_5
END

IF ~~ BEGIN d5beltsd_5
  SAY @7019
  IF ~~ EXTERN LIIA d5liiasd_3
END

IF ~~ BEGIN d5beltsd_6
  SAY @7033
  IF ~~ GOTO d5beltsd_7
END

IF ~~ BEGIN d5beltsd_7
  SAY @7034
  IF ~Alignment(Player1,MASK_GOOD)~ REPLY @7024 GOTO d5beltsd_8
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ REPLY @7024 GOTO d5beltsd_8
  IF ~Alignment(Player1,MASK_EVIL)~ REPLY @7025 GOTO d5beltsd_9
  IF ~~ REPLY @7026 GOTO d5beltsd_10
END

IF ~~ BEGIN d5beltsd_8 // chose SoD !evil
  SAY @7027
  IF ~~ REPLY @7040 DO ~SetGlobal("ENDOFBG1","GLOBAL",1)~ GOTO d5beltsd_12
  IF ~~ REPLY @7041 GOTO d5beltsd_11
END

IF ~~ BEGIN d5beltsd_9 // chose SoD evil
  SAY @7027
  IF ~~ REPLY @7040 DO ~SetGlobal("ENDOFBG1","GLOBAL",1)~ GOTO d5beltsd_13
  IF ~~ REPLY @7041 GOTO d5beltsd_11
END

IF ~~ BEGIN d5beltsd_10 // chose clone
  SAY @7028
  IF ~~ REPLY @7040 DO ~SetGlobal("ENDOFBG1","GLOBAL",1)~ GOTO d5beltsd_18 // go to clone cut scene
  IF ~~ REPLY @7041 GOTO d5beltsd_11
END

IF ~~ BEGIN d5beltsd_11
  SAY @7039
  IF ~Alignment(Player1,MASK_GOOD)~ REPLY @7024 GOTO d5beltsd_8
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ REPLY @7024 GOTO d5beltsd_8
  IF ~Alignment(Player1,MASK_EVIL)~ REPLY @7025 GOTO d5beltsd_9
  IF ~~ REPLY @7026 GOTO d5beltsd_10
END

IF ~~ BEGIN d5beltsd_12 // chose SoD !evil
  SAY @7035
  IF ~~ GOTO d5beltsd_14
END

IF ~~ BEGIN d5beltsd_13 // chose SoD evil
  SAY @7036
  IF ~~ GOTO d5beltsd_14
END

IF ~~ BEGIN d5beltsd_14
  SAY @7037
  IF ~Global("C#st_HeroCutScene","GLOBAL",0) Global("D5HeroSceneInstalled","GLOBAL",1)~ GOTO d5beltsd_15 // hero tribute diversion
  IF ~Global("D5HeroSceneInstalled","GLOBAL",0)~ GOTO d5beltsd_16
  IF ~Global("C#st_HeroCutScene","GLOBAL",1)~ GOTO d5beltsd_16
END

IF ~~ BEGIN d5beltsd_15
  SAY @7042
  IF ~~ DO ~SetGlobal("C#st_HeroCutScene","GLOBAL",1)
	ClearAllActions()
	StartCutSceneMode()
	StartCutSceneEx("c#stcut1",FALSE)~ EXIT // ... out to tribute cut scene
END

IF ~~ BEGIN d5beltsd_16
  SAY @7043
  IF ~~ DO ~AddJournalEntry(@7001,QUEST_DONE)
	SetInterrupt(FALSE)
	ClearAllActions()
	StartCutSceneMode()
	StartCutSceneEx("d5st2so2",TRUE)
	SetInterrupt(TRUE)~ EXIT 
END

IF ~Global("C#st_HeroCutScene","GLOBAL",1) Global("D5AltEndSoD","GLOBAL",0)~ BEGIN d5beltsd_17
  SAY @7043
  IF ~~ DO ~AddJournalEntry(@7001,QUEST_DONE)
	SetInterrupt(FALSE)
	ClearAllActions()
	StartCutSceneMode()
	StartCutSceneEx("d5st2so2",TRUE)
	SetInterrupt(TRUE)~ EXIT // ... back from hero cut scene, out to start SoD
END

IF ~~ BEGIN d5beltsd_18
  SAY @7038
  IF ~~ DO ~SetGlobal("D5GregsonPalace","GLOBAL",1) 
    SetGlobal("D5SkipSoD","GLOBAL",1) 
    AddJournalEntry(@7050,QUEST_DONE)~ EXIT // ...clone cut scene
END

END

APPEND LIIA

IF ~Global("D5EntarCut","GLOBAL",7) Dead("SAREVOK") Global("D5CloneCut","GLOBAL",0) Global("C#st_HeroCutScene","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ BEGIN d5beltsd_0
  SAY @7002
  IF ~~ REPLY @7003 EXTERN BELT d5beltsd_1
  IF ~~ REPLY @7004 EXTERN BELT d5beltsd_1
END

IF ~~ BEGIN d5liiasd_1
  SAY @7009
  IF ~~ REPLY @7010 EXTERN BDENTAR d5entarsd_1
  IF ~~ REPLY @7011 EXTERN BDENTAR d5entarsd_1
END

IF ~~ BEGIN d5liiasd_2
  SAY @7014
  IF ~~ EXTERN BDENTAR d5entarsd_2
END

IF ~~ BEGIN d5liiasd_3
  SAY @7020
  IF ~~ GOTO d5liiasd_4
END

IF ~~ BEGIN d5liiasd_4
  SAY @7021
  IF ~~ GOTO d5liiasd_5
END

IF ~~ BEGIN d5liiasd_5
  SAY @7022
  IF ~Alignment(Player1,MASK_GOOD)~ REPLY @7024 EXTERN BELT d5beltsd_8 	// go to SoD
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ REPLY @7024 EXTERN BELT d5beltsd_8 	// go to SoD
  IF ~Alignment(Player1,MASK_EVIL)~ REPLY @7025 EXTERN BELT d5beltsd_9 	// go to SoD
  IF ~~ REPLY @7026 EXTERN BELT d5beltsd_10 								// go to clone cut scene
  IF ~~ REPLY @7023 DO ~SetGlobal("D5heritageRevealed","GLOBAL",1)~ EXTERN BDENTAR d5entarsd_4 							// disclose your heritage to dukes
END

IF ~~ BEGIN d5liiasd_6
  SAY @7031
  IF ~~ EXTERN BELT d5beltsd_6
END

IF ~~ BEGIN d5liiasd_7
  SAY @7032
  IF ~~ EXTERN BELT d5beltsd_6
END

END

APPEND BDENTAR

IF ~Global("D5EntarCut","GLOBAL",7) Dead("SAREVOK") Global("C#st_HeroCutScene","GLOBAL",0) Global("D5AltEndSoD","GLOBAL",0)~ BEGIN d5entarsd_0
  SAY @7002
  IF ~~ REPLY @7003 EXTERN BELT d5beltsd_1
  IF ~~ REPLY @7004 EXTERN BELT d5beltsd_1
END

IF ~~ BEGIN d5entarsd_1
  SAY @7012
  IF ~!IsValidForPartyDialogue("SKIE")~ EXTERN LIIA d5liiasd_2
  IF ~IsValidForPartyDialogue("SKIE")~ EXTERN SKIE d5skiesd_0
END

IF ~~ BEGIN d5entarsd_2
  SAY @7015
  IF ~~ GOTO d5entarsd_3
END

IF ~~ BEGIN d5entarsd_3
  SAY @7016
  IF ~!Dead("PORIOS")~ EXTERN BELT d5beltsd_3
  IF ~Dead("PORIOS")~ EXTERN BELT d5beltsd_4
END

IF ~~ BEGIN d5entarsd_4
  SAY @7030
  IF ~!IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_GOOD)~ EXTERN LIIA d5liiasd_6
  IF ~!IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_GENEUTRAL)~ EXTERN LIIA d5liiasd_6
  IF ~!IsValidForPartyDialogue("SKIE") Alignment(Player1,MASK_EVIL)~ EXTERN LIIA d5liiasd_7
  IF ~IsValidForPartyDialogue("SKIE")~ EXTERN SKIE d5skiesd_1
END

END

APPEND SKIE

IF ~~ BEGIN d5skiesd_0
  SAY @7013
  IF ~~ EXTERN LIIA d5liiasd_2
END

IF ~~ BEGIN d5skiesd_1
  SAY @7029
  IF ~Alignment(Player1,MASK_GOOD)~ EXTERN LIIA d5liiasd_6
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ EXTERN LIIA d5liiasd_6
  IF ~Alignment(Player1,MASK_EVIL)~ EXTERN LIIA d5liiasd_7
END

END

>>>>>>>> 
COPY ~d5/transit+.d~ ~weidu_external/%MOD_FOLDER%/compile/transit+.d~


// cross-compatibility with component "Sarevok's Sword": we add script to remove Sarevok's Sword to BG1 area, too 

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~4~) BEGIN

<<<<<<<< .../ar0110_remove_Sarevoks_sword.baf
/* Sarevok's Sword will be stolen after PC returns from Korlasz' Dungeon */
IF
  GlobalGT("bd_plot","GLOBAL",1)
  Global("C#st_RemoveSwordPCChest","MYAREA",0)
  HasItem("c#stsrvs","Container1")
THEN
  RESPONSE #100
    ActionOverride("Container1",DestroyItem("c#stsrvs"))
    SetGlobal("C#st_RemoveSwordPCChest","MYAREA",1)
END
>>>>>>>>
COPY ~.../ar0110_remove_Sarevoks_sword.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar0110_remove_Sarevoks_sword.baf~
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L3%.bcs~ ~weidu_external/%MOD_FOLDER%/compile/ar0110_remove_Sarevoks_sword.baf~ EVALUATE_BUFFER 

END	//	end EBG1 #4


// cross-compatibility with component "Short Hero Tribute Scene": set a variable so the dialogue can trigger the cut scene 

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~2~) BEGIN

 <<<<<<<< d5/hero108.baf
 IF
	Global("D5HeroSceneInstalled","GLOBAL",0)
 THEN
	RESPONSE #100
		SetGlobal("D5HeroSceneInstalled","GLOBAL",1)
		Continue()
 END
 >>>>>>>>
 COPY ~d5/hero108.baf~ ~weidu_external/%MOD_FOLDER%/compile/hero108.baf~
 EXTEND_TOP ~%bg1_area_prefix%0108.BCS~ ~weidu_external/%MOD_FOLDER%/compile/hero108.baf~

 OUTER_SET belt_ebg1_0 = STATE_WHICH_SAYS 0 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
 OUTER_SET belt_ebg1_2 = STATE_WHICH_SAYS 2 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~
 OUTER_SET belt_ebg1_113 = STATE_WHICH_SAYS 113 IN ~c#endlessbg1/translations/%s/dialogues.tra~ FROM ~BELT~

 PRINT ~the state we want is # %belt_ebg1_0%~
 PRINT ~oh and also # %belt_ebg1_0%~
 PRINT ~and there is also # %belt_ebg1_113%~

 APPEND_OUTER ~weidu_external/%MOD_FOLDER%/compile/transit+.d~ ~~~~~
 ADD_STATE_TRIGGER BELT %belt_ebg1_0% ~Global("D5GregsonExist","GLOBAL",0)~

 ADD_STATE_TRIGGER BELT %belt_ebg1_2% ~Global("D5GregsonExist","GLOBAL",0)~
  
 ALTER_TRANS BELT // file name
  BEGIN %belt_ebg1_113% END // state number (can be more than one)
  BEGIN 0 END // transition number (can be more than one)
  BEGIN "EPILOGUE" ~GOTO d5beltsd_16~ END
~~~~~
 
END	//	end EBG1 #2


// compile the dialogues
COMPILE ~weidu_external/%MOD_FOLDER%/compile/transit+.d~


// fix some lines

ACTION_IF (GAME_IS ~bgee~) BEGIN
  STRING_SET 65221 @7045
  STRING_SET 70019 @7045
END
ACTION_IF (GAME_IS ~eet~) BEGIN
  STRING_SET 265221 @7045
  STRING_SET 270019 @7045
END


// cleanup stuff...?

<<<<<<<< d5/bd120+7.baf
IF
	Global("D5KorlaszDone","GLOBAL",1)
	Dead("Sarevok")  // Sarevok
THEN
	RESPONSE #100
		SetGlobal("bd_Imoen_farewell","bd0120",2)
		TakeObjectGoldGlobal("BD_TAKEN_GOLD","GLOBAL",Player1)
		SetGlobal("BD_EXTRA_GOLD","GLOBAL",17)
		SetGlobal("BD_SAFEHOUSE_DONE","GLOBAL",1)
		SmallWait(5)
		ActionOverride(Player2,LeaveAreaLUA("bd0103","",[131.485],E))
		ActionOverride(Player3,LeaveAreaLUA("bd0103","",[152.510],NE))
		ActionOverride(Player4,LeaveAreaLUA("bd0103","",[179.544],NE))
		ActionOverride(Player5,LeaveAreaLUA("bd0103","",[202.567],NE))
		ActionOverride(Player6,LeaveAreaLUA("bd0103","",[239.581],N))
		ActionOverride(Player1,LeaveAreaLUAPanic("bd0103","",[215.396],SE))
		ActionOverride(Player1,LeaveAreaLUA("bd0103","",[215.396],SE))
END
>>>>>>>>
COPY ~d5/bd120+7.baf~ ~weidu_external/%MOD_FOLDER%/compile/bd120+7.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~bd0120.bcs~) BEGIN
  EXTEND_TOP ~bd0120.bcs~ ~weidu_external/%MOD_FOLDER%/compile/bd120+7.baf~
END

//___________________________________________________________________________________
//

END	//	end define function