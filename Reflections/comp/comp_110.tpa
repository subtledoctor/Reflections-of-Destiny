
DEFINE_ACTION_FUNCTION mirror_shards BEGIN

//___________________________________________________________________________________
//

// make mirror shards

ACTION_IF !(FILE_EXISTS_IN_GAME ~D5SIMUL9.SPL~) BEGIN
//...add statchecks
//
  APPEND ~splprot.2da~ ~D5_109_BIT=n%TAB%109%TAB%-1%TAB%8~ UNLESS ~D5_109_BIT=n~

  COPY_EXISTING ~splprot.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 0 ~stat~
      PATCH_IF (~%stat%~ STRING_EQUAL_CASE ~D5_109_BIT=n~) BEGIN
        SET 109_bit_equal = %row%
      END
    END
  BUT_ONLY

  COPY_EXISTING ~spin101.spl~ ~override/d5si8049.spl~
    SAY NAME1 ~~
    SAY UNIDENTIFIED_DESC ~~
    LPF ALTER_SPELL_HEADER INT_VAR target = 1 END
    LPF DELETE_EFFECT END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 target = 2 opcode = 233 parameter1 = (8 << 8) parameter2 = (109 + (0x10000 * 1)) timing = 9 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 236 target = 2 parameter2 = 3 timing = 0 duration = 60 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 parameter2 = 3 timing = 1 duration = 0 STR_VAR resource = ~SPPROIMG~ END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 4 duration = 2 STR_VAR resource = ~d5si8049~ END
  BUT_ONLY

  COPY_EXISTING ~simulacr.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR target = 2 opcode = 326 parameter1 = (8 << 8) parameter2 = %109_bit_equal% timing = 1 STR_VAR resource = ~d5simul9~ END
  IF_EXISTS BUT_ONLY

  COPY_EXISTING ~spin101.spl~ ~override/d5simul9.spl~
    SAY NAME1 ~~
    SAY UNIDENTIFIED_DESC ~~
    LPF DELETE_EFFECT END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 337 target = 2 parameter1 = 241 parameter2 = 101 timing = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 72 target = 2 parameter1 = 4 parameter2 = 0 timing = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 241 target = 2 parameter1 = 4 parameter2 = 1002 timing = 0 Duration = 1 END
  BUT_ONLY

END

COPY ~%MOD_FOLDER%/data/mirror_shard/d5mshard.bam~ ~override~

COPY ~%MOD_FOLDER%/data/mirror_shard/d5shard3.itm~ ~override~
	SAY NAME1 @11001
	SAY NAME2 @11001
	SAY UNIDENTIFIED_DESC @11002
	SAY IDENTIFIED_DESC @11002

COPY ~%MOD_FOLDER%/data/mirror_shard/d5shard1.itm~ ~override~
	SAY NAME1 @11001
	SAY NAME2 @11001
	SAY UNIDENTIFIED_DESC @11002
	SAY IDENTIFIED_DESC @11002

ACTION_IF (FILE_EXISTS_IN_GAME ~deathk.cre~) BEGIN
  COPY_EXISTING ~deathk.cre~ ~override~
	ADD_CRE_ITEM ~d5shard3~ #3 #0 #0 ~IDENTIFIED~ ~INV~
END

// place other on the demon knight in chateau irenicus - "cambion" idemon01.cre
// ...and set the scripts and stuff for the shard's activation

ACTION_IF (FILE_EXISTS_IN_GAME ~idemon02.cre~) BEGIN

<<<<<<<< d5/d5irecam.baf
IF
	See("D5MICAMB")
THEN
	RESPONSE #100
		Attack("D5MICAMB")
END
>>>>>>>>
COPY ~d5/d5irecam.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5irecam.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5irecam.baf~

<<<<<<<< d5/d5mircam.baf
IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,FIREBALL_DEATH)
END

IF
	Dead("D5CAMBIO")
THEN
	RESPONSE #100
		DestroySelf()
END

IF
	See("D5CAMBIO") 
THEN
	RESPONSE #100
		Attack("D5CAMBIO")
END
>>>>>>>>
COPY ~d5/d5mircam.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5mircam.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5mircam.baf~

COPY_EXISTING ~idemon02.cre~ ~override/d5idem01.cre~
	WRITE_ASCII 0x248 ~D5MIRCAM~ #8
	WRITE_ASCII 0x280 ~D5MICAMB~ #20

COPY_EXISTING ~idemon02.cre~ ~override~
	WRITE_ASCII 0x248 ~D5IRECAM~ #8
	WRITE_ASCII 0x280 ~D5CAMBIO~ #20
	ADD_CRE_ITEM ~d5shard1~ #1 #0 #0 ~IDENTIFIED~ ~INV~

ACTION_IF (FILE_EXISTS_IN_GAME ~ar0602.bcs~) BEGIN

<<<<<<<< d5/d5ar602+.baf
IF
	Global("DemonWheel","AR0602",2)
THEN
	RESPONSE #100
		Wait(2)
		CreateCreature("D5IDEM01",[2525.1100],9)  // Mirror Fiend
		SetGlobal("DemonWheel","AR0602",3)
		Continue()
END
>>>>>>>>
EXTEND_TOP ~ar0602.bcs~ ~d5/d5ar602+.baf~

END	//	end ar0602

END	//	end if idemon02


// set variables for later
<<<<<<<< d5/ar516+1.baf
IF
	Global("D5PartyHadShard","GLOBAL",0)
	PartyHasItem("D5SHARD3")
THEN
	RESPONSE #100
		SetGlobal("D5PartyHadShard","GLOBAL",1)
		Continue()
END
>>>>>>>>
COPY ~d5/ar516+1.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar516+1.baf~
EXTEND_TOP ~%bg1_area_prefix%0516.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar516+1.baf~

<<<<<<<< d5/ar500+1.baf
IF
	Global("D5PartyHadSoultaker","GLOBAL",0)
	PartyHasItem("DAGG10")
THEN
	RESPONSE #100
		SetGlobal("D5PartyHadSoultaker","GLOBAL",1)
		Continue()
END
>>>>>>>>
COPY ~d5/ar500+1.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar500+1.baf~
EXTEND_TOP ~%bg1_area_prefix%0500.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar500+1.baf~

<<<<<<<< d5/ar1002+1.baf
IF
	Global("D5PartyHadSoultaker","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5PartyHadSoultaker","GLOBAL",2)
		Continue()
END
>>>>>>>>
COPY ~d5/ar1002+1.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar1002+1.baf~
EXTEND_TOP ~%bg1_area_prefix%1002.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar1002+1.baf~


//___________________________________________________________________________________
//

END	//	end define function