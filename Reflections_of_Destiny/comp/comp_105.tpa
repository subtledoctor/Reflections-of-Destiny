
DEFINE_ACTION_FUNCTION keep_playing BEGIN

/*
- plan: keep the game running after Sarevok dies

- make sure door locks behind PC when enter temple (door0123)

- stop game from ending when S is dead
 -- ...play the vid, tho

- unlock door when S is dead

- ***** suppress any FF arriving, if EBG1 is installed

- ***** add dialogue from Sarevok? Something about having tagged you in a way that attuned your soul to the temple of Bhaal... no resurrection for the likes of you and me, no afterlife and no second chances. your death can only fuel my ascension.

*/

//___________________________________________________________________________________
//

// prep journal entries

// ADD_JOURNAL TITLE (@10200) @10501

// stuff from EBG1

ACTION_IF !(MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~0~) BEGIN

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  COPY_EXISTING ~%Undercity_TempleofBhaal%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Dead("Sarevok")[%newline%]*GlobalLT("EndOfBG1","GLOBAL",2)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~False()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
  BUT_ONLY
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  COPY_EXISTING ~%Undercity_TempleofBhaal%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Dead("Sarevok")[%newline%]*Global("EndOfBG1","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~False()~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_PRINT ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
  BUT_ONLY
END


// EXTEND_TOP ar0125.bcs to lock/unlock door

<<<<<<<< d5/ar0125+.baf
IF
	!Dead("SAREVOK")
	Global("D5TempleLock","GLOBAL",0)
THEN
	RESPONSE #100
		TriggerActivation("Door0123",FALSE)
		SetGlobal("D5TempleLock","GLOBAL",1)
		Continue()
END

IF
	Dead("SAREVOK")
	Global("D5TempleLock","GLOBAL",1)
THEN
	RESPONSE #100
		TriggerActivation("Door0123",TRUE)
		SetGlobal("D5TempleLock","GLOBAL",2)
		AddJournalEntry(@10501,QUEST)
		Continue()
END

IF
	Dead("Sarevok")  // Armored Figure (Sarevok)
	Global("C#st_BG1End","GLOBAL",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SaveGame(0)
		SmallWait(1)
		ClearAllActions()
		StartCutSceneMode()
		SetGlobal("C#st_BG1End","GLOBAL",1)
		SetGlobal("SarevokBehavior","GLOBAL",5)
		TriggerActivation("Door0123",TRUE)
		Wait(2)
		FadeToColor([30.0],0)
		Wait(2)
		StartMovie("ENDMOVIE")
		FadeFromColor([30.0],0)
		EndCutSceneMode()
		SetInterrupt(TRUE)
END
>>>>>>>>
COPY ~d5/ar0125+.baf~ ~weidu_external/%MOD_FOLDER%/compile/105/ar0125+.baf~
EXTEND_TOP ~%Undercity_TempleofBhaal%.bcs~ ~weidu_external/%MOD_FOLDER%/compile/105/ar0125+.baf~ EVALUATE_BUFFER

END	//	end if EBG1 not installed


// ***** expand exposition in Sarevok's dialogue - use states 13, 14, and 20

/* option 1: just replace the text of states 13/14/20; good for compatibility...
// but would need to change the transitions so that all the exposition is revealed
REPLACE_SAY SAREVO 13 ~And now comes the mosquito to the spider's den. I underestimated you, <CHARNAME>. You successfully toppled my plans. War was to come to Baldur's Gate - I would have been a tragic hero, as the city fell to conquerors. As the Amnish army poured in, there would have been murder on the grandest scale!~

REPLACE_SAY SAREVO 14 ~You have no idea what this place it. We are directly under the gates to the inner city. The Bhaalist clergy were once lords of this land, using death to fuel their power. The old rituals have been prepared - all of the deaths at the hands of Amnish invaders would have been magically captured, and funneled to a single point of focus - the residual spark of Bhaal that beats in my heart!~ // And here below the center of the city, with Bhaalist rituals prepared, all of that death would have fueled my ascension!~

REPLACE_SAY SAREVO 20 ~Fool! I do not wish to restore Bhaal's power. He is long gone. All that remains is us, and what we do with his legacy. I know you have felt the hunger, the urge not just to end life, but to TAKE it!~ // But all is not lost - the rituals are still active, and now attuned to you! The doors are sealed - this will be your slaughterhouse. I will absorb your divine spark, and with double the power from Bhaal I will be unstoppable.

~But it is far too late for you. The rituals in this place are still active - but now attuned to you! The doors are sealed - this will be your slaughterhouse. As you sputter your last breath I will absorb your divine soul! I will not be a god - not yet - but with twice Bhaal's power I will be unstoppable!~ 
*/

// option 2: completely divert the dialogue to a new state
<<<<<<<< d5/nusarev.d
REPLACE_STATE_TRIGGER SAREVO 13 ~Global("SarevokBehavior","GLOBAL",99)~

APPEND SAREVO

IF WEIGHT #0 ~Global("SarevokBehavior","GLOBAL",2)~ THEN BEGIN d5nusarevo_1
  SAY @10511
  IF ~~ REPLY @10512 GOTO d5nusarevo_2
  IF ~~ REPLY @10513 GOTO d5nusarevo_3
  IF ~~ REPLY @10514 GOTO d5nusarevo_2
END

IF ~~ THEN BEGIN d5nusarevo_2
  SAY @10516
  IF ~~ REPLY @10517 GOTO d5nusarevo_3
  IF ~~ REPLY @10518 GOTO d5nusarevo_3
END
  
IF ~~ THEN BEGIN d5nusarevo_3
  SAY @10521
  IF ~~ REPLY @10522 GOTO d5nusarevo_4
  IF ~~ REPLY @10523 GOTO d5nusarevo_4
END

IF ~~ THEN BEGIN d5nusarevo_4
  SAY @10525
  IF ~~ GOTO d5nusarevo_5
END

IF ~~ THEN BEGIN d5nusarevo_5
  SAY @10526
  IF ~~ REPLY @10527 GOTO d5nusarevo_6
  IF ~~ REPLY @10528 GOTO d5nusarevo_6
END

IF ~~ THEN BEGIN d5nusarevo_6
  SAY @10530
  IF ~~ 
    DO ~SetGlobal("SarevokBehavior","GLOBAL",3)
		TriggerActivation("Door0123",FALSE)
		ActionOverride("Galdor",Enemy())
		ActionOverride("Semaj",Enemy())
		ActionOverride("Tazok",Enemy())
		Enemy() 
	~ EXIT
END

END
>>>>>>>>
COPY ~d5/nusarev.d~ ~weidu_external/%MOD_FOLDER%/compile/105/nusarev.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/105/nusarev.d~


// ***** if SCS not installed, add a thing where anytime someone dies Sarevok gets a stat boost and healed
//		...and maybe if S dies, his lackeys die as well?
// 		...or, make that a separate component?

//___________________________________________________________________________________
//

END	//	end define function

////////////////////
////////////////////
////////////////////
